$JOB           COBOL EDITOR
$EXECUTE       IBJOB
$IBJOB         GO
$IBCBC EDITOR  REF,NODECK,M94,XR7
       IDENTIFICATION DIVISION.
       PROGRAM-ID.
           EDITOR.
       AUTHOR.
           J H FORTNER.
       DATE-WRITTEN.
           07/23/72.

       NOTE.
           THIS VERSION OF EDITOR 1 COMPRISES AN ENTIRE RE-WRITE
           OF THE BASIC EDITOR WITH ONLY ONE CHANGE IN THE COMMAND
           STRUCTURE, THAT BEING THE ADDITION OF A "CHANGE" COMMAND
           TO SERVE IN PLACE OF THE INSERT-DELETE COMBINATION WHICH
           WAS REQUIRED IN PREVIOUS VERSIONS. 
           CHANGE 1.
           MODIFY TO RUN ON IBM-7094.
      
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.
           IBM-7094.
       OBJECT-COMPUTER.
           IBM-7094.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT OLD-VERSION  ASSIGN TO SYSUT1.
           SELECT NEW-VERSION  ASSIGN TO SYSUT2.
           SELECT PRT-VERSION  ASSIGN TO SYSUT2.
           SELECT MODIFICATION ASSIGN TO SYSIN1.
           SELECT COMMENTARY   ASSIGN TO SYSOU1.
      
       DATA DIVISION.

       FILE SECTION.

       FD  OLD-VERSION
           LABEL RECORDS ARE OMITTED
           BLOCK CONTAINS 10 RECORDS
           DATA RECORD IS OLD-RECORD.

       01  OLD-RECORD.
           02 OLD-STATEMENT       PICTURE X(75).
           02 OLD-NUMBER          PICTURE X(5).
           02 FILLER              PICTURE X(4).

       FD  NEW-VERSION
           LABEL RECORDS ARE OMITTED
           BLOCK CONTAINS 10 RECORDS
           DATA RECORD IS NEW-RECORD.

       01  NEW-RECORD.
           02 NEW-STATEMENT       PICTURE X(75).
           02 NEW-NUMBER          PICTURE X(5).
           02 NEW-PAD             PICTURE X(4).

       FD  PRT-VERSION
           LABEL RECORDS ARE OMITTED
           BLOCK CONTAINS 10 RECORDS
           DATA RECORD IS PRT-RECORD.

       01  PRT-RECORD.
           02 PRT-STATEMENT       PICTURE X(75).
           02 PRT-NUMBER          PICTURE X(5).
           02 FILLER              PICTURE X(4).

       FD  MODIFICATION
           LABEL RECORDS ARE OMITTED
           BLOCK CONTAINS 80 CHARACTERS
           DATA RECORD IS UPDATE-ORDER.

       01  UPDATE-ORDER.
           02 INSERTION.
              03 COMMAND          PICTURE X(6).
                 88 ENDJOB        VALUE 'ENDJOB'.
                 88 ENDSET        VALUE 'ENDSET'.
                 88 REMOVE        VALUE 'REMOVE'.
                 88 ADDNEW        VALUE 'INSERT'.
                 88 CHANGE        VALUE 'CHANGE'.
                 88 DISPLY        VALUE 'DISPLY'.
              03 FILLER           PICTURE X.
              03 A-FIELD          PICTURE 9(5).
              03 A-ALPHA REDEFINES A-FIELD    PICTURE X(5).
                 88 A-BLANK       VALUE SPACES.
              03 FILLER           PICTURE X(4).
              03 B-FIELD          PICTURE 9(5).
              03 B-ALPHA REDEFINES B-FIELD    PICTURE X(5).
                 88 B-BLANK       VALUE SPACES.
              03 FILLER           PICTURE X(54).
           02 FILLER              PICTURE X(5).

       FD  COMMENTARY
           LABEL RECORDS ARE OMITTED
           BLOCK CONTAINS 120 CHARACTERS
           DATA RECORD IS COMMENT-LINE.

       01  COMMENT-LINE.
           02 FILLER              PICTURE X(120).

       WORKING-STORAGE SECTION.

       77  COMMAND-ADDITIONS      PICTURE 9(3) COMPUTATIONAL VALUE 0.
       77  COMMAND-SUBTRACTIONS   PICTURE 9(3) COMPUTATIONAL VALUE 0.
       77  TOTAL-INSERTED         PICTURE 9(3) COMPUTATIONAL VALUE 0.
       77  TOTAL-DELETED          PICTURE 9(3) COMPUTATIONAL VALUE 0.
       77  OUTPUT-COUNT           PICTURE 9(5) COMPUTATIONAL VALUE 0.
       77  LINE-COUNT             PICTURE 9(2) COMPUTATIONAL VALUE 0.
       77  FIELDA                 PICTURE 9(5) VALUE 0.
       77  FIELDB                 PICTURE 9(5) VALUE 0.
       77  BLANK-LINE             PICTURE X(82) VALUE SPACES.

       01  DATE-FROM-SYS.
           02 DFSYS OCCURS 3 TIMES PICTURE 99.
       
       01  HOLD-RECORD.
           02 HOLD-STATEMENT      PICTURE X(75) VALUE SPACES.
           02 HOLD-NUMBER         PICTURE 9(5) VALUE 0.

       01  HEADINGS-LINE.
           02 FILLER              PICTURE X(2) VALUE '1 '.
           02 FILLER              PICTURE X(15) VALUE 'EDITOR VERSION'.
           02 FILLER              PICTURE X(60) VALUE '1.1 - 206/72'.
           02 PHASE               PICTURE X(17) VALUE 'UPDATING AS OF'.
           02 MONTH-RUN           PICTURE XX VALUE '07'.
           02 FILLER              PICTURE X VALUE '/'.
           02 DAY-RUN             PICTURE XX VALUE '23'.
           02 FILLER              PICTURE X VALUE '/'.
           02 YEAR-RUN            PICTURE XX VALUE '72'.
           02 FILLER              PICTURE X(8) VALUE '  PAGE '.
           02 PAGE-NUMBER         PICTURE 9(4) VALUE 0.

       01  COMMAND-LISTING.
           02 FILLER              PICTURE X(2) VALUE SPACES.
           02 COMMAND-IMAGE       PICTURE X(80).

       01  ACTIVITIES-LISTING.
           02 FILLER              PICTURE X(2) VALUE SPACES.
           02 DISPOSITION         PICTURE X(2).
           02 ACTIVE-IMAGE        PICTURE X(80).

       01  UPSI-BYTE.
           02 BIT OCCURS 8 TIMES  PICTURE X.

       01  MESSAGE-LOG.
           02 FILLER              PICTURE X(2) VALUE SPACES.
           02 MESSAGE-TEXT        PICTURE X(80).

       01  DISPLAY-MESSAGE.
           02 FILLER              PICTURE X(2) VALUE SPACES.
           02 DISPLAY-TEMP        PICTURE X(6).
           02 FILLER              PICTURE X(2) VALUE SPACES.
           02 DISPLAY-TEXT        PICTURE X(72).
      
       PROCEDURE DIVISION.

       START-SECTION.
           OPEN INPUT OLD-VERSION, MODIFICATION,
                OUTPUT NEW-VERSION, COMMENTARY.
           MOVE 'F' TO BIT (1), BIT (2).
           READ MODIFICATION AT END
              MOVE 'MODIFICATION FILE ENDED' TO MESSAGE-TEXT
              WRITE COMMENT-LINE FROM MESSAGE-LOG
              GO TO FINISH-JOB.
           MOVE COMMAND TO DATE-FROM-SYS.
           MOVE DFSYS (1) TO YEAR-RUN.
           MOVE DFSYS (2) TO MONTH-RUN.
           MOVE DFSYS (3) TO DAY-RUN.
           READ OLD-VERSION AT END
              MOVE 'NO OLD VERSION FOUND' TO MESSAGE-TEXT
              WRITE COMMENT-LINE FROM MESSAGE-LOG
              GO TO END-JOB.
           MOVE OLD-STATEMENT TO NEW-STATEMENT.
           PERFORM OUTPUT-A-RECORD.

       TOP-OF-PAGE-ROUTINE.
           ADD 1 TO PAGE-NUMBER.
           MOVE ZERO TO LINE-COUNT.
           WRITE COMMENT-LINE FROM HEADINGS-LINE.
           WRITE COMMENT-LINE FROM BLANK-LINE.
           WRITE COMMENT-LINE FROM BLANK-LINE.

       READ-A-COMMAND.
           READ MODIFICATION AT END
              MOVE 'MODIFICATION FILE ENDED' TO MESSAGE-TEXT
              WRITE COMMENT-LINE FROM MESSAGE-LOG
              GO TO FINISH-JOB.
           MOVE UPDATE-ORDER TO COMMAND-IMAGE.
           WRITE COMMENT-LINE FROM BLANK-LINE.
           WRITE COMMENT-LINE FROM COMMAND-LISTING.
           ADD 2 TO LINE-COUNT.
           IF A-BLANK MOVE ZEROES TO A-FIELD.
           IF B-BLANK MOVE ZEROES TO B-FIELD.
           MOVE A-FIELD TO FIELDA.
           MOVE B-FIELD TO FIELDB.

       TEST-COMMAND-TYPE.
           IF CHANGE GO TO CHANGE-A-RECORD.
           IF REMOVE GO TO DELETE-A-RECORD.
           IF DISPLY MOVE 'T' TO BIT (2)
              GO TO FINISH-JOB.
           IF ENDJOB GO TO FINISH-JOB.
           IF ADDNEW GO TO INSERT-A-RECORD.
           MOVE 'INVALID COMMAND IGNORED.' TO MESSAGE-TEXT.
           WRITE COMMENT-LINE FROM MESSAGE-LOG.
           GO TO READ-A-COMMAND.

       CHANGE-A-RECORD.
           ALTER RETURN-TO-USER TO PROCEED TO INSERTION-PROCESS.
           ALTER NEXT-JOB-STEP TO PROCEED TO DELETION-PROCESS.
           
       FIND-FIELDA.
           IF OLD-NUMBER IS GREATER THAN FIELDA
              MOVE 'RECORD ALREADY PASSED' TO MESSAGE-TEXT
              WRITE COMMENT-LINE FROM MESSAGE-LOG
              GO TO READ-A-COMMAND.
           READ OLD-VERSION AT END
              MOVE 'NOT FOUND IN OLD VERSION' TO DISPLAY-TEXT
              MOVE FIELDA TO DISPLAY-TEMP
              WRITE COMMENT-LINE FROM DISPLAY-MESSAGE
              GO TO END-JOB.
           IF OLD-NUMBER IS LESS THAN FIELDA
              MOVE OLD-STATEMENT TO NEW-STATEMENT
              PERFORM OUTPUT-A-RECORD
              GO TO FIND-FIELDA.

       RETURN-TO-USER.
           GO TO END-JOB.

       INSERT-A-RECORD.
           ALTER RETURN-TO-USER TO PROCEED TO INSERTION-PROCESS.
           ALTER NEXT-JOB-STEP TO PROCEED TO FORCED-WRITE.
           GO TO FIND-FIELDA.

       INSERTION-PROCESS.
           READ MODIFICATION AT END
              MOVE 'NO ENDSET FOUND' TO MESSAGE-TEXT
              WRITE COMMENT-LINE FROM MESSAGE-LOG
              GO TO END-JOB.
           IF ENDSET
              MOVE COMMAND-ADDITIONS TO DISPLAY-TEMP
              MOVE 'RECORDS INSERTED.' TO DISPLAY-TEXT
              WRITE COMMENT-LINE FROM DISPLAY-MESSAGE
              ADD COMMAND-ADDITIONS TO TOTAL-INSERTED
              MOVE ZEROES TO COMMAND-ADDITIONS
              GO TO NEXT-JOB-STEP.
           MOVE INSERTION TO NEW-STATEMENT, ACTIVE-IMAGE.
           MOVE 'I ' TO DISPOSITION.
           PERFORM OUTPUT-A-RECORD.
           WRITE COMMENT-LINE FROM ACTIVITIES-LISTING.
           ADD 1 TO COMMAND-ADDITIONS.
           ADD 1 TO LINE-COUNT.
           IF LINE-COUNT EQUAL 56 PERFORM TOP-OF-PAGE-ROUTINE.
           GO TO INSERTION-PROCESS.

       NEXT-JOB-STEP.
           GO TO END-JOB.
           
       FORCED-WRITE.
           MOVE OLD-STATEMENT TO NEW-STATEMENT.
           PERFORM OUTPUT-A-RECORD.
           GO TO READ-A-COMMAND.

       DELETE-A-RECORD.
           ALTER RETURN-TO-USER TO PROCEED TO DELETION-PROCESS.
           GO TO FIND-FIELDA.

       DELETION-PROCESS.
           MOVE OLD-RECORD TO ACTIVE-IMAGE.
           MOVE 'D ' TO DISPOSITION.
           WRITE COMMENT-LINE FROM ACTIVITIES-LISTING.
           ADD 1 TO LINE-COUNT.
           IF LINE-COUNT EQUAL 56 PERFORM TOP-OF-PAGE-ROUTINE.
           ADD 1 TO COMMAND-SUBTRACTIONS.
           IF OLD-NUMBER IS NOT LESS THAN FIELDB
              MOVE COMMAND-SUBTRACTIONS TO DISPLAY-TEMP
              MOVE 'RECORDS DELETED.' TO DISPLAY-TEXT 
              WRITE COMMENT-LINE FROM DISPLAY-MESSAGE
              ADD COMMAND-SUBTRACTIONS TO TOTAL-DELETED
              MOVE OLD-STATEMENT TO NEW-STATEMENT
              MOVE ZEROES TO COMMAND-SUBTRACTIONS
              GO TO READ-A-COMMAND.
           READ OLD-VERSION AT END
              MOVE 'NOT FOUND IN OLD VERSION DOING DELETE'
                    TO DISPLAY-TEXT
              WRITE COMMENT-LINE FROM DISPLAY-MESSAGE
              GO TO END-JOB.
           GO TO DELETION-PROCESS.

       OUTPUT-A-RECORD.
           ADD 1 TO OUTPUT-COUNT.
           MOVE OUTPUT-COUNT TO NEW-NUMBER.
           MOVE SPACES TO NEW-PAD.
           WRITE NEW-RECORD.

       FINISH-JOB.
           READ OLD-VERSION AT END GO TO TEST-FOR-LISTING.
           MOVE OLD-STATEMENT TO NEW-STATEMENT.
           GO TO OUTPUT-A-RECORD.

       TEST-FOR-LISTING.
           PERFORM TOP-OF-PAGE-ROUTINE.
           MOVE OLD-NUMBER TO DISPLAY-TEMP.
           MOVE 'RECORDS READ.' TO DISPLAY-TEXT.
           WRITE COMMENT-LINE FROM DISPLAY-MESSAGE.
           MOVE TOTAL-INSERTED TO DISPLAY-TEMP.
           MOVE 'RECORDS ADDED.' TO DISPLAY-TEXT.
           WRITE COMMENT-LINE FROM DISPLAY-MESSAGE.
           MOVE TOTAL-DELETED TO DISPLAY-TEMP.
           MOVE 'RECORDS DROPPED.' TO DISPLAY-TEXT.
           WRITE COMMENT-LINE FROM DISPLAY-MESSAGE.
           MOVE OUTPUT-COUNT TO DISPLAY-TEMP.
           MOVE 'RECORDS IN NEW FILE.' TO DISPLAY-TEXT.
           WRITE COMMENT-LINE FROM DISPLAY-MESSAGE.
           CLOSE NEW-VERSION.
           IF BIT (2) EQUAL 'F' GO TO END-JOB.
           OPEN INPUT PRT-VERSION.
           MOVE 'UPDATED LISTING' TO PHASE.
           MOVE ZEROES TO PAGE-NUMBER.
           PERFORM TOP-OF-PAGE-ROUTINE.
           MOVE SPACES TO DISPOSITION.

       LISTING-LOOP.
           READ PRT-VERSION AT END GO TO END-LISTING.
           MOVE PRT-RECORD TO ACTIVE-IMAGE.
           WRITE COMMENT-LINE FROM ACTIVITIES-LISTING.
           ADD 1 TO LINE-COUNT.
           IF LINE-COUNT EQUAL 56 PERFORM TOP-OF-PAGE-ROUTINE.
           GO TO LISTING-LOOP.

       END-LISTING.
           CLOSE PRT-VERSION.

       END-JOB.
           MOVE 'PROGRAM TERMINATION' TO MESSAGE-TEXT.
           WRITE COMMENT-LINE FROM MESSAGE-LOG.
           CLOSE OLD-VERSION, MODIFICATION, COMMENTARY.
           STOP RUN.

$CBEND
~
120221
DISPLY
ENDJOB
$*CHANGE 00010 TO 00011
$*      TIN=400.0
$*      TOUT=0.0
$*      MAXIT = 100
$*      EPS=50.0
$*      OMEGA=1.84
$*      LENGTH=151
$*ENDSET
$*REMOVE 00113
