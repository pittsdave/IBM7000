          R CARD IMAGE I/O MODULE FOR EDL PACKAGE.
          R
          R    PETER R. BOS     SEPTEMBER 1967.
          R
          R THIS MODULE, TOGETHER WITH THE OTHER COMPONENTS
          R OF THE EDL COMMAND, IS INTENDED TO PROVIDE AN
          R ALTERNATIVE TO THE SYSTEM COMMAND 'ED', GIVING
          R SOMEWHAT GREATER CONVENIENCES, AND ALLOWING
          R INTERMEDIATE FILES TO BE WRITTEN IN TEMPORARY
          R MODE WHEN EXCEEDING RECORD QUOTAS (WHICH ED DOES
          R NOT DO). WHEN USED TOGETHER WITH THE MODIFIED
          R MAIN PROGRAM 'EDITCD', TAB SETTINGS ARE INTERPRETED,
          R AND FILES MAY BE SERIALIZED.
          R
            EXTERNAL FUNCTION (LINE, L, EOF)
            NORMAL MODE IS INTEGER
            BOOLEAN SRINHB
            STATEMENT LABEL EOF
          R INSERT FILE .MOD.
                DEFINE BINARY OPERATOR .MOD., PRECEDENCE SAME AS /
                MODE STRUCTURE 1 .MOD. 1 TO 1
       JMP      *+1,AT,*+3
       JMP      *+11,AC,*+1
       JMP      *+12,LA,*+6
       JMP      *+13,AC,*+1
       JMP      *+2,MQ,*+1
       JMP      *+13,LA,*+2
       STQ      T
       LDQ      A
       PXD
       LLS      0
       DVP      B
       OUT      AC
       XCA
       JMP      *-5
       XCL
       JMP      *-7
       STO      T
       JMP      *-10
       SLW      T
       JMP      *-12
       END
            DIMENSION CARD(14), TB(20), BK(84)
            VECTOR VALUES BK=377777777777K
            VECTOR VALUES NBKS=0
            VECTOR VALUES MXBKS=83
            VECTOR VALUES TB=377777777777K
            VECTOR VALUES MXTBS=20
            VECTOR VALUES NTBS=0
            VECTOR VALUES FNS3=377777777777K
            VECTOR VALUES FNS7=777777777777K
            VECTOR VALUES FSTNO=10
            VECTOR VALUES INCR=10
            VECTOR VALUES NXTNO=10
            VECTOR VALUES ZLNO=$ 00000$
            VECTOR VALUES SRINHB=0B
            VECTOR VALUES BLNK=60K
            VECTOR VALUES BLNKS=$ $
            VECTOR VALUES RETURN=55K
            VECTOR VALUES TAB=72K
            VECTOR VALUES BKSP=35K
          R   (COLON USED FOR BACKSPACE)
            VECTOR VALUES NCOLS=72
            VECTOR VALUES MXCOLS=84
            VECTOR VALUES SRCOL=72
            VECTOR VALUES NWRDS=14
            VECTOR VALUES NCLWD=2
            VECTOR VALUES NCHRS=6
            VECTOR VALUES BTSCHR=6
            VECTOR VALUES CHRMSK=77K
            VECTOR VALUES UNMSK=777777777700K
          R
            ENTRY TO SETBK.
            BKSP=LINE.A.CHRMSK
            FUNCTION RETURN
          R
            ENTRY TO WRITE.
          R ( WRITE.(LINE,LENGTH) )
            B=0
            T=0
            WHENEVER L.L.1, FUNCTION RETURN
            LTH=L-1
            J=1
            THROUGH UNCNV, FOR I=1,1,I.G.LTH
GTTB          THROUGH GTTB, FOR T=0,1,TB(T).G.J
GTBK          THROUGH GTBK, FOR B=0,1,BK(B).GE.J
              CHAR=LINE(I).A.CHRMSK
              WHENEVER CHAR.E.TAB
                WHENEVER TB(T).E.FNS3
                  CHAR=BLNK
                  TRANSFER TO ISCHR
                END OF CONDITIONAL
                THROUGH FILLT, FOR J=J,1,J.E.TB(T)
                  STCHR.(BLNK,J,EOLW)
FILLT           CONTINUE
              OR WHENEVER CHAR.E.BKSP.AND.J.E.BK(B)
                J=J-1
              OTHERWISE
ISCHR           STCHR.(CHAR,J,EOLW)
                J=J+1
              END OF CONDITIONAL
UNCNV       CONTINUE
EOLW        FILLB.
            WHENEVER .NOT.SRINHB, SERIAL.
            WRCARD.
            FUNCTION RETURN
          R
            ENTRY TO READ.
          R ( LENGTH=READ.(LINE,BUFSIZ,EOF) )
            B=0
            T=0
            LTH=L-1
            J=1
            LAST=0
            RDCARD.(EOF)
            THROUGH CNVRT, FOR I=1,1,I.G.LTH
              GTCHR.(CHAR,J,EOLR)
              WHENEVER CHAR.NE.BLNK, LAST=I
              LINE(I)=CHAR
              J=J+1
CNVRT       CONTINUE
EOLR        LINE(LAST+1)=RETURN
            FUNCTION RETURN LAST+1
          R
            ENTRY TO BOTTOM.
RD          RDCARD.(ENDB)
            J=NCOLS+1
            FILLB.
            WHENEVER .NOT. SRINHB, SERIAL.
            WRCARD.
            TRANSFER TO RD
ENDB        FUNCTION RETURN
          R
            ENTRY TO NEWTBS.
            TB=FNS3
            NTBS=0
            FUNCTION RETURN
          R
            ENTRY TO NEWBKS.
            BK=FNS3
            NBKS=0
            FUNCTION RETURN
          R
            ENTRY TO TABSET.
            WHENEVER NTBS.GE.MXTBS, FUNCTION RETURN
            WHENEVER LINE.L.TB(NTBS-1).AND.NTBS.G.0, FUNCTION RETURN
            TB(NTBS)=LINE
            NTBS=NTBS+1
            TB(NTBS)=FNS3
            FUNCTION RETURN
          R
            ENTRY TO BKSET.
            WHENEVER LINE.G.NCOLS, FUNCTION RETURN
            WHENEVER NBKS.GE.MXBKS, FUNCTION RETURN
            WHENEVER LINE.LE.1, FUNCTION RETURN
            WHENEVER LINE.L.BK(NBKS-1).AND.NBKS.G.0, FUNCTION RETURN
            BK(NBKS)=LINE
            NBKS=NBKS+1
            BK(NBKS)=FNS3
            FUNCTION RETURN
          R
            ENTRY TO SETSR.
            NXTNO=LINE
            FSTNO=LINE
            INCR=L
            FUNCTION RETURN
          R
            ENTRY TO SINHB.
            EQUIVALENCE(INHB,SRINHB)
            INHB=LINE
            FUNCTION RETURN
          R
            ENTRY TO SETCOL.
            NCOLS=LINE
            WHENEVER NCOLS.E.0, NCOLS=SRCOL
            WHENEVER NCOLS.NE.SRCOL, SRINHB=1B
            WHENEVER NCOLS.G.MXCOLS, NCOLS=MXCOLS
            FUNCTION RETURN
          R
            ENTRY TO CLOSEW.
            NXTNO = FSTNO
            FILE.
            FUNCTION RETURN
          R
          R
          R ...INTERNAL SUBROUTINES.
          R
            INTERNAL FUNCTION (ENDF)
            STATEMENT LABEL ENDF
            ENTRY TO RDCARD.
            THROUGH RDLP, FOR N=NWRDS,-1,N.L.1
              CARD(N)=RDWORD.(ENDF)
RDLP        CONTINUE
            FUNCTION RETURN
          R
            ENTRY TO WRCARD.
            THROUGH WRLP, FOR N=NWRDS,-1,N.L.1
              WRWORD.(CARD(N))
WRLP        CONTINUE
            FUNCTION RETURN
            END OF FUNCTION
          R
            INTERNAL FUNCTION (ARG1,ARG2,LOC)
            STATEMENT LABEL LOC
            ENTRY TO GTCHR.
            WHENEVER ARG2.G.NCOLS, TRANSFER TO LOC
            M=BTSCHR*(NCHRS-1-(ARG2-1).MOD.NCHRS)
            N=NWRDS-(ARG2-1)/NCHRS
            ARG1=(CARD(N).RS.M).A.CHRMSK
            FUNCTION RETURN
          R
            ENTRY TO STCHR.
            WHENEVER ARG2.G.NCOLS, TRANSFER TO LOC
            M=BTSCHR*(NCHRS-1-(ARG2-1).MOD.NCHRS)
            N=NWRDS-(ARG2-1)/NCHRS
            CARD(N)=(CARD(N).A.UNMSK.LS.M).V.((ARG1.A.CHRMSK).LS.M)
            FUNCTION RETURN
            END OF FUNCTION
          R
            INTERNAL FUNCTION (ARG1,ARG2)
            ENTRY TO SERIAL.
            LNO=DERBC.(NXTNO).V.ZLNO
            CARD(2)=(LNO.RS.(BTSCHR*NCLWD)).V.
          1   (BLNKS.LS.(BTSCHR*(NCHRS-NCLWD)))
            CARD(1)=LNO.LS.(BTSCHR*(NCHRS-NCLWD)).V.
          1   (BLNKS.RS.(BTSCHR*NCLWD))
            NXTNO=NXTNO+INCR
            FUNCTION RETURN
            END OF FUNCTION
          R
            INTERNAL FUNCTION
            ENTRY TO FILLB.
            NC=J-1
            M=BTSCHR*((NC-1).MOD.NCHRS+1)
            N=NWRDS-(NC-1)/NCHRS
            CARD(N)=(CARD(N).A.(FNS7.LS.(BTSCHR*NCHRS-M))).V.
          1     (BLNKS.RS.M)
            THROUGH BLANK, FOR N=N-1,-1,N.L.1
              CARD(N)=BLNKS
BLANK       CONTINUE
            FUNCTION RETURN
            END OF FUNCTION
          R
            END OF FUNCTION
