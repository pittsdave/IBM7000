          R          S. ROSENBAUM, X6035, M1416-1484, 18 JANUARY 1966
          R
          R
          R          SORTING PROCEDURES FOR 'LISTF'
          R
          R
          R
            EXTERNAL FUNCTION(ISRT,REV,WORD,POINT)
          R
          R     ISRT=0 - FILE DIRECTORY ORDER
          R          1 - ALPHABETICALLY (NAME1,NAME2)
          R          2 - ALPHABETICALLY (NAME2 NAME1)
          R          3 - NUMERICALLY (MODE,MADE)
          R          4 - NUMERICALLY (LENGTH,MADE)
          R          5 - CHRONOLOGICALLY (USED,MADE)
          R          6 - CHRONOLOGICALLY (MADE)
          R
          R     REV=(+) - LARGEST FIRST
          R         (-) - SMALLEST FIRST
          R
          R     WORD = ARRAY CONTAINING FILE DIRECTORY TO BE SORTED
          R
          R     POINT(0)=NUMBER OF ENTRIES TO BE SORTED
          R     POINT(1)...POINT(POINT(0))=POINTERS TO ACCEPTABLE FILES
          R
          R
            NORMAL MODE IS INTEGER
          R
          R     (OPERATOR) - LOGICALLY GREATER THAN, .LG.
          R
          R
            DEFINE BINARY OPERATOR .LG., PRECEDENCE SAME AS .G.
            MODE STRUCTURE 1 .LG. 1 TO 2
       JMP  *+1,AC,*+2
       STO  T
       JMP  *+1,LA,*+2
       SLW  T
       CAL  A
       LAS  B
       TRA  LOC+3
       AXT  0,0
       PXD  0
       OUT  AC
       END
          R
          R
          R
          R     (OPERATOR) - LOGICALLY EXCHANGE, .XCL.
          R
            DEFINE BINARY OPERATOR .XCL., PRECEDENCE SAME AS .G.
            MODE STRUCTURE 1 .XCL. 1 TO 1
          R
       CAL  A
       LDQ  B
       STQ  A
       SLW  B
       OUT  ACQ
       END
          R
          R
          R
          R     (PROCEDURE) - ADJUST WORDS FOR ALPHABETIC SORT
          R
          R     POINT(I)3-5=NO. OF LEADING BLANKS IN NAME1(I)
          R     POINT(I)6-8=NO. OF LEADING BLANKS IN NAME2(I)
          R
            INTERNAL FUNCTION (REL)
          R
          R
          R     (ENTRY) - REMOVE LEADING BLANKS AND LEFT ADJUST WORDS
          R
            ENTRY TO SET.
          R
            SHIFT=30-REL*3
            THROUGH SETL,FOR I=1,1,I.G.NP
            P=POINT(I)-REL
            XI=WORD(P)
            THROUGH BLANKS,FOR J=0,1,J.E.6
            WHENEVER (XI.A.770000000000K).NE.BLANK(1)
                 POINT(I)=POINT(I).V.(J.LS.SHIFT)
                 WORD(P)=XI
                 TRANSFER TO SETL
            OTHERWISE
                 XI=XI.LS.6
            END OF CONDITIONAL
BLANKS      CONTINUE
SETL        CONTINUE
            FUNCTION RETURN
          R
          R     WORDS READY TO BE SORTED
          R
          R
          R
          R     (ENTRY) - RIGHT ADJUST WORDS AND REPLACE LEADING BLANKS
          R
            ENTRY TO UNSET.
            SHIFT=30-REL*3
            THROUGH UNSETL,FOR I=1,1,I.G.NP
            P=POINT(I)-REL
            XJ=(POINT(I).RS.SHIFT).A.7K
            WORD(P)=(WORD(P).RS.(6*XJ)).V.BLANK(XJ)
UNSETL      CONTINUE
            FUNCTION RETURN
          R
          R     WORDS RESTORED
          R
            END OF FUNCTION
          R
          R
          R
          R     (PROCEDURE) - EXCHANGE SORT, LARGEST ITEM FIRST
          R
            INTERNAL FUNCTION (REL,MSK)
          R
          R
            ENTRY TO SORTS.
          R
            P=NP+1
            THROUGH SORT,FOR I=1,1,I.E.NP
            XCHNGE=0
            XI=WORD(POINT(1)-REL).A.MSK
            THROUGH SORT1,FOR J=2,1,J.E.P
            XJ=WORD(POINT(J)-REL).A.MSK
            WHENEVER XJ.LG.XI
                 XCHNGE=1
                 POINT(J).XCL.POINT(J-1)
            OTHERWISE
                 XI=XJ
            END OF CONDITIONAL
SORT1       CONTINUE
            WHENEVER XCHNGE.E.0,FUNCTION RETURN
          R
          R     CONTINUE SORTING
            P=P-1
SORT        CONTINUE
            FUNCTION RETURN
          R
          R     FINISHED SORTING
          R
          R
            END OF FUNCTION
          R
          R
          R
          R     (ENTRY) - BODY OF ROUTINE
          R
          R
          R
            ENTRY TO SORTER.
          R
            NP=POINT
          R
          R     RETURN TO CALLER IF LESS THAN TWO ITEMS
          R
            WHENEVER NP.L.2,FUNCTION RETURN
            ISORT=ISRT
            R=REV
            WHENEVER ISORT.E.0,TRANSFER TO ORDER
            WHENEVER ISORT.L.3,TRANSFER TO SORT12
          R
          R     DO PRELIMINARY SORT ON TIME MADE
          R
            SORTS.(2,777777777777K)
          R
          R     CHOOSE A PRIMARY SORT
            TRANSFER TO SORTI(ISORT)
          R
          R     MODE
SORTI(3)    SORTS.(4,776000000000K)
            TRANSFER TO ORDER
          R
          R     LENGTH (NOREC AND LCOUNT)
SORTI(4)    SORTS.(5,077777077777K)
            TRANSFER TO ORDER
          R
          R     DATE LAST USED
SORTI(5)    SORTS.(3,777777000000K)
            TRANSFER TO ORDER
          R
          R     TIME MADE
SORTI(6)    TRANSFER TO ORDER
          R
          R
          R     FILE NAMES
          R
          R     ADJUST NAMES FOR SORTING
SORT12      SET.(0,777777777777K)
            SET.(1,777777777777K)
          R
          R     SORT NAMES
            WHENEVER ISORT.E.1
                 SORTS.(1,777777777777K)
                 SORTS.(0,777777777777K)
            OTHERWISE
                 SORTS.(0,777777777777K)
                 SORTS.(1,777777777777K)
            END OF CONDITIONAL
          R
          R     RESTORE ORIGINAL NAMES
            UNSET.(0,777777777777K)
            UNSET.(1,777777777777K)
          R
          R     SET PRINT-OUT ORDER
            R=-R
            TRANSFER TO ORDER
          R
          R
          R
          R     PUT POINTERS IN CORRECT ORDER FOR PRINT-OUT
          R
ORDER       WHENEVER (R.G.0),FUNCTION RETURN
          R
          R     REVERSE ORDER
          R
            XI=NP+1
            XJ=NP/2+1
            THROUGH REORD,FOR I=1,1,I.E.XJ
REORD       POINT(I).XCL.POINT(XI-I)
            FUNCTION RETURN
          R
          R     FINISHED ORDERING - RETURN TO CALLER
          R
          R
          R
          R     STORAGE
          R
          R     TABLE USED FOR RESTORING NAMES AFTER SORTING
            VECTOR VALUES BLANK=0,$ 00000  0000   000    00     0      $
          R
          R
            END OF FUNCTION
