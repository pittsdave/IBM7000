          R
            EXTERNAL FUNCTION (PARAM)
            NORMAL MODE IS INTEGER
            ERASABLE DUM(200),N(2000),INTEG(2000),FUNCT(1000),DIM(2000),
          1 I(666),HOL(500),OUT(500),ARRG(50),STNO,NSNO,STACON,CCT,
          2 WHOLE,DUMP,BUMP,LUMP,RUMP,CRUMP,PUMP,FLAG,BOOLFL,SUB,SW3,
          3 TS3,PR,ERR,CSW,NUMBER,MOX,ZZXQ1
            BOOLEAN BOOLFL,CSW,MOX,DUMP,BUMP,LUMP,RUMP,CRUMP,PUMP
            STATEMENT LABEL SW3,TS3,PR,ZZXQ1
            STATEMENT LABEL SW
          R
          R  FNCTFN.
          R
            ENTRY TO FNCTFN.
T4          THROUGH T4, FOR T1=1,1,T1.G.FUNCT .OR. FUNCT(T1).E.PARAM
            WHENEVER T1.LE.FUNCT, FUNCTION RETURN T1
            FUNCT=FUNCT+1
            WHENEVER FUNCT.G.1000
            FUNCT=FUNCT-1
            EXECUTE ERRP.(22)
            END OF CONDITIONAL
            FUNCT(FUNCT)=PARAM
            FUNCTION RETURN FUNCT
          R
          R  FINDN.
          R
            ENTRY TO FINDN.
            ROB=PARAM
            WHENEVER PARAM.E.$*$,TRANSFER TO T2B
            SW=T2
            TRANSFER TO T7
T2          THROUGH T2, FOR T1=  1,1,T1.G.N.OR.N(T1).E.ROB
            WHENEVER T1.LE.N, FUNCTION RETURN T1
T2B         N=N+1
            WHENEVER N.G.2000
            N=N-1
            EXECUTE ERRP.(21)
            END OF CONDITIONAL
            N(N)=ROB
            FUNCTION RETURN N
          R
          R  OPCHK.
          R
            ENTRY TO OPCHK.
            THROUGH T6, FOR VALUES OF
          1  A=$+$,$-$,$*$,$/$,$)$,$($,$.$,$=$,$,$
T6          WHENEVER PARAM.E.A, FUNCTION RETURN 1B
            FUNCTION RETURN 0B
          R
          R  LZR.
          R
            ENTRY TO LZR.
            SW=T8
T7          THROUGH T7, FOR T1=0,6,PARAM.LS.T1.A.77K10.NE.0.OR.T1.E.30
            ROB=PARAM.LS.T1.V.$$.RS.(36-T1)
            TRANSFER TO SW
T8          FUNCTION RETURN ROB
          R
          R  FINDIN.
          R
            ENTRY TO FINDIN.
T3          THROUGH T3, FOR T1=  1,1,T1.G.INTEG.OR.INTEG(T1).E.PARAM
            WHENEVER T1.LE.INTEG, FUNCTION RETURN T1
            INTEG=INTEG+1
            WHENEVER INTEG.G.2000
            INTEG=INTEG+1
            EXECUTE ERRP.(23)
            END OF CONDITIONAL
            INTEG(INTEG)=PARAM
            FUNCTION RETURN INTEG
          R
          R  DIMFN.
          R
            ENTRY TO DIMFN.
T5          THROUGH T5, FOR T1=1,1,T1.G.DIM.OR.DIM(T1 ).E.PARAM
            WHENEVER T1.G.DIM,TRANSFER TO ZZXQ1
            FUNCTION RETURN DIM(T1+1000)
            END OF FUNCTION
