          R                  MTR2
          R
            EXTERNAL FUNCTION
            INSERT FILE TDFEV
            NORMAL MODE IS INTEGER
            ERASABLE DUM(200),N(2000),INTEG(2000),FUNCT(1000),DIM(2000),
          1 I(666),HOL(500),OUT(500),ARRG(50),STNO,NSNO,STACON,CCT,
          2 WHOLE,DUMP,BUMP,LUMP,RUMP,CRUMP,PUMP,FLAG,BOOLFL,SUB,SW3,
          3 TS3,PR,ERR,CSW,NUMBER,MOX,ZZXQ1
            BOOLEAN BOOLFL,CSW,MOX,DUMP,BUMP,LUMP,RUMP,CRUMP,PUMP
            STATEMENT LABEL SW3,TS3,PR,ZZXQ1
            DIMENSION TEMP(6),ARG(100)
            EQUIVALENCE (S2,S45,AS2), (STOUT,ASTOUT)
            BOOLEAN PSET,OPCHK.,DGT.
            STATEMENT LABEL S45, STOUT
          R
          R  MADTR2.
          R
            ENTRY TO MADTR2.
            TRANSFER TO S(FLAG)
          R
          R  END FILE
          R
S(1)        I(1)=$END OF$
            I(2)=$FILE T$
            I(3)=$APE$
            OUT(1)=1
            OUT(2)=2
            OUT(3)=3
            OUT(4)=-4
            OUT(5)=-5
            OUT(6)=-6
            OUT(7)=-7
            OUT(8)=700
            TRANSFER TO S2
          R
          R  COMMON
          R
S(15)       I(1)=$PROGRA$
            I(2)=$M COMM$
            I(3)=$ON$
            OUT(1)=1
            OUT(2)=2
            OUT(3)=3
            OUT(4)=-4
            OUT(5)=-5
            OUT(6)=-6
            OUT(7)=700
            TRANSFER TO S2
          R
          R  BACKSPACE
          R
S(11)       I(7)=$ACE RE$
            I(8)=$CORD 0$
            I(9)=$F TAPE$
            OUT(1)=7
            OUT(2)=8
            OUT(3)=9
            OUT(4)=700
            TRANSFER TO S2
          R
          R  REWIND
          R
S(8)        WHENEVER I(7).E.$($,TRANSFER TO S30
            I(5)=$ND$
            I(6)=$TAPE$
            OUT(1)=5
            OUT(2)=6
            OUT(3)=700
            TRANSFER TO S2
          R
          R  DIMENSION
          R
S(9)        SW1=0
            CT=0
            A4 = 10
            SW2=10
S78         THROUGH S78,FOR A1=SW2,1,OPCHK.(I(A1)).OR.A1.G.CCT
            WHENEVER I(A1).NE.$($.OR.A1.G.CCT
S76         ERRO =11
            TRANSFER TO S60
            END OF CONDITIONAL
            WHENEVER SUB.NE.$ $
            CT=0
            EXECUTE COMPZ.(A1-SW2,I(SW2),I)
S74A        THROUGH S74A, FOR A6=1,1,ARRG(A6).E.I.OR.A6.G.ARRG
            WHENEVER A6.LE.ARRG
            CT=1
            END OF CONDITIONAL
            END OF CONDITIONAL
            WHENEVER SUB.NE.$ $,DCHECK=A4
            SW5=0
            SW4=A1+1
            A3=SW4
S75         THROUGH S75,FOR A2=SW4 ,1,OPCHK.(I(A2)).OR.A2.G.CCT
            WHENEVER I(A2).E.$,$.OR.I(A2).E.$)$
            SW5=SW5+1
            EXECUTE ZCOMPZ.(A2-SW4,I(SW4  ),TEMP(SW5))
            WHENEVER I(A2).E.$)$.AND.SW5.E.1,TRANSFER TO S74
            WHENEVER SW5.G.3
            ERRO =12
            TRANSFER TO S60
            OR WHENEVER SW5.E.2
            SW4=A2+1
            OTHERWISE
            SW4=A2+1
            END OF CONDITIONAL
            WHENEVER I(A2).E.$,$,TRANSFER TO S75
            OTHERWISE
            TRANSFER TO S76
            END OF CONDITIONAL
            I(A3)=TEMP(1)
            I(7)=LZR.(TEMP(2))
            WHENEVER SW5.E.2
            I(5)=$2$
            I(A3)=LZR.(BNBCD.(BCDBN.(TEMP(1))*BCDBN.(TEMP(2))))
            OTHERWISE
            I(5)=$3$
            I(A3)=LZR.(BNBCD.(BCDBN.(TEMP(1))*BCDBN.(TEMP(2))*
          1  BCDBN.(TEMP(3))))
            I(9)=LZR.(TEMP(3))
            END OF CONDITIONAL
            WHENEVER CT.E.0
            I(1)=$VECTOR$
            I(2)=$VALUES$
            I(4)=$  =$
            I(6)=$ ,1,$
            I(8)=$  ,$
            STNO=$ $
            WHOLE =1
            I(3)=STACON.V.BNBCD.(FINDN.($*$))
            TEMP=CCT
            I(A3+1)=$,$
            I(A3+2)=I(3)
            SW3=S77
            CCT=3+2*SW5
            TRANSFER TO S2
S77         CCT=TEMP
            END OF CONDITIONAL
            OUT(A4)=A3
            OUT(A4+1)=A3+1
            OUT(A4+2)=A3+2
            A4=A4+3
            THROUGH S79,FOR A5=A3+3,1,A5.E.A2
            OUT(A4)=-A5
S79         A4=A4+1
            TEMP(1)=I(A3)
S74         DIM=DIM+1
            WHENEVER DIM.G.1000
            DIM = DIM -1
            ERRO =1
            TRANSFER TO S60
            END OF CONDITIONAL
            SW1=SW1+1
            DIM(DIM)=COMPZ.(A1-SW2,I(SW2),I)
            DIM(DIM+1000)=TEMP(1)
            WHENEVER SUB.NE.$ $
            WHENEVER CT.E.1
            SW1=SW1-1
            A4=DCHECK
            THROUGH S74B, FOR A2=SW2,1,I(A2).E.$)$
            OUT(A4)=-A2
S74B        A4=A4+1
            OUT(A4)=-A2
            A4=A4+1
            WHENEVER A2.NE.CCT
            OUT(A4)=-A2-1
            A4=A4+1
            OTHERWISE
            WHENEVER SW1.G.0
SHARY       THROUGH SHARY,FOR A6942=A4-1,-1,OUT(A6942)
          1  .E.-SW2
            THROUGH SHAIRY,FOR A6942=A6942,1,A6942.G.A4-1
SHAIRY      OUT(A6942)=OUT(A6942)+1
            OUT(A4)=-A2
            A4=A4+1
            END OF CONDITIONAL
            END OF CONDITIONAL
            END OF CONDITIONAL
            EN D OF CONDITIONAL
            WHENEVER A2+1.G.CCT
            WHENEVER SW1.E.0,TRANSFER TO S3
            SW3=S3
            THROUGH S29B,FOR A3=1,1,A3.G.7
S29B        OUT(A3)=-A3
            OUT(8)=8
            OUT(9)=9
            I(8)=$DIMENS$
            I(9)=$ION$
            OUT(A4)=700
            WHOLE=0
            TRANSFER TO S2
            OTHERWISE
            SW2=A2+2
            WHENEVER I(A2+1).NE.$,$,TRANSFER TO S76
            TRANSFER TO S78
            END OF CONDITIONAL
          R
          R  FORMAT
          R
S(10)       THROUGH S(10),FOR A1=CCT,-1,I(A1).NE.$ $
            CCT=A1
            HSET=0
            THROUGH S82, FOR A1=8,1,A1.G.CCT
            WHENEVER I(A1).E.$=$
            TRANSFER TO S(3)
            OR WHENEVER I(A1).E.$H$
            A3=12
            THROUGH S82A, FOR A2=A1-1,-1,(I(A2).G.$9$.OR.I(A2).L.$0$)
          1 .AND.I(A2).NE.$$.OR.A2.L.HSET
            WHENEVER I(A2).NE.$ $
            OUT(A3)=I(A2)
            A3=A3-1
            END OF CONDITIONAL
S82A        CONTINUE
            WHENEVER A3.E.12
            A2=I(A1+1)
S82B        THROUGH S82B, FOR A1=A1+2,1,I(A1).E.A2.OR.A1.GE.CCT
            OTHERWISE
            A1=A1+BCDBN.(ZCOMPZ.(12-A3,OUT(A3+1),I))
            END OF CONDITIONAL
            WHENEVER A1.GE.CCT, ERRP.(24)
            HSET=A1+1
            END OF CONDITIONAL
S82         CONTINUE
            I(1)=$VECTOR$
            A3=8
            OUT(1)=1
            OUT(2)=2
            OUT(3)=3
            OUT(4)=4
            OUT(5)=-5
            OUT(6)=-6
            OUT(7)=7
            I(2)=$ VALUE$
            I(3)=$S     $
            PSET = 0B
            WHENEVERI(CCT).NE.$)$
            ERRO=24
            TRANSFER TO S60
            END OF CONDITIONAL
            I(4)=STNO
            I(7)=$00=0R $.V.$  0 20$
            SW2=8
            HSET=0
            THROUGH S300, FOR A1=SW2,1,A1.G.CCT
            WHENEVER I(A1).E.$=$
S302        ERRO=24
            TRANSFER TO S60
            OR WHENEVER I(A1).E.$X$
            THROUGH S302A, FOR A01=A1-1,-1,.NOT. DGT.(I(A01))
          1 .OR.A01.L.HSET
     S302A  I(A01+1)= I(A01)
            I(A01+1)=$S$
S303A       THROUGH S303A, FOR A5=A1+1,1,I(A5).NE.$$
            WHENEVER .NOT.OPCHK.(I(A5)).OR.I(A5).E.$+$.OR.I(A5).E.$-$
          1 .OR.I(A5).E.$($
            I(A1)=I(A1).V.$0  ,  $
            OUT(A3)=A1
            A3=A3+1
            END OF CONDITIONAL
            OR WHENEVER I(A1).E.$A$
            I(A1)=$C$
            OR WHENEVER I(A1).E.$O$
            I(A1)=$K$
            OR WHENEVER I(A1).E.$H$
            A5=CCT+5
            THROUGH S301A, FOR A4=A1-1,-1,.NOT.DGT.(I(A4))
          1 .AND.I(A4).NE.$$.OR.A4.L.HSET
            WHENEVER I(A4).NE.$ $
            I(A5)=I(A4)
            A5=A5-1
            END OF CONDITIONAL
S301A       CONTINUE
            WHENEVER A5.E.CCT+5
            A4=I(A1+1)
S303        THROUGH S303, FOR A1=A1+2,1,I(A1).E.A4.OR.A1.GE.CCT
            OTHERWISE
            A1=A1+BCDBN.(ZCOMPZ.(CCT+5-A5,I(A5+1),I))
            END OF CONDITIONAL
            WHENEVER A1.GE.CCT, ERRP.(24)
            HSET=A1+1
            OR WHENEVER I(A1).E.$P$
            PSET = 1B
S304        THROUGH S304,FOR   A4=A1-1,-1,.NOT. DGT.(I(A4))  .AND.
          1 I(A4).NE.$-$.AND.I(A4).NE.$+$.OR.A4.L.HSET
            THROUGH S304A, FOR A2=A4+1,1,A2.G.A1
            TEMP(A2-A4)=I(A2)
            OUT(A3)=-A2
S304A       A3=A3+1
            A6=A1-A4
            OR WHENEVER (I(A1).E.$E$.OR.I(A1).E.$F$).AND.PSET
S304B       THROUGH S304B, FOR A4=A1-1,-1,.NOT. DGT.(I(A4))
            A2=A6
            THROUGH S304C, FOR A4=A4+1,1,A4.G.A1
            OUT(A3)=-A4
            A3=A3+1
            A2=A2+1
S304C       TEMP(A2)=I(A4)
            OUT(A3-1)=A1
            I(A1)=COMPZ.(A2,TEMP(1),I)
            END OF CONDITIONAL
      S300  CONTINUE
            WHENEVER A1.G.CCT.AND.I(CCT ).NE.$)$,TRANSFER TO S302
            I(CCT)=$*R$.V.$02$
            OUT(A3)=CCT
            OUT(A3+1)=700
            STNO =$ $
            TRANSFER TO S2
          R
          R  IF(CHECKING) AND SENSELIGHT
          R
S(17)       I(1) = $EXECUT$
            I(2)=$E    Q$
            I(3)=$QSLN.($
            I(4)=I(CCT)
            I(5)=$)$
            WHOLE = 1
            FNCTFN.($QQSLN$,S602)
            CCT=5
            TRANSFER TO S2
S(18)       THROUGH S(18), FOR A1 = CCT,-1,I(A1).G.$9$.OR. I(A1).L.$0$
            NUMST=STACON.V.BNBCD.(FINDN.(COMPZ.(CCT-A1,I(A1+1))))
            WHENEVER I(A1).E.$,$
            NUMSU=NUMST
            CCT=A1-1
            TRANSFER TO S(18)
            OR WHENEVER I(A1).E.$)$
            NUMSV=I(A1-1)
            OR WHENEVER OPCHK. (I(A1))
            TRANSFER TO S30
            OTHERWISE
            NUMSV=$ $
            END OF CONDITIONAL
            WHENEVER I(6).E.$U$
            I(3)=$QACO.($
            OR WHENEVER I(6).E.$I$
            I(3)=$QDCT.($
            OR WHENEVER I(6).E.$T$
            I(3)=$QMQO.($
            OR WHENEVER I(9).E.$L$
            I(3)=$QSLT.($
            OR WHENEVER I(9).E.$S$
            I(3)=$QSWT.($
            OTHERWISE
            ERRO =7
            TRANSFER TO S60
            END OF CONDITIONAL
            I(1)=$EXECUT$
            I(2)=$E    Q$
            I(4)=NUMST
            I(5)=$  ,$
            FNCTFN.((I(3).RS.6).EV.500000000053K,S602)
            WHENEVER I(6).E.$N$
            I(7)=$  ,$
            I(8)=NUMSV
            CCT=9
            OTHERWISE
            CCT=7
            END OF CONDITIONAL
            I(CCT)=$)$
            WHOLE =1
            I(6)=NUMSU
            TRANSFER TO S2
          R
          R  IF
          R
S(5)        SW2=4
            A2=1
S21A        THROUGH S21A,FOR A3=CCT,-1,OPCHK.(I(A3))
            WHENEVER I(A3).NE.$,$,TRANSFER TO S30
S21B        THROUGH S21B,FOR A1=A3,-1,I(A1).E.$)$.OR.A1.E.0
            WHENEVER A1.E.0
            ERRO =15
            TRANSFER TO S60
            END OF CONDITIONAL
            EXTT=A1-1
            SW2=CCT
            THROUGH S49,FOR A1=1,1,A1.G.3
S50         THROUGH S50, FOR A2=SW2,-1,I(A2).E.$,$.OR.I(A2).E.$)$
            TEMP(A1)=STACON.V.BNBCD.(FINDN.(COMPZ.(SW2-A2,I(A2+1))))
       S49  SW2=A2-1
            I(1)=$WHENEV$
            I(2)=$ER$
            OUT(1)=1
            OUT(2)=2
            ENTER=4
            OUT=3
            STOUT=S49A
            EXECUTE LEVIN.
            TRANSFER TO S30A
S49A        WHENEVER TEMP(1).E.TEMP(2)
            TEMP=1
            WHENEVER TEMP(1).E.TEMP(3), TRANSFER TO S56
            I(A2+1)=$.L.0,$
            I(A2+4)=TEMP(3)
            OR WHENEVER TEMP(2).E.TEMP(3)
            TEMP=2
            I(A2+1)=$.G.0,$
            I(A2+4)=TEMP(1)
            OR WHENEVER TEMP(1).E.TEMP(3)
            TEMP=3
            I(A2+1)=$.E.0,$
            I(A2+4)=TEMP(2)
            OTHERWISE
            I(A2+1)=$.L.0,$
            I(A2+4)=TEMP(3)
            SW3=S54
            TRANSFER TO S55
            END OF CONDITIONAL
            SW3=S56
S55         OUT(OUT+1)=A2+1
            OUT(OUT+2)=A2+2
            OUT(OUT+3)=A2+3
            OUT(OUT+4)=A2+4
            OUT(OUT+5)=700
            I(A2+2)=$TRANSF$
            I(A2+3)=$ER TO $
            CCT=A2+4
            TRANSFER TO S2
S56         WHENEVER TEMP(TEMP).E.NSNO, TRANSFER TO S3
            I(1)=$TRANSF$
            I(2) = $ER TO $
            I(3)=TEMP(TEMP)
            WHOLE =1
            SW3=S3
            CCT=3
S55B        STNO=$ $
            TRANSFER TO S45
S54         I(A2+1)=$.G.0,$
            I(A2+4)=TEMP(1)
            TEMP=2
            WHOLE=0
            SW3=S56
            TRANSFER TO S55B
          R
          R  DO
          R
S(19)       WHENEVER I(3).G.$9$.OR.I(3).L.$0$,TRANSFER TO S30
S601        THROUGH S601, FOR A1=3,1,A1.G.CCT.OR.I(A1).L.$0$.OR.I(A1).G.
          1 $9$
            WHENEVER A1.G.CCT
S603        ERRO =3
            TRANSFER TO S60
            OR WHENEVER OPCHK.(I(A1))
            TRANSFER TO S30
            END OF CONDITIONAL
            NUMST=STACON.V.BNBCD.(FINDN.(COMPZ.(A1-3,I(3))))
S602        THROUGH S602 , FOR A2=A1+1,1,OPCHK.(I(A2)).OR.A2.G.CCT
            WHENEVER A2.G.CCT.OR.I(A2).NE.$=$,TRANSFER TO S603
S604        THROUGH S604, FOR A3=A2+1,1,A3.G.CCT.OR.OPCHK.(I(A3))
            WHENEVER I(A3).NE.$,$, TRANSFER TO S30
            EXECUTE COMPZ.(A2-A1,I(A1),NUMSU)
            WHENEVER I(A1).E.$I$.OR.I(A1).LE.$J$.AND.I(A1).GE.$N$,
          1  EXECUTE FINDIN.(NUMSU)
            EXECUTE COMPZ.(A3-A2-1,I(A2+1),NUMSV)
            WHENEVER I(A2+1).E.$I$.OR.I(A2+1).LE.$J$.AND.I(A2+1).GE.$N$,
          1   EXECUTE FINDIN.(NUMSV)
S605        THROUGH S605, FOR A1=A3+1,1,A1.G.CCT.OR.I(A1).E.$,$
            EXECUTE COMPZ.(A1-A3-1,I(A3+1),TEMP)
            WHENEVER I(A3+1).E.$I$.OR.I(A3+1).LE.$J$.AND.I(A3+1).GE.$N$,
          1   EXECUTE FINDIN.(TEMP)
            WHENEVER TEMP.E.RET,TEMP=RETN
            WHENEVER NUMSU.E.RET,NUMSU=RETN
            WHENEVER NUMSV.E.RET,NUMSV=RETN
            WHENEVER NUMST.E.RET,NUMST=RETN
            WHENEVER NUMSU.E.NUMSV
            FLUBB=SW3
            STATEMENT LABEL FLUBB
            SW3=XPYMZZ
            GLAG=CCT
            GLAGG=STACON.V.BNBCD.(FINDN.($*$))
            I(1)=GLAGG
            I(2)=$=$
            I(3)=NUMSV
            CCT=3
            WHOLE=1
            TRANSFER TO S2
XPYMZZ      NUMSV=GLAGG
            WHENEVER I(A2+1).E.$I$.OR.I(A2+1).LE.$J$.AND.I(A2+1).GE.$N$,
          1   EXECUTE FINDIN.(GLAGG)
            SW3=FLUBB
            STNO=$ $
            CCT=GLAG
            END OF CONDITIONAL
            WHENEVER A1.G.CCT
            I(8)=$ ,1,$
            I(9)=NUMSU
            I(10)=$ .G.$
            I(11)=TEMP
            CCT=11
            OTHERWISE
            EXECUTE COMPZ.(CCT-A1  ,I(A1+1),I(9))
            WHENEVER I(9) .E.RET, I(9)=RETN
            WHENEVER I(A1+1).E.$I$.OR.I(A1+1).LE.$J$.AND.I(A1+1).GE.$N$,
          1  EXECUTE FINDIN.(I(9))
            I(8)=$,$
            I(10)=$,$
            I(11)=NUMSU
            I(12)=$ .G.$
            I(13)=TEMP
            CCT=13
            END OF CONDITIONAL
            I(1)=$THROUG$
            I(2)=$H$
            I(3)=NUMST
            I(4)=$ ,FOR$
            I(5)=NUMSU
            I(6)=$  =$
            I(7)=NUMSV
            CCT=CCT+1
            I(CCT)=$.AND.$
            I(CCT+1)=NUMSU
            I(CCT+2)=$.NE.$
            I(CCT+3)=NUMSV
            CCT=CCT+3
            WHOLE = 1
            TRANSFER TO S2
          R
          R  ASSIGN
          R
S(2)        WHENEVER I(7).E.$($.OR.I(7).E.$=$, TRANSFER TO S(3)
S57         THROUGH S57,FOR A1=7,1,I(A1).G.$9$.OR.I(A1).L.$0$.OR.
          1  A1.G.CCT
            WHENEVER A1.G.CCT
            ERRO =10
            TRANSFER TO S60
            END OF CONDITIONAL
            SW2=COMPZ.(CCT-A1-1,I(A1+2),I(1))
            I(3)=STACON.V.BNBCD.(FINDN.(COMPZ.(A1-7,I(7))))
            I(2)=$=$
            WHOLE=1
            CCT=3
            SW3=S57A
            TRANSFER TO S2
S57A        I(4)=SW2
            I(1)=$STATEM$
            SW2=0
            WHENEVER I(A1).E.$I$.OR.I(A1).GE.$N$.AND.I(A1).LE.$K$,SW2=1
            I(2)=$ENT LA$
            I(3)=$BEL$
            SW3=S3
            STNO=$ $
            CCT=4
            TRANSFER TO S45
          R
          R  CALL
          R
S(16)       THROUGH S(16), FOR A1=5,1,OPCHK.(I(A1)).OR.A1.G.CCT
            I(CCT+1)=0
            WHENEVER OPCHK.(I(A1)).AND.I(A1).NE.$($,TRANSFER TO S30
            A3=A1
S23B        THROUGH S23B, FOR A2=A1,1,OPCHK.(I(A2)).OR.A2.G.CCT
          1  .OR.I(A2).E.$H$
            WHENEVER A2.G.CCT,TRANSFER TO S23C
            WHENEVER I(A2).E.$H$
SSSAA       THROUGH SSSAA,FOR A6942=A2-1,-1,I(A6942).L.$0$.OR.
          1   I(A6942).G.$9$.OR.A6942.LE.A1
            WHENEVER .NOT.A6942.LE.A1.AND..NOT.OPCHK.(I(A6942))
          1   .OR.A6942.E.A2-1,TRANSFER TO SSSAAA
            SKORK=BCDBN.(ZCOMPZ.(A2-A6942,I(A6942),SKORK))
            A1=A2+SKORK+1
            TRANSFER TO S23B
            END OF CONDITIONAL
            WHENEVER I(A2).E.$=$,TRANSFER TO S30
SSSAAA      A1=A2+1
            TRANSFER TO S23B
S23C        I(1)=$EXECUT$
            I(2)=$E$
            I(A3)=$ .($
            OUT(5)=A3
            EXECUTE FNCTFN.(COMPZ.(A3-5,I(5),I),S602)
            WHENEVER I(5).E.$I$.OR.I(5).GE.$N$.AND.I(5).LE.$J$
          1 ,EXECUTE FINDIN.(I,S603)
            WHENEVER A3.G.CCT
            OUT=5
            I(A1+1)=$0$
            I(A1+2)=$)$
            CCT=A1+2
            OTHERWISE
            OUT=6
            ENTER =A3+1
            EXTT=CCT-1
            STOUT=S23D
            EXECUTE LEVIN.
            EXECUTE LUP.
            EXECUTE SETL.
            TRANSFER TO S30A
            END OF CONDITIONAL
S23D        OUT(1)=1
            OUT(2)=2
            OUT(3)=-3
            OUT(4)=-4
            OUT(OUT+1)=700
            TRANSFER TO S2
          R
          R  FUNCTION AND SUBROUTINE
          R
S(4)        WHENEVER I(1).E.$S$
            SW1=11
            OTHERWISE
            SW1=9
            END OF CONDITIONAL
S58         THROUGH S58,FOR A1=SW1,1,I(A1).E.$($.OR.A1.G.CCT
            WHENEVER A1.G.CCT
            TRANSFER  TO S582
S581        ERRO=4
            TRANSFER TO S60
            END OF CONDITIONAL
            A3=A1+1
S580        THROUGH S580, FOR A2=A3,1,I(A2).E.$,$.OR.I(A2).E.$)$.OR.
          1   A2.G.CCT
            WHENEVER A2.G.CCT,TRANSFER TO S581
            ARRG=ARRG+1
            WHENEVER ARRG.G.50,TRANSFER TO S581
            EXECUTE COMPZ.(A2-A3,I(A3),ARRG(ARRG))
            A3=A2+1
            WHENEVER I(A2).NE.$)$,TRANSFER TO S580
S582        EXECUTE COMPZ.(A1-SW1,I(SW1),RET)
            SUB=I(1)
            I(1)=$EXTERN$
            I(2)=$AL FUN$
            I(3)=$CTION$
            OUT(1)=1
            OUT(2)=2
            OUT(3)=3
            THROUGH S59,FOR A2=4,1,A2.E.A1
S59         OUT(A2)=-A2
            OUT(A2)=700
            SW3=S61
            TRANSFER TO S2
S61         I(1)=$ENTRY $
            I(2)=$TO$
             I(3)=RET
            I(4)=$.$
            RETN=STACON.V.FINDN.(RET)
            WHENEVER I(SW1).E.$I$.OR.I(SW1).GE.$N$.AND.I(SW1).LE.
          1 $J$.OR.I(SW1).E.$X$,EXECUTE FINDIN.(RETN)
            CCT=4
            WHOLE=1
            OUT=(700)
            SW3=S3
            STNO=$ $
            TRANSFER TO S45
          R
          R  RETURN
          R
S(7)        WHENEVER CCT.G.6,TRANSFER TO S30
            I(1)=$FUNCTI$
            I(2)=$ON RET$
            I(3)=$URN$
            OUT(1)=1
            OUT(2)=2
            OUT(3)=3
            WHENEVER SUB .E. $S$
            OUT(4)=700
            CCT=3
            OTHERWISE
            CCT=4
            OUT(4)=4
            OUT(5)=700
            I(4)=RETN
            END OF CONDITIONAL
            RET=$0$
            TRANSFER TO S2
          R
          R  SUBST.
          R
            ENTRY TO SUBST.
S(3)        STOUT =S2
            ENTER =1
            EXTT =CCT
            EXECUTE LEVIN.
            OUT=1
S30A        A4=ENTER
            FUN=0
            ARG=0
            XFN=0
            FFZAG=0B
            THRZAG=0B
            BOOLEAN FFZAG,THRZAG
            THROUGH S92, FOR A3=ENTER,1,A3.G.EXTT
            FFZAG=0B
            THRZAG=0B
            WHENEVER I(A3).GE.$0$.AND.I(A3).LE.$9$ .OR.OPCHK.(I(A3))
          1 .OR.(I(A3).E.$E$.OR.I(A3).E.$H$).AND.(I(A4-1).E.$.$
          2 .OR.I(A4-1).GE.$0$.AND.I(A4-1).LE.$9$).AND.A4.NE.ENTER
            I(A4)=I(A3)
            WHENEVER I(A4).E.$)$,EXECUTE LDN.
            WHENEVER I(A4).E.$($,EXECUTE LUP.
            A4=A4+1
            WHENEVER I(A3).E.$=$.AND.FUN.E.1
            FUN=2
            OR WHENEVER I(A3).E.$H$
S30D        THROUGH S30D, FOR CT=A4-2,-1,I(CT).L.$0$.OR.I(CT).G.$9$
          1  .OR.CT.L.ENTER
            TEST=BCDBN.(ZCOMPZ.(A4-CT-2,I(CT+1),TEST))
            EXECUTE FINDN.($*$)
            HOL(HOL+1)=FINDN.($*$)
            A4=(TEST-1)/6+1
            HOL(HOL)=A4
            I(CT+1)=STACON.V.BNBCD.(N)
            I(CT+2)=$(0000*$.EV.LZR.(BNBCD.(A4)).RS.6
            OUT(OUT)=CT+1
            OUT(OUT+1)=CT+2
            OUT=OUT+2
            A8=HOL+2
            HOL=A8+HOL(HOL)
            HOL(HOL)=900
            A4=6
            THROUGH S94, FOR A9=HOL-1,-1,A9.L.A8
            WHENEVER A9.E.A8, A4=TEST-(TEST/6)*6
            WHENEVER A4.E.0.AND.TEST.E.(TEST/6)*6,A4=6
S94         COMPZ.(A4,I(A3-5+6*(HOL-A9)),HOL(A9))
            A4=CT+3
            A3=A3+TEST
            OR WHENEVER I(A3).E.$*$.AND.I(A4-2).E.$*$
            I(A4-2)=$ .P.$
            OUT(OUT)=A4-2
            OUT=OUT+1
            A4=A4-1
            END OF CONDITIONAL
            OR WHENEVER I(A3).NE.$ $
            I(A4)=I(A3)
            A6=A4+1
            SW1=A3+1
            CHECK =1
S93         THROUGH S93, FOR A5=SW1,1,I(A5).NE.$ $.OR.A5.G.EXTT
            I(A6)=I(A5)
            WHENEVER OPCHK.(I(A5)).OR.A5.G.EXTT,TRANSFER TO     S93A
            A6=A6+1
            CHECK =0
            SW1=A5+1
            TRANSFER TO S93
S93A        EXECUTE COMPZ.(A6-A4,I(A4),I)
            WHENEVER I(A6).E.$($
            EXECUTE LUP.
            ZZXQ1=S93AA
            EXECUTE DIMFN.(I,ZZXQ1)
            TRANSFER TO S93C
S93AA       WHENEVER I(A6-1).NE.$F$
            FFZAG=0B
            TRANSFER TO S93B
            END OF CONDITIONAL
            WHENEVER A6-A4-1 .G. 6
S600        ERRO = 8
            TRANSFER TO S60
            END OF CONDITIONAL
            FFZAG=1B
            WHENEVER A6-A4-1.L.3
            THRZAG=1B
            TRANSFER TO S93B
            END OF CONDITIONAL
            THRZAG=0B
            EXECUTE COMPZ.(A6-A4-1,I(A4),I)
            WHENEVER I(A4).E.$X$, XFN=1
            WHENEVER A4.NE.1
            A6=A6-1
            I(A6)=I(A6+1)
            END OF CONDITIONAL
            WHENEVER I.E.$ABS$.OR.I.E.$FLOAT$.OR.I.E.$XABS$
            LEVT=0B
            LEV=LEV-1
            WHENEVER I(A4).E.$A$
            I(A4)=$    .A$
            OUT(OUT)=A4
            OUT=OUT+1
            A4=A4+1
            OR WHENEVER I(A4).E.$X$
            I(A4)=$.$
            OTHERWISE
            I(A4)=$($
            I(A4+1)=$0$
            I(A4+2)=$.$
            I(A4+3)=$0$
            I(A4+4)=$+$
            FL=1
            END OF CONDITIONAL
            TRANSFER TO S93F
            ORWHENEVER A4.E.1
            WHENEVER A6.L.5,TRANSFER TO S93C
            FUN=1
            I(1)=$INTERN$
            I(2)=$AL FUN$
            I(3)=$CTION$
            I(4)=I
            OUT(OUT)=1
            OUT(OUT+1)=2
            OUT(OUT+2)=3
            OUT(OUT+  3)=4
            OUT=OUT+4
            A6=5
            END OF CONDITIONAL
S93B        WHENEVER I.NE.$ABS$.AND.I.NE.$FLOAT$, EXECUTE FNCTFN.(I)
            WHENEVER XFN.E.1.OR.FFZAG.AND..NOT.THRZAG.AND.I(A4).E.$X$
          1 .OR..NOT.FFZAG.AND.(I(A4).E.$I$.OR.I(A4).GE.$N$.AND.
          2 I(A4).LE.$J$).OR.FFZAG.AND.THRZAG.AND.(I(A4).E.$I$.OR.
          3 I(A4).GE.$N$.AND.I(A4).LE.$J$)
            EXECUTE FINDIN.(I,S603)
            XFN=0
            END OF CONDITIONAL
            FFZAG=0B
            THRZAG=0B
S93F        EXECUTE SETL.
            WHENEVER I(A5).NE.$($.AND.A4.NE.1
            ERRO  =18
            TRANSFER TO S60
            END OF CONDITIONAL
            I(A6)=$ .($
            OUT(OUT)=A6
            WHENEVER FL.E.1
            I(A6)=$ $
            FL=0
            OUT=OUT-1
            END OF CONDITIONAL
            OUT=OUT+1
            A4=A6+1
S93C        OTHERWISE
            WHENEVER I.E.RET
            I(A4)=RETN
            OUT(OUT)=A4
            OUT=OUT+1
            THROUGH S93D, FOR A7=A4+1,1,A7.E.A6
            OUT(OUT)=-A7
S93D        OUT=OUT+1
            END OF CONDITIONAL
            WHENEVER A6-A4.G.6, TRANSFER TO S600
            WHENEVER FUN.E.1
            ARG(ARG+1)=STACON.V.BNBCD.(FINDN.($*$))
            ARG(ARG+2)=I
            WHENEVER I(A4).E.$I$.OR.I(A4).GE.$N$.AND.I(A4).LE.$J$
            EXECUTE FINDIN.(ARG(ARG+1),S603)
            END OF CONDITIONAL
            I(A4)=ARG(ARG+1)
            OUT(OUT)=A4
            OUT=OUT+1
            ARG=ARG+2
            WHENEVER ARG.G.100
            ERRO  =19
            TRANSFER TO S60
            END OF CONDITIONAL
            I(A4+1)=I(A6)
            A4=A4+2
            OR WHENEVER FUN.E.2
            THROUGH S95, FOR A7=2,2,A7.G.ARG
            WHENEVERARG(A7).E.I
            I(A4)=ARG(A7-1)
            OUT(OUT)=A4
            OUT=OUT+1
            I(A4+1)=I(A6)
            A4=A4+2
            TRANSFER TO S92A
S95         END OF CONDITIONAL
            WHENEVER I(A4).E.$I$.OR.I(A4).GE.$N$.AND.I(A4).LE.$J$,
          1  EXECUTE FINDIN.(I)
            A4=A6+1
            WHENEVER A5.G.EXTT.AND..NOT.OPCHK.(I(A6)),A4=A4-1
            OTHERWISE
            WHENEVER I(A4).E.$I$.OR.I(A4).GE.$N$.AND.I(A4).LE.$J$
            EXECUTE FINDIN.(I)
            END OF CONDITIONAL
            WHENEVER I(A6).NE.$($.AND.(I(A4-1).NE.$,$.AND.I(A4-1).NE.
          1  $ .($.OR.I(A6).NE.$,$.AND.I(A6).NE.$)$.OR..NOT.LEVT)
            ZZXQ1=S95A
            EXECUTE DIMFN.(I,ZZXQ1)
            OUT(OUT)=A6-1
            OUT=OUT+1
            I(A6-1)=$000(1)$.V.((I(A6-1)/4096)*4096)
S95A        CONTINUE
            END OF CONDITIONAL
            A4=A6+1
S92A        WHENEVER A5.G.EXTT,A4=A4-1
            END OF CONDITIONAL
            END OF CONDITIONAL
            A3=A5
            WHENEVER A5.LE.EXTT.AND.I(A6).E.$)$,EXECUTE LDN.
            END OF CONDITIONAL
S92         CHECK =0
            WHENEVER ASTOUT.E.AS2
            CCT=A4-1
            OUT(OUT)=700
            OTHERWISE
            THROUGH S96,FOR A7=A4  ,1,A7.G.EXTT
            OUT(OUT)=-A7
S96         OUT=OUT+1
            OUT=OUT-1
            END OF CONDITIONAL
            TRANSFER TO STOUT
S2          CONTINUE
            TRANSFER TO PR
S3          CONTINUE
            TRANSFER TO TS3
S60         EXECUTE ERRP.(ERRO)
S30         EXECUTE SUBST.
            BOOLEAN LEVT
            DIMENSION LEV(20)
            INTERNAL FUNCTION (X)
          R
          R  DGT.
          R
            ENTRY TO DGT.
            THROUGH SD,FOR VALUES OF XX=$0$,$1$,$2$,$3$,$4$,$5$,$6$,$7$,
          1   $8$,$9$
SD          WHENEVER X.E.XX,FUNCTION RETURN 1B
            FUNCTION RETURN 0B
            END OF FUNCTION
            INTERNAL FUNCTION
          R
          R  SETL.
          R
            ENTRY TO SETL.
              LEV=LEV+1
              LEV(LEV)=LEVEL
               LEVT=1B
            FUNCTION RETURN
          R
          R  LUP.
          R
            ENTRY TO LUP.
              LEVEL = LEVEL +1
               LEVT=0B
            FUNCTION RETURN
          R
          R  LDN.
          R
            ENTRY TO LDN.
            WHENEVER LEV(LEV).E.LEVEL,LEV=LEV-1
            WHENEVER LEV.E.-1
              ERRO=17
            TRANSFER TO S60
            END OF CONDITIONAL
            LEVEL= LEVEL-1
            WHENEVER LEV(LEV).E.LEVEL
            LEVT=1B
            OTHERWISE
            LEVT=0B
            END OF CONDITIONAL
            FUNCTION RETURN
          R
          R  LEVIN.
          R
            ENTRY TO LEVIN.
            LEV=0
            LEVEL=0
            LEVT=0B
            FUNCTION RETURN
            END OF FUNCTION
            END OF FUNCTION
