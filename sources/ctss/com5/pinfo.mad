          R PINFO COMMAND - PETER R. BOS  11/67
          R
          R USAGE...
          R    PINFO NAME1 -NAME2-
          R PINFO WILL SWITCH TO THE PUBLIC FILE AND PRINT
          R FILE NAME1 NAME2. DEFAULT FOR NAME2 IS 'INFO'.
          R
            NORMAL MODE IS INTEGER
            DIMENSION BUF1(0), BUF2(0), LINE(15)
            VECTOR VALUES FNS=777777777777K
          R
            DEFINE UNARY OPERATOR .CAL., PRECEDENCE SAME AS =
            MODE STRUCTURE .CAL.1 TO 1
       CAL      B
       OUT      LAC
       END
            DEFINE UNARY OPERATOR .LDQ., PRECEDENCE SAME AS =
            MODE STRUCTURE .LDQ.1 TO 1
       LDQ      B
       OUT      MQ
       END
          R
            SETBRK.(CHN)
            NM1=COMARG.(1)
            WHENEVER NM1.E.FNS
              PRINT COMMENT $'PINFO' NAME1 -NAME2-$
              PRINT COMMENT $IF NAME2 OMITTED, INFO ASSUMED$
              CHNCOM.(0)
            END OF CONDITIONAL
            TMP=COMARG.(2)
            WHENEVER TMP.NE.FNS, NM2=TMP
            VECTOR VALUES NM2=$  INFO$
          R
            TSSFIL.
            GETBUF.(BUF1,431)
            GETBUF.(BUF2,431)
            BFOPEN.($R$,NM1,NM2,BUF1(BUF1+431),BUF2(BUF2+431),-0,ERR)
            BFREAD.(NM1,NM2,LMARK...1,EOF,TMP,ERR)
            WHENEVER LMARK.A.777777K6.E.777777K6, TRANSFER TO RDLINE
            LINE(14)=LMARK
            BFREAD.(NM1,NM2,LINE(13)...13,EOF,TMP,ERR)
            TRANSFER TO PCARD
          R
RDCARD      BFREAD.(NM1,NM2,LINE(14)...14,EOF,TMP,ERR)
PCARD       WRFX.(LINE(14)...12)
            TRANSFER TO RDCARD
RDLM        BFREAD.(NM1,NM2,LMARK...1,EOF,TMP,ERR)
            WHENEVER LMARK.A.777777K6.NE.777777K6, TRANSFER TO RDLM
RDLINE      LMARK=LMARK.A.17K
            BFREAD.(NM1,NM2,LINE(LMARK)...LMARK,EOF,TMP,ERR)
            WRFX.(LINE(LMARK)...LMARK)
            TRANSFER TO RDLM
          R
EOF         BFCLOS.(NM1,NM2,ERR)
CHN         USRFIL.
            CHNCOM.(0)
          R
ERR         .CAL.$PRNTER$
            .LDQ.400000000071K
            NEXCOM.
          R
            END OF PROGRAM
