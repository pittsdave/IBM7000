PRFIB     R     PRINT ALL PENDING FIB JOBS FOR USER
          R
          R
          R MODIFIED FOR '(ALL)' ARGUMENT BY ROGER ROACH 5/18/67
          R MODIFIED FOR 'PROB PROG' ARGUMENTS BY JIM BIXBY 12/26/67
          R ABOVE OPTIONS RESTRICTED TO M1416 BY P.R.BOS 1/2/68
          R
            NORMAL MODE IS INTEGER
            DIMENSION CARD (13), WHO (1),  BUFF1 (431)
          R
          R     PRESET DECLARATIONS.
          R
            VECTOR VALUES FIBJOB = $FIBJOB$
            VECTOR VALUES   FILE = $  FILE$
            VECTOR VALUES  C0056 = $ C0056$
          R VECTOR VALUES FIBMON = $ 99995$
            VECTOR VALUES FIBMON = $FIBMON$
            VECTOR VALUES  FENCE = 777777777777K
            VECTOR VALUES    MZE = -0
            VECTOR VALUES     CT = 0
            VECTOR VALUES  FIRST = 1B
            VECTOR VALUES    ALL = $ (ALL)$
            VECTOR VALUES  M1416 = $ M1416$
          R
          R
            EQUIVALENCE  (PROBN, WHO (1)), (PROGN, WHO)
            EQUIVALENCE  (JPROB, CARD (13)), (JPROG, CARD (11)),
          1      (ESTTIM, CARD (9)), (DRB4, CARD (8)),
          2      (JNAME, CARD (10))
          R
          R
          R
          R     PICK UP PARAMETERS
          R
            PRMESS.
            WHOAMI. (WHO (1) ... 2)
            WHENEVER WHO(1) .NE. M1416
              ARG = FENCE
            OTHERWISE
              ARG = COMARG.(1)
              ARG2 = COMARG.(2)
              WHENEVER ARG.NE.FENCE.AND.ARG2.NE.FENCE
                PROBN = ARG
                PROGN = ARG2
              END OF CONDITIONAL
            END OF CONDITIONAL
          R
            FERRTN. (ERROR)
          R
          R
          R     LINK TO FIBJOB FILE AND SEARCH FOR USER'S JOB.
          R
            LINK. (FIBJOB, FILE, C0056, FIBMON, MZE, MZE, 124K, ERR1)
            BFOPEN. ($R$, FIBJOB, FILE, BUFF1 (431), MZE, MZE, ERR1)
            UNLINK. (FIBJOB, FILE)
          R
READ        BFREAD. (FIBJOB, FILE, CARD (13) ... 14, CLOS, CT, ERROR)
          R
            WHENEVER ARG .E. ALL, TRANSFER TO ONWARD
            WHENEVER JPROB .NE. PROBN .OR. JPROG .NE. PROGN,
          1      TRANSFER TO READ
          R
          R
          R     CARD FOUND.
          R
ONWARD      WHENEVER FIRST
              WHENEVER ARG.E.ALL,PRMESA.($ PROB   PROG$,BL)
              PRMESS. (HEADER ... 5)
              VECTOR VALUES HEADER = $JOBNAM  LIMIT   DATE  TIME$
              PRMESS.
              FIRST = 0B
            END OF CONDITIONAL
          R
            FTIME. (DRB4, DATE, TIME)
            DATEX = ((DATE.A.7777K8)).V.((DATE.A.7777K4).RS.6).V.
          1   $00/00 $
            WHENEVER ARG.E.ALL,PRMESA.(JPROB,JPROG,BL)
            PRMESS. (JNAME, BZEL. (DERBC. (ESTTIM)), BL, DATEX, TIME)
            VECTOR VALUES BL = 6060 6057 5757K
            TRANSFER TO READ
          R
CLOS        BFCLOS. (FIBJOB, FILE, ERROR)
          R
            WHENEVER FIRST
              PRMESS. (NOJOB ... 3, PROBN, PROGN, $.$)
              VECTOR VALUES NOJOB = $ NO FIB JOBS FOR$
            END OF CONDITIONAL
          R
            PRMESS.
          R
            CHNCOM. (0)
          R
          R
          R     FILE SYSTEM ERROR ENCOUNTERED.
          R
ERR1        UNLINK. (FIBJOB, FILE)
ERROR       DSKER.
          R
          R
            END OF PROGRAM
