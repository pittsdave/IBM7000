       REM
       ENTRY   QED
       ENTRY   TYPE0    
       EXTERN  FREEDT   
       EXTERN  INITDT   
       EXTERN  ZCMD     
       EXTERN  CANON    
       EXTERN  SETOPS   
       EXTERN  INESC    
       EXTERN  OUTESC   
       EXTERN  EKEDIT   
       EXTERN  CNVTBL   
       REM
       EVEN 
DIVIDE MACRO   A,B
       ZAC  
       DVP     =A 
       SLW     BUF1+B   
DIVIDE END
       IFF     XBUILD,1,1                                        DGPHACK
VNVTBL TRA     CNVTBL                                            DGPHACK
       REM
QED    LMTM 
       TSX     WHOAMI,4 
       PZE     BUF1,,4  
       CAL     BUF1+1   
       SLW     CRC6+1   
       SLW     CRC7+1   
       CAL     =O100000 
       IFF     XBUILD,0,1                                        DGPHACK
       ORS     CNVTBL
       IFF     XBUILD,1,1                                        DGPHACK
       ORS     VNVTBL                                            DGPHACK
       TSX     INITDT,4 
       LAS     =H(FIB)  
       TRA     *+2
       TRA     INAR     
       TSX     OPEN,4   
       PAR     =HR
       PAR     =H  USER 
       PAR     =HPROFIL 
       PAR     =-0
       PAR     =-0
       PAR     INAR     
       PAR     COPY     
       TSX     RDWAIT,4 
       PAR     =H  USER 
       PAR     =HPROFIL 
       PAR     =404     
       PAR     PPPMT,,2 
       PAR     *+2
       PAR     COPY     
       TSX     CLOSE,4  
       PAR     =H  USER 
       PAR     =HPROFIL 
       TSX     PERMIT,4 
       PAR     PPPMT    
       PAR     PPPMT+1  
       STL     PMTFLG   
INAR   TSX     GETMEM,4 
       ARS     2  
       ADD     =1 
       ALS     2  
       PAC     ,5 
       PAC     ,1 
       TXI     *+1,1,-5 
       SXD     IN2,1    
       ARS     8  
       ADD     =1 
       ALS     8  
       PAC     ,1 
       TSX     SETMEM,4 
       STZ     04,1     
IN1    TXI     *+1,1,4  
       PCA     ,1 
       STO     04,1     
       STZ     03,1     
       STZ     02,1     
       STZ     01,1     
IN2    TXL     IN1,1,** 
       SCA     BUFER,5  
       STZ     BBASE    
       CAL     =O60     
       TSX     BOPEN,4  
       SCA     INPUT,5  
       SCA     IBASE,5  
       CAL     00,5     
       PAC     ,5 
       STZ     RECLEV   
       AXT     00,7     
       TSX     FREEDT,4 
       TSX     SETFUL,4 
       TSX     SETBRK,4 
       PZE     INTRUP   
       TSX     WRFLX,4  
       PZE     MES2,,1  
       TSX     GETCOM,4 
       PZE     1  
       LAS     =O777777777777 
       TRA     *+2
       TRA     ADDRES   
       SLW     COPY     
       CAL     =O30     
       SLW     BUF6+298 
       CAL     =O60     
       SLW     BUF6+297 
       CAL     =O12     
       SLW     BUF6+296 
       AXT     1,7
       CAL     =O56     
       TSX     BOPEN,4  
       CAL     ZER
       SLW     ADDR2    
       TSX     INSERT,4 
       PZE     IN3
       CAL     =O60     
       TSX     BOPEN,4  
       TSX     SETFL,4  
       AXC     CRD2-1,4 
       SCA     FC1,4    
       AXC     1,2
       TRA     SETFC+2  
IN3    SCA     PM5,4    
       SCA     PM5+1,2  
PM1    AXC     **,2     
       TXH     PM4,2,0  
       CLA     =O142    
       SLW     BUF3     
       CAL     =O60     
       SLW     BUF3+1   
       CAL     =O12     
       SLW     BUF3+2   
       CAL     =O162    
       SLW     BUF3+3   
       CAL     =O141    
       SLW     BUF3+4   
       CAL     =O40     
       SLW     BUF3+5   
       TSX     GETCOM,4 
       PZE     1  
       SLW     FILNAM   
       TSX     PMN,4    
       CAL     =O40     
       SLW     BUF3+5,2 
       CAL     =H   QED 
       SLW     FILNAM+1 
       TSX     PMN,4    
       CAL     =O30     
       SLW     BUF3+6,2 
       CAL     =O60     
       SLW     BUF3+7,2 
       CAL     =O12     
       SLW     BUF3+8,2 
       TXI     *+1,2,-3 
       TSX     GETCOM,4 
PM2    PZE     2  
       LAS     =O777777777777 
       TRA     *+2
       TRA     PM3
       TSX     PMN,4    
       CAL     PM2
       ACL     =1 
       SLW     PM2
       TRA     PM2-1    
PM3    CAL     =O3
       SLW     BUF3+6,2 
       AXC     0,2
PM4    CAL     BUF3,2   
       TXI     *+1,2,-1 
       SCA     PM1,2    
PM5    AXC     **,4     
       AXC     **,2     
       TRA     1,4
PMN    XCL  
       ZAC  
       LGL     6  
       XCL  
       ORA     =O55     
       XCL  
       LAS     =O60     
       TRA     *+2
       TRA     PMN+1    
       PAC     ,1 
       IFF     XBUILD,0,1                                        DGPHACK
       CAL*    CNVTBL
       IFF     XBUILD,1,1                                        DGPHACK
       CAL*    VNVTBL                                            DGPHACK
       ARS     16 
       ANA     =O177    
       SLW     BUF3+6,2 
       TXI     *+1,2,-1 
       LAS     =O12     
       TRA     PMN+1    
       TRA     1,4
       TRA     PMN+1    
MES2   BCI     1,0Q0E0D 
       ORG     QED+432  
INTRUP STL     INTFLG   
       SLW     AC 
       STO     AC+1     
       STQ     AC+2     
       SCA     INT1,4   
       TSX     SETFUL,4 
       TSX     GETBRK,4 
       STA     INT1+1   
       TSX     SETBRK,4 
       PZE     INTRUP   
       CLA     AC+1     
       ORA     AC 
       LDQ     AC+2     
       ZET     RFLG     
       TRA     INT2     
INT1   AXC     **,4     
       TRA     ** 
INT2   NZT     TFLG     
       TRA     TYPE4    
       STZ     RFLG     
       STZ     VFLG     
       LAC     GC5,4    
       CAL     =O12     
       TRA     GC11     
MORMEM SCA     MM1+1,4  
       TSX     GETMEM,4 
       PAC     ,5 
       PAC     ,4 
       TXI     *+1,4,-5 
       SXD     MM1,4    
       ADD     =O400    
       CAS     =O100000 
       TRA     MM2
       TSX     MM4,4    
       STA     *+2
       TSX     SETMEM,4 
       AXC     **,4     
       STZ     -04,4    
MM3    TXI     *+1,4,4  
       PCA     ,4 
       STO     -04,4    
       STZ     -03,4    
       STZ     -02,4    
       STZ     -01,4    
MM1    TXL     MM3,4,** 
       AXC     **,4     
       TRA     1,4
MM2    TSX     DORMNT,4 
       HTR     0  
MM4    SCA     *+3,4    
       TSX     WRFLX,4  
       PZE     MES3,,1  
       AXC     **,4     
       CAL     =O77774  
       STL     INTFLG   
       TRA     1,4
MES3   BCI     1,0M0E0M 
ADDRES ZET     INTFLG   
       TRA     TYPE4    
       STZ     DFLG     
       STZ     AFLG     
       STZ     BFLG     
       TSX     GETCHA,4 
       CAL     ZER
       SLW     ADDR3    
       IFF     XBUILD,0,1                                        DGPHACK
ADR1   CAL*    CNVTBL
       IFF     XBUILD,1,1                                        DGPHACK
ADR1   CAL*    VNVTBL                                            DGPHACK
       ARS     32 
       PAC     ,4 
       TRA     *+1,4    
       TRA     AUNKN    
       TRA     APLUS    
       TRA     AMINUS   
       TRA     ASRCH    
       TRA     ANUMB    
       TRA     ALAST    
       TRA     ACURNT   
       TRA     ABLNK    
       TRA     AANUM    
ABLNK  TSX     GETCHA,4 
       TRA     ADR1     
APLUS  TRA     ADV2     
AMINUS ZET     DFLG     
       TRA     *+3
       STL     DFLG     
       TRA     ADV2     
       STZ     DFLG     
       TRA     ADV2     
ASRCH  CAL     =O57     
       SLW     SLCHR    
       STZ     SUBFLG   
       TSX     COMPIL,4 
       TRA     SRC3     
       PZE     SRC2     
       LAC     DOT,4    
       CAL     00,4     
       STA     SRC1     
       SCD     SRC1,4   
       TSX     SETBC,4  
SRC1   PZE     **,,**   
       STL     IFLG     
       STZ     JFLG     
SRC2   STZ     FFLG     
       NZT     IFLG     
       TRA     *+3
       STZ     IFLG     
       STL     FFLG     
       TSX     GETBC,4  
       PAC     ,1 
       TXL     *+2,1,-4 
       TXL     SRC4,1,-3
       AXC     BUF2+50-1,2    
       TXL     BUF3,1,-11     
       TXH     BUF3,1,-10     
       STL     IFLG     
       TRA     BUF3     
SRC3   LAC     BC2,2    
       NZT     IFLG     
       TRA     *+3
       CAL     00,2     
       PDC     ,2 
       TSX     BC4,4    
       TSX     GETCHA,4 
       TRA     AREL     
SRC4   NZT     RECLEV   
       TRA     TYPE1    
       TSX     BC4,4    
       TRA     ADDRES   
ALAST  LAC     ZER,2    
       CAL     00,2     
       PDC     ,2 
       TSX     GETCHA,4 
       TRA     AREL     
ACURNT LAC     DOT,2    
       TSX     GETCHA,4 
       TRA     AREL     
NUMB   TXI     *+1,1,48 
       SCA     NUMB1,1  
       TSX     GETCHA,4 
       IFF     XBUILD,0,1                                        DGPHACK
       CAL*    CNVTBL
       IFF     XBUILD,1,1                                        DGPHACK
       CAL*    VNVTBL                                            DGPHACK
       ARS     32 
       SUB     =4 
       TNZ     NUMB1    
       LAC     NUMB1,4  
       PCA     ,4 
       XCA  
       MPY     =10
       XCA  
       TXI     *+1,1,48 
       SCA     NUMB1,1  
       ACL     NUMB1    
       STA     NUMB1    
       TRA     NUMB+2   
NUMB1  AXC     **,4     
       TRA     1,2
ANUMB  TSX     NUMB,2   
       LAC     ADDR3,2  
       CLA     00,2     
       ZET     DFLG     
       TRA     NUMB3    
NUMB2  TXL     NUMB4,4,0
       PAC     ,2 
       CLA     00,2     
       TMI     TYPE5    
       TXI     NUMB2,4,1
NUMB3  TXL     NUMB4,4,0
       TMI     TYPE5    
       PDC     ,2 
       CLA     00,2     
       TXI     NUMB3,4,1
NUMB4  SCA     ADDR3,2  
       TRA     ADV3     
AANUM  TSX     GETCHA,4 
       IFF     XBUILD,0,1                                        DGPHACK
       CAL*    CNVTBL
       IFF     XBUILD,1,1                                        DGPHACK
       CAL*    VNVTBL                                            DGPHACK
       ARS     32 
       SUB     =4 
       TNZ     TYPE2    
       TSX     NUMB,2   
       LAC     ZER,2    
       TXL     AREL,4,0 
       CAL     00,2     
       PAC     ,2 
AAN1   PCA     ,4 
       LAS     03,2     
       TRA     *+3
       TRA     AREL     
       TRA     TYPE1    
       CAL     00,2     
       PAC     ,2 
       CLA     00,2     
       TPL     AAN1     
       TRA     TYPE1    
AREL   LAC     ADDR3,4  
       ZET     DFLG     
       TRA     REL2     
       CLA     00,4     
       TMI     REL3     
REL1   CLA     00,2     
       TMI     REL4     
       PDC     ,2 
       CLA     00,4     
       PAC     ,4 
       TPL     REL1     
       TRA     TYPE5    
REL2   CLA     00,2     
       TMI     REL4     
       PDC     ,2 
       CLA     00,4     
       PDC     ,4 
       TPL     REL2     
       TRA     TYPE5    
REL3   SCA     ADDR3,2  
       TRA     ADV3     
REL4   SCA     ADDR3,4  
       TRA     ADV3     
ADV2   TSX     GETCHA,4 
       STL     AFLG     
       TRA     ADR1     
ADV3   STZ     DFLG     
       STL     AFLG     
       TRA     ADR1     
AUNKN  ZET     BFLG     
       TRA     UNK1     
       CAL     ADDR3    
       SLW     ADDR1    
       SLW     ADDR2    
       STL     BFLG     
       TRA     UNK2     
UNK1   CAL     ADDR2    
       SLW     ADDR1    
       CAL     ADDR3    
       SLW     ADDR2    
UNK2   PCA     ,1 
       SUB     =O54     
       TZE     UNK3     
       SUB     =O17     
       TNZ     COMAND   
       CAL     ADDR3    
       SLW     DOT
UNK3   TSX     GETCHA,4 
       CAL     ZER
       SLW     ADDR3    
       TRA     ADR1     
TYPE   MACRO   A  
       ZET     TRAP'A   
       TSX     TRAP,4   
       CAL     =H0'A'0 0
       TRA     ILLADD-1 
TYPE   END
TYPE0  CAL     =H000 0  
       TRA     ILLADD-1 
TYPE1  TYPE    1  
TYPE2  TYPE    2  
TYPE3  TYPE    3  
TYPE4  TYPE    4  
TYPE5  TYPE    5  
TYPE6  TYPE    6  
TYPE7  TYPE    7  
TYPE8  TYPE    8  
TYPE9  TYPE    9  
TRAP1 
TRAP2 
TRAP3 
TRAP4 
TRAP5 
TRAP6 
TRAP7 
TRAP8 
TRAP9 
TRAP   CAL*    -1,4     
       STZ*    -1,4     
       SLW     TRP1     
       STZ     INTFLG   
       ZET     TFLG     
       TSX     GC11+2,4 
       LAC     BUFER,4  
       CAL     03,4     
       SLW     TRP2     
       CAL     TRP1     
       TSX     BOPEN,4  
       LAC     ZER,4    
       CAL     00,4     
       STO     *+2
       TSX     SETBC,4  
       PZE     **,,**   
       CAL     TRP2     
       TSX     BOPEN,4  
       TRA     ADDRES   
TRP2   PZE     ** 
TRP1   PZE     ** 
       SLW     MES1+1   
ILLADD STZ     INTFLG   
       NZT     TRCFLG   
       TRA     IL2
       LAS     =H040 0  
       TRA     *+2
       TRA     IL2
       NZT     RECLEV   
       TRA     IL2
       TSX     PRINT,4  
       PZE     GETBC    
IL2    TSX     SETFUL,4 
       ZET     FILFLG   
       TSX     CLOS,4   
       NZT     RECLEV   
       TRA     IL1
       STZ     RECLEV   
       LAC     INPUT,4  
       PCA     ,5 
       SLW     00,4     
       LAC     IBASE,4  
       CAL     00,4     
       PAC     ,5 
       SCA     INPUT,4  
IL1    TSX     WRFLX,4  
       PZE     MES1,,2  
       AXT     00,7     
       TRA     ADDRES   
MES1   OCT     17501320156,0  
BLINE  SCA     *+3,4    
       TSX     WRFLX,4  
       PZE     *+3,,1   
       AXC     **,4     
       TRA     1,4
       OCT     006000600060   
COMPIL SCA     CEOF,4   
       SCA     OPONST,0 
       AXC     00,3     
       AXC     100,2    
       CAL     =1 
       SLW     BUF2     
       STZ     IFLG     
       STZ     JFLG     
ADVANC ZET     IFLG     
       TRA     AN1
       TSX     GETCHA,4 
       LAS     SLCHR    
       TRA     *+2
       TRA     SL1
       ZET     CPLFLG   
       STZ     NSCHA    
       STZ     CPLFLG   
IO1    ZET     VFLG     
       TRA     AD1
       TXL     CH1,1,-126     
       TXL     RP0,1,-125     
       TXL     OR1,1,-124     
       TXL     LP1,1,-123     
       TXL     CH1,1,-43
       TXL     ST1,1,-42
       TXL     RP1,1,-41
       TXL     LP1,1,-40
AD1    TXL     CH1,1,-11
       TXL     CILAD,1,-10    
CH1    PCD     ,1 
IO2    ZET     VFLG     
       SSM  
       STO     COPY     
       AXC     7,1
       ZET     JFLG     
       TRA     CH2
       STL     JFLG     
       TRA     OPONST   
LP1    AXC     8,1
       NZT     JFLG     
       TRA     OPONST   
       STZ     JFLG     
CH2    SCA     COPY,1   
       AXC     5,1
       STL     IFLG     
       TRA     OPONST   
OR1    AXC     4,1
       NZT     JFLG     
       TRA     CILAD    
       STZ     JFLG     
       TRA     OPONST   
AN1    LAC     COPY,1   
       STZ     IFLG     
       TRA     OPONST   
RP0    TSX     GETCHA,4 
       SLW     COPY     
       AXC     2,1
       LAS     SLCHR    
       TRA     RP2
       TRA     CILAD    
       TRA     RP2
RP1    AXC     2,1
       STZ     COPY     
       TRA     RP2
ST1    AXC     6,1
       TRA     RP2
SL1    ZET     VFLG     
       TRA     CH1
       AXC     1,1
RP2    ZET     JFLG     
       TRA     OPONST   
       LAC     CPLFLG,3 
       TXH     CEOF,3,0 
CILAD  TRA     TYPE3    
OPONST AXC     **,4     
       SXD     OP1,1    
OP0    CAL     BUF2,4   
       PAC     ,1 
OP1    TXL     OP2,1,** 
       LXD     OP1,1    
       TXH     *+2,1,-8 
       TXI     *+1,1,5  
       CLA     COPY     
       STO     BUF2+1,4 
       PCA     ,1 
       STA     BUF2+1,4 
       TXI     *+1,4,-1 
       SCA     OPONST,4 
       TRA     ADVANC   
OP2    TRA     *,1
       TRA     CEOF     
       TRA     CRTP     
       TRA     CLFP     
       TRA     COR
       TRA     CAND     
       TRA     CSTR     
       TRA     CCHR     
CEOF   AXC     **,4     
       SCA     CNO1,0   
       SCA     NO1,0    
       CAL     1,4
       SLW     BUF3,3   
       SCA     CPLFLG,3 
       CAL     2,4
       STA     XCH2     
       CAL     TRA3     
       SLW     BUF2+50+1
       TRA     3,4
CRTP   TRA     CILAD    
CLFP   LXD     OP1,1    
       TXH     CILAD,1,-2     
       TXI     *+1,4,1  
       SCA     OPONST,4 
       NZT     COPY     
       TRA     ADVANC   
       LAC     NSCHA,4  
       CAL     COPY     
       SLW     SCHARS,4 
       TXI     *+1,4,-1 
       TXL     TYPE0,4,-9     
       SCA     NSCHA,4  
       SLW     BUF3+1,3 
       CAL     TSX5     
       SLW     BUF3,3   
       CAL     TSX4     
       SLW     BUF3+2,3 
       CAL     BUF2,2   
       PAC     ,4 
       CAL     BUF3,4   
       SLW     BUF3+3,3 
       PCA     ,3 
       ACL     TRA1     
       ACL     =2 
       SLW     BUF3,4   
       TXI     ADVANC,3,-4    
COR    TXI     *+1,4,1  
       SCA     OPONST,4 
       CAL     BUF2+1,2 
       PAC     ,4 
       CAL     BUF3,4   
       SLW     BUF3+2,3 
       PCA     ,3 
       ACL     TRA1     
       ACL     =1 
       SLW     BUF3,4   
       ACL     =3 
       SLW     BUF3,3   
       XCL  
       CAL     BUF2,2   
       PDC     ,4 
       TXL     COR1,4,0 
       CAL     BUF2+1,2 
       LAS     =O100000 
       TRA     *+5
       TRA     *+4
       PCD     ,4 
       STD     BUF2+1,2 
       TRA     COR1     
       ARS     18 
       ACL     TRA1     
       SLW     BUF3,4   
COR1   CAL     BUF2,2   
       PAC     ,4 
       CAL     BUF3,4   
       SLW     BUF3+3,3 
       STQ     BUF3,4   
       CAL     TSX2     
       SLW     BUF3+1,3 
       TXI     *+1,2,-1 
       LAC     OPONST,4 
       TXI     OP0,3,-4 
CAND   CAL     BUF2,2   
       LAS     =O100000 
       TXI     CAN1,2,-1
       TXI     CAN1,2,-1
       CAL     BUF2+1,2 
       LAS     =O100000 
       ANA     =O77777  
       SLW     BUF2+1,2 
       TXI     CAN1,2,-1
CAN1   TXI     OP0,4,1  
CSTR   TXI     *+1,4,1  
       SCA     OPONST,4 
       CLA     BUF2,2   
       TMI     CILAD    
       PDC     ,4 
       TXL     *+4,4,0  
       CAL     TRA2     
       SLW     BUF3,4   
       CAL     BUF2,2   
       PAC     ,4 
       CAL     BUF3,4   
       SLW     BUF3+1,3 
       SLW     BUF3+4,3 
       PCA     ,3 
       ACL     TRA1     
       ACL     =3 
       SLW     BUF3,4   
       ACL     =3 
       SLW     BUF3+2,3 
       SLW     BUF3+5,3 
       PCD     ,3 
       ACL     =O5000000
       STD     BUF2,2   
       CAL     TSX2     
       SLW     BUF3,3   
       SLW     BUF3+3,3 
       LAC     OPONST,4 
       TXI     OP0,3,-6 
CCHR   SCA     OPONST,4 
       PCA     ,3 
       SLW     BUF2-1,2 
       ACL     TRA1     
       ACL     =1 
       SLW     BUF3,3   
       CLA     BUF2,4   
       PDC     ,1 
       TMI     CCH1     
       TXL     CCH1,1,-95     
       TXL     CCHZ,1,-94     
       TXL     CCH1,1,-47     
       TXL     CCHC,1,-46     
       TXL     CCH1,1,-37     
       TXL     CCHL,1,-36     
CCH1   CAL     TSX1     
       SLW     BUF3+3,3 
       AXC     00,4     
CCH2   PXD     ,1 
       ORA     TXH1     
       SLW     BUF3+2,3 
       TXL     *+4,1,0  
       TXI     *+1,1,-1 
       PXD     ,1 
       ORA     TXL1     
       SLW     BUF3+1,3 
       TXL     *+2,4,0  
       TXI     CCH3,3,-3
       TXI     CCH3,3,-4
CCH3   TXI     *+1,2,1  
       LAC     OPONST,4 
       TXI     OP0,4,1  
CCHZ   CAL     NZT1     
       SLW     BUF3+1,3 
       CAL     TRA2     
       SLW     BUF3+2,3 
       CLA     BUF2-1,2 
       SSM  
       STO     BUF2-1,2 
       TXI     CCH3,3,-3
CCHC   PCA     ,3 
       ACL     TXL2     
       SLW     BUF3+1,3 
       AXC     10,4     
       PXD     ,4 
       ACL     TXL1     
       SLW     BUF3+2,3 
       CAL     TSX1     
       SLW     BUF3+3,3 
       TXI     CCH3,3,-4
CCHL   AXC     10,1     
       CLA     BUF2-1,2 
       SSM  
       STO     BUF2-1,2 
       TSX     CCH2,4   
START  NZT     SUBFLG   
       TRA     1,4
       SCA     START2,4 
       SCA     START1,5 
       CAL     -1,4     
       SLW     1,5
       CAL     1,2
       PDC     ,4 
       TXL     START0,4,0     
START4 CAL     1,4
       LAS     1,5
       TRA     *+2
       TRA     START3   
       CAL     0,4
       PAC     ,4 
       TXH     START4,4,0     
START0 CAL     0,5
       PAC     ,5 
       TXH     *+2,5,0  
       TSX     MORMEM,4 
       CAL     1,2
       PDC     ,4 
       PCA     ,4 
START1 AXC     **,4     
       SLW     0,4
       PCD     ,4 
       STD     1,2
START3 CAL     BUF2+50  
       SLW     2,4
       STZ     3,4
START2 AXC     **,4     
       TRA     1,4
FINISH NZT     SUBFLG   
       TRA     4,4
       SCA     FINIS3,4 
       CAL     1,4
       SLW     COPY     
       CAL     1,2
       PDC     ,4 
FINIS1 TXL     TYPE0,4,0
       CAL     1,4
       LAS     COPY     
       TRA     *+2
       TRA     FINIS2   
       CAL     0,4
       PAC     ,4 
       TRA     FINIS1   
FINIS2 CAL     BUF2+50  
       SLW     3,4
FINIS3 AXC     **,4     
       TRA     4,4
FAIL   NZT     SUBFLG   
       TRA     2,2
       CAL     1,2
       PDC     ,4 
       TXL     FAIL1+1,4,0    
       SCA     FAIL1,4  
       CAL     0,4
       TZE     *+3
       PAC     ,4 
       TRA     *-3
       PCA     ,5 
       SLW     0,4
FAIL1  AXC     **,5     
       TRA     2,2
NODE   PCA     ,4 
       ACL     TSX3     
NO1    AXC     **,4     
       TXL     NO2,4,0  
       LAS     BUF2-2,4 
       TXI     *-2,4,2  
       TRA     NO3
       TXI     *-4,4,2  
NO2    LAC     NO1,4    
       TXI     *+1,4,-2 
       TXL     TYPE0,4,-50    
       SCA     NO1,4    
       SLW     BUF2-2,4 
       CAL     1,2
       SLW     BUF2-1,4 
       TRA     2,2
NO3    CAL     BUF2-1,4 
       ANA     =O77777  
       SLW     COPY     
       CAL     1,2
       ANA     =O77777  
       LAS     COPY     
       TRA     FAIL     
       TRA     FAIL     
       CAL     1,2
       SLW     BUF2-1,4 
       TRA     2,2
CNODE  SCA     CNO3,4   
       PCA     ,4 
       ACL     TSX3     
CNO1   AXC     **,4     
       TXI     *+1,4,-2 
       TXL     TYPE0,4,-50    
       SCA     CNO1,4   
       SLW     BUF2+50-1,4    
       CAL     TRA3     
       SLW     BUF2+50+1,4    
       CAL     1,2
       SLW     BUF2+50,4
       NZT     SUBFLG   
       TRA     CNO3     
       PDC     ,4 
       TXL     CNO3,4,0 
       PCD     ,5 
       STD     1,2
CNO4   CAL     1,4
       SLW     1,5
       CAL     2,4
       SLW     2,5
       CAL     3,4
       SLW     3,5
       SCA     CNO5,5   
       CAL     0,5
       PAC     ,5 
       TXH     *+4,5,0  
       SCA     *+2,4    
       TSX     MORMEM,4 
       AXC     **,4     
       PCA     ,5 
CNO5   SLW     ** 
       CAL     0,4
       PAC     ,4 
       TXH     CNO4,4,0 
       STZ*    CNO5     
CNO3   AXC     **,4     
       TRA     2,4
XCHG   LAC     NO1,4    
       AXC     00,2     
       SCA     NO1,0    
       TXL     *+5,4,0  
       TXI     *+1,4,2  
       DLD     BUF2,4   
       DST     BUF2+50+1,2    
       TXI     *-4,2,-2 
       CAL     TRA3     
       SLW     BUF2+50+1,2    
       SCA     CNO1,2   
XCH2   TRA     ** 
*      INSERT  1QED                                              DGPHACK
SIZ1  
SIZ2  
GETCHA STZ     VFLG     
       ZET     RECLEV   
       TRA     GETBUF   
       TXH     GC5+1,7,0
       STL     RFLG     
       STZ     SIZ1     
       SCA     GC5,4    
GC0    TSX     RDFLXA,4 
       PZE     BUF1,,100
       LDQ     BUF1     
       ZAC  
       LGL     6  
       LAS     =O77     
       TRA     *+2
       TRA     ICOM     
       AXT     00,6     
       AXT     00,7     
GC1    TIX     *+4,7,1  
       AXT     3,7
       LDQ     BUF1,6   
       TXI     *+1,6,-1 
       ZAC  
       LGL     12 
       ANA     =O177    
       PAC     ,1 
       IFF     XBUILD,0,1                                        DGPHACK
       CAL*    CNVTBL
       IFF     XBUILD,1,1                                        DGPHACK
       CAL*    VNVTBL                                            DGPHACK
       ARS     16 
       ANA     =O177    
       LXA     SIZ1,1   
       SLW     BUF6+299,1     
       TXI     *+1,1,1  
       TXH     TYPE0,1,300    
       SXA     SIZ1,1   
       LAS     =O12     
       TRA     GC1
       TRA     *+2
       TRA     GC1
       TSX     OUTESC,4 
       PAR     BUF6+299 
       PAR     SIZ1     
       PAR     BUF6+299 
       PAR     SIZ2     
       PAR     =300     
       TSX     CANON,4  
       PAR     BUF6+299 
       PAR     SIZ2     
       PAR     =300     
       TSX     EKEDIT,4 
       PAR     BUF6+299 
       PAR     SIZ2     
       PAR     =300     
       STZ     RFLG     
       STZ     CFLG     
       AXT     00,7     
GC5    AXC     **,4     
       CAL     BUF6+299,7     
       TXI     *+1,7,1  
GC4    PAC     ,1 
       ZET     CAPFLG   
       TXL     GC10,1,-97     
GC10R  ZET     TFLG     
       TRA     GC11     
       ZET     VFLG     
       TRA     GC12     
       LAS     =O31     
       TRA     GC13     
       TRA     GC14     
       LAS     =O30     
       TRA     GC15     
       TRA     GC16     
       TRA     GC15     
GC13   LAS     =O36     
       TRA     GC15     
       TRA     GC17     
GC15   LAS     V  
       TRA     *+2
       TRA     GC14     
       LAS     B  
       TRA     *+2
       TRA     GC16     
       LAS     R  
       TRA     *+2
       TRA     GC17     
GC12   LAS     =O12     
       TRA     GC6
       ZET     RECLEV   
       TRA     *+3
       AXT     00,7     
       TRA     1,4
       LAS     =O11     
       STL     CFLG     
       TRA     1,4
       STL     CFLG     
       TRA     1,4
GC6    LAS     =O40     
       STL     CFLG     
       TRA     1,4
       STL     CFLG     
       TRA     1,4
GC10   TXL     GC10R,1,-123   
       SUB     =O40     
       TXI     GC10R,1,32     
GC11   LAS     =O12     
       TRA     1,4
       TRA     *+2
       TRA     1,4
       LXA     TFLG,7   
       TXI     *+1,7,-1 
       SXA     RECLEV,7 
       LAC     IBASE,7  
       STZ     01,7     
       LXA     TFLG+1,6 
       LXD     TFLG+1,7 
       STZ     TFLG     
       TRA     GC12+2   
GC14   STL     VFLG     
       TRA     GETCHA+1 
GC16   SCA     GC8,4    
       LAC     BUFER,4  
       CAL     03,4     
       SLW     GC81     
       TSX     GETBN,4  
       TSX     BOPEN,4  
       NZT     RECLEV   
       TRA     GC16.2   
       TXL     GC16.2,7,1     
       XEC     CAL+1,7  
       XEC     ARS+1,7  
       ANA     =O177    
       SUB     =O12     
       TNZ     GC16.2   
       LXD     BC2+1,4  
       SCA     GC16.1,4 
       CLA     BC2
       SUB     GC16.1   
       TNZ     GC16.2   
       ZET     INTFLG   
       TRA     GC16.2   
       TSX     BC4,4    
       TRA     GC16.2   
GC16.1 AXC     **,6     
GC16.2 LAC     ZER,4    
       CAL     00,4     
       STO     *+2
       TSX     SETBC,4  
       PZE     **,,**   
       CAL     GC81     
       TSX     BOPEN,4  
GC8    AXC     **,4     
       TRA     GETCHA   
GC81   PZE  
GC17   LXA     RECLEV,1 
       TXI     *+1,1,1  
       SXA     TFLG,1   
       SXA     TFLG+1,6 
       SXD     TFLG+1,7 
       LAC     IBASE,1  
       CAL     01,1     
       AXT     00,7     
       STZ     RECLEV   
       TRA     GETCHA   
GETBUF SCA     *+2,4    
       TSX     GETBC,4  
       AXC     **,4     
       LAS     =O3
       TRA     GC4
       TRA     GETCHA   
       TRA     GC4
SETBC  SCA     RE1,4    
       LAC     INPUT,4  
       PCA     ,6 
       SLW     01,4     
       PCD     ,7 
       STD     01,4     
       CAL     =O200000 
       ZET     EFLG     
       STT     01,4     
       CAL     BC2
       SLW     02,4     
       CAL     BC2+1    
       STD     03,4     
       CAL     BC2+2    
       ARS     18 
       STA     03,4     
       PCA     ,5 
       STA     00,4     
       STA     INPUT    
       PCD     ,4 
       STD     00,5     
       CAL     00,5     
       PAC     ,5 
       TXH     *+2,5,0  
       TSX     MORMEM,4 
       LXA     RECLEV,4 
       TXI     *+1,4,1  
       TXL     RE1-1,4,500    
       TXI     *+1,4,10 
       SXD     *-2,4    
       TRA     TYPE7    
       SXA     RECLEV,4 
RE1    AXC     **,4     
       CAL     1,4
       PAC     ,6 
       PDC     ,7 
       SCA     BC2,6    
       CAL     01,6     
       PAC     ,6 
       SXD     BC2+1,7  
       TXI     *+1,7,-1 
       SXD     BC2+2,7  
       AXT     19,7     
       STZ     EFLG     
       TRA     2,4
GETBC  ZET     EFLG     
       TRA     BC4
       TIX     BC1,7,1  
       CAL     00,6     
       PAC     ,6 
       AXT     18,7     
BC1    XEC     CAL,7    
       XEC     ARS,7    
       ANA     =O177    
       LAS     =O12     
       TRA     1,4
       TRA     BC2
       TRA     1,4
BC2    AXC     **,6     
       TXH     *+2,6,** 
       TXH     BC3,6,** 
       CAL     00,6     
       PAC     ,6 
       SCA     BC2,6    
       CAL     01,6     
       PAC     ,6 
       AXT     19,7     
       CAL     =O12     
       TRA     1,4
BC3    STL     EFLG     
       CAL     00,6     
       STA     BC2
       CAL     =O12     
       TRA     1,4
BC4    STZ     EFLG     
       LXA     RECLEV,6 
       TXI     *+1,6,-1 
       SXA     RECLEV,6 
       LAC     INPUT,6  
       PCA     ,5 
       STA     00,6     
       LAC     INPUT,5  
       CAL     00,6     
       PDC     ,6 
       SCA     INPUT,6  
       CAL     03,6     
       STD     BC2+1    
       ALS     18 
       STD     BC2+2    
       CAL     02,6     
       SLW     BC2
       CAL     01,6     
       PDC     ,7 
       PAC     ,6 
       STT     EFLG     
       CAL     =O3
       TRA     1,4
SETFC  SCA     FC1,4    
       TSX     GETFN,4  
       SCA     GF4+1,6  
       SCA     GF4+2,7  
       CAL     OCT3,2   
       SLW     OCT3     
       CAL     SHFT,2   
       STA     GETFC+1  
       ARS     18 
       STA     GF2+1    
       CAL     RETRN,2  
       STA     GF3
       STL     VFLG     
       TSX     OPEN,4   
       PAR     =HR
       PAR     FILNAM   
       PAR     FILNAM+1 
       PAR     =O400000000000 
       PAR     =O400000000000 
       PAR     TYPE8    
       PAR     COPY     
       STL     FILFLG   
       TSX     BUFFER,4 
       PAR     FILNAM   
       PAR     FILNAM+1 
       PAR     BUF3,,432
       STZ     CPLFLG   
       TXH     FC1,2,-3 
       TSX     RDWAIT,4 
       PAR     FILNAM   
       PAR     FILNAM+1 
       PAR     =0 
       PAR     BUF2,,1  
       PAR     FC1
       PAR     COPY     
FC1    AXC     **,4     
       AXT     1,6
       AXT     1,7
       TRA     1,4
GETFC  TIX     GF2,7,1  
       AXT     **,7     
       TIX     GF1+1,6,1
       AXT     100,6    
       SCA     GF1,4    
       TSX     RDWAIT,4 
       PAR     FILNAM   
       PAR     FILNAM+1 
       PAR     =0 
       PAR     BUF2,,100
       PAR     GF5
       PAR     COPY     
GF1    AXC     **,4     
       LDQ     BUF2+100,6     
       ZAC  
       TRA     GF2+1    
GF2    DLD     SYLBUF+2 
       LGL     ** 
       STQ     SYLBUF+3 
GF3    TRA     ** 
       SCA     GF4,4    
       TSX     CLOS,4   
GF4    AXC     **,4     
       AXC     **,6     
       AXC     **,7     
       CAL     =O3
       TRA     1,4
CLOS   SCA     CLO1,4   
       TSX     CLOSE,4  
       PAR     FILNAM   
       PAR     FILNAM+1 
       PAR     *+2
       PAR     COPY     
       STZ     FILFLG   
CLO1   AXC     **,4     
       TRA     1,4
GF5    LAC     COPY,4   
       CAL     OCT3     
       SLW     BUF2,4   
       CAL     =O777777777777 
       SLW     BUF2+1,4 
       TRA     GF1
GFGE   ANA     =O177    
       TZE     GETFC    
       LAS     =O177    
       TRA     *+2
       TRA     GETFC    
       LAS     =O15     
       TRA     GF8
       CAL     =O12     
       TRA     GF8
GFASCI ANA     =O177    
       TRA     GF8
GFSIX  PAC     ,1 
       TXH     *+2,1,-63
       TXH     GF7,1,-64
       IFF     XBUILD,0,1                                        DGPHACK
       CAL*    CNVTBL
       IFF     XBUILD,1,1                                        DGPHACK
       CAL*    VNVTBL                                            DGPHACK
       ARS     16 
       TRA     GF6
GFTWLV ANA     =O177    
       PAC     ,1 
       TXH     *+2,1,-127     
       TXH     GF7,1,-128     
       IFF     XBUILD,0,1                                        DGPHACK
       CAL*    CNVTBL
       IFF     XBUILD,1,1                                        DGPHACK
       CAL*    VNVTBL                                            DGPHACK
       ARS     16 
GF6    ANA     =O177    
       LAS     =O177    
       TRA     *+1
       TRA     GETFC    
GF8    LAS     =O3
       TRA     1,4
       TRA     GF3+1    
       TRA     1,4
GF7    AXT     1,7
       CAL     BUF2+100,6     
       LAS     =O777777777777 
       TRA     *+2
       TRA     GF3+1    
       CAL     =O12     
       TRA     1,4
GETBN  SCA     BN7,4    
       TSX     GETCHA,4 
       ZET     VFLG     
       TRA     BN7
       TXL     BN7,1,-41
       TXH     BN7,1,-40
       STZ     BN6
BN1    TSX     GETCHA,4 
       ZET     VFLG     
       TRA     BN2
       TXL     BN2,1,-42
       TXL     BN3,1,-41
       TXL     BN2,1,-11
       TXL     TYPE9,1,-10    
BN2    LGR     7  
       CAL     BN6
       LGL     7  
       SLW     BN6
       TRA     BN1
BN3    CAL     BN6
BN7    AXC     **,4     
       TRA     1,4
BN6    PZE  
GETFN  SCA     FN7,4    
       ZET     FILFLG   
       TSX     CLOS,4   
       STZ     AFLG     
       STZ     BFLG     
       TSX     GETCHA,4 
       AXT     16,4     
       LAS     TYCHA+16,4     
       TRA     *+2
       TRA     *+3
       TIX     *-3,4,2  
       TRA     TYPE9    
       CAL     TYCHA+17,4     
       PAC     ,2 
FN3    CAL     FNASUM,2 
       SLW     FILNAM   
       SLW     FILNAM+1 
       LAS     =H   FAP 
       TRA     FN4-1    
       TRA     *+2
       TRA     FN4-1    
       STL     CAPFLG   
       TRA     FN4
       STZ     CAPFLG   
FN4    TSX     GETCHA,4 
       TXL     FN5-1,1,-33    
       TXL     FN4,1,-32
       TXL     FN5-1,1,-11    
       TXL     FN7,1,-10
       LDI     =H 
       IFF     XBUILD,0,1                                        DGPHACK
FN5    CAL*    CNVTBL
       IFF     XBUILD,1,1                                        DGPHACK
FN5    CAL*    VNVTBL                                            DGPHACK
       LGR     14 
       PIA  
       LGL     6  
       PAI  
       TSX     GETCHA,4 
       TXL     FN5,1,-33
       TXL     FN6,1,-32
       TXL     FN5,1,-11
       TXL     FN6,1,-10
       TRA     FN5
FN6    NZT     BFLG     
       TRA     *+5
       CAL     FILNAM+1 
       SLW     FILNAM   
       STI     FILNAM+1 
       TRA     FN4+1    
       ZET     AFLG     
       TRA     *+4
       STI     FILNAM   
       STL     AFLG     
       TRA     FN4+1    
       STI     FILNAM+1 
       STL     BFLG     
       TRA     FN4+1    
FN7    AXC     **,4     
       TRA     1,4
INSERT SCA     PU5,4    
       CAL     1,4
       STA     PU4
PU1    XEC     PU4
       PAC     ,1 
       ZET     INTFLG   
       TRA     TYPE4    
       ZET     VFLG     
       TRA     PU7
       LAS     E  
       TRA     *+2
       TRA     PU5
       TXL     *+4,1,-29
       TXL     PU5,1,-28
PU7    TXL     *+2,1,-4 
       TXL     PU5,1,-3 
       LAC     ADDR2,4  
       CAL     00,4     
       PDC     ,2 
       STD     00,5     
       PCA     ,5 
       STP     00,5     
       STA     00,2     
       PAC     ,2 
       PCD     ,2 
       STD     00,4     
       CAL     00,5     
       PAC     ,5 
       PCA     ,4 
       STA     00,2     
       STZ     03,2     
       SCA     DOT,2    
       TXH     *+2,5,0  
       TSX     MORMEM,4 
       PCA     ,5 
       STO     01,2     
PU2    PAC     ,2 
       CLA     00,5     
       PAC     ,5 
       TXH     *+2,5,0  
       TSX     MORMEM,4 
       STZ     00,2     
       STZ     01,2     
       STZ     02,2     
       STZ     03,2     
       AXT     18,3     
       PCA     ,1 
PU3    LAS     =O3
       TRA     *+2
       TRA     PU8
       XEC     ALS,3    
       XEC     ORS,3    
       TXL     *+2,1,-11
       TXL     PU6,1,-10 (O12)
PU4    TSX     **,4     
       PAC     ,1 
       TIX     PU3,3,1  
       PCA     ,5 
       STA     00,2     
       TRA     PU2
PU8    CAL     =O12     
       XEC     ALS,3    
       XEC     ORS,3    
PU5    AXC     **,4     
       TRA     2,4
PU6    LAC     DOT,4    
       PCD     ,2 
       STD     01,4     
       TRA     PU1
BOPEN  SCA     BO4,4    
       SLW     BO5
       LAC     BUFER,4  
       CAL     DOT
       ALS     18 
       ORA     ZER
       SLW     01,4     
       LAC     BBASE,4  
BO1    TXL     BO2,4,0  
       CLA     BO5
       SUB     03,4     
       TZE     BO3
       CAL     00,4     
       PAC     ,4 
       TRA     BO1
BO2    SCA     BUFER,5  
       CAL     00,5     
       PAC     ,5 
       TXH     *+2,5,0  
       TSX     MORMEM,4 
       SCA     ZER,5    
       CAL     00,5     
       PAC     ,5 
       TXH     *+2,5,0  
       TSX     MORMEM,4 
       LAC     BUFER,4  
       STZ     01,4     
       STZ     02,4     
       CAL     BO5
       SLW     03,4     
       CAL     BBASE    
       SLW     00,4     
       SCA     BBASE,4  
       LAC     ZER,4    
       SCA     DOT,4    
       PCA     ,4 
       SSM  
       STO     00,4     
       PCD     ,4 
       STD     00,4     
       PCA     ,5 
       STA     01,4     
       PCD     ,5 
       STD     01,4     
       PDC     ,1 
       CAL     00,5     
       PAC     ,5 
       TXH     *+2,5,0  
       TSX     MORMEM,4 
       STZ     00,1     
       CAL     =O024000000000 
       SLW     01,1     
       TRA     BO4
BO3    CAL     01,4     
       STA     ZER
       ARS     18 
       STA     DOT
       SCA     BUFER,4  
BO4    AXC     **,4     
       TRA     1,4
BO5    PZE  
SETDD  CAL     DOT
       SLW     ADDR1    
       SLW     ADDR2    
       STL     AFLG     
       TRA     1,4
SETFL  LAC     ZER,1    
       CAL     00,1     
       PDC     ,1 
       SCA     ADDR2,1  
       STA     ADDR1    
       STL     AFLG     
       TRA     1,4
CKWRAP LAC     ADDR1,1  
       LAC     ADDR2,2  
CK1    CLA     00,1     
       TMI     TYPE6    
       PAC     ,1 
       SUB     00,2     
       TNZ     CK1
       TRA     1,4
DELETE SCA     DEL3,4   
       NZT     AFLG     
       TSX     SETDD,4  
       TSX     CKWRAP,4 
       LAC     ADDR1,4  
       CAL     00,4     
       PDC     ,1 
       LAC     ADDR2,2  
       CAL     00,2     
       PAC     ,2 
       STA     00,1     
       PCD     ,1 
       STD     00,2     
       SCA     DOT,2    
       LAC     ADDR1,1  
DEL1   CAL     00,1     
       PAC     ,2 
       CAL     01,1     
       STA     00,1     
       PDC     ,1 
       PCA     ,2 
       CAS     DOT
       TRA     *+2
       TRA     DEL2     
       STA     00,1     
       PAC     ,1 
       TRA     DEL1     
DEL2   PCA     ,5 
       STA     00,1     
       LAC     ADDR1,5  
DEL3   AXC     **,4     
       TRA     1,4
COP1   ZET     AFLG     
       TRA     COP2     
       TSX     GETCHA,4 
       LAS     =O12     
       TRA     *+2
       TRA     COP2     
       LAS     =O40     
       TRA     1,2
       CAL     =O200    
       TRA     1,2
COP2   STL     AFLG     
       CAL     =O200    
       TRA     1,2
PRINT  SCA     PR6,4    
       CAL     1,4
       STA     PR7
       AXC     00,3     
       AXT     24,4     
       SXA     PR3,4    
       STZ     BUF1     
       STL     RFLG     
       AXT     00,2     
PR7    TSX     **,4     
       LAS     =O3
       TRA     *+2
       TRA     PR9
       SLW     BUF7+299,2     
       TXH     TYPE0,2,300    
       LAS     =O12     
       TXI     PR7,2,1  
       TXI     *+2,2,1  
       TXI     PR7,2,1  
       SXA     SIZ1,2   
       TSX     INESC,4  
       PAR     BUF7+299 
       PAR     SIZ1     
       PAR     BUF7+299 
       PAR     SIZ2     
       PAR     =300     
       AXT     00,2     
PR2    CAL     BUF7+299,2     
       PAC     ,1 
       IFF     XBUILD,0,1                                        DGPHACK
       CAL*    CNVTBL
       IFF     XBUILD,1,1                                        DGPHACK
       CAL*    VNVTBL                                            DGPHACK
       ANA     =O177    
       LAS     =O177    
       TXI     *+3,2,1  
       TXI     PR2,2,1  
       TXI     *+1,2,1  
PR3    ALS     ** 
       ORS     BUF1,3   
       LXA     PR3,4    
       TXI     *+1,4,-12
       TXL     PR5,4,-20
       TXI     *+1,3,-1 
       TXH     PR4,3,-25
       TSX     WRFLXA,4 
       PZE     BUF1,,25 
       AXC     00,3     
PR4    STZ     BUF1,3   
       AXT     24,4     
PR5    SXA     PR3,4    
       TXL     PR2,1,-11
       TXH     PR2,1,-10
       TRA     PR7-1    
PR9    LAC     PR3,4    
       CAL     =O57     
       ALS     00,4     
       ORS     BUF1,3   
       LAS     =O57     
       TXI     PR9+1,4,12     
       TRA     *+2
       TXI     PR9+1,4,12     
       TXI     *+1,3,-1 
       SCD     *+2,3    
       TSX     WRFLXA,4 
       PZE     BUF1,,** 
PR6    AXC     **,4     
       STZ     RFLG     
       TRA     2,4
SUB    SCA     SUB4,4   
       LAC     BC2,4    
       CAL     00,4     
       PDC     ,4 
       CAL     00,4     
       PDC     ,4 
       SCA     S5,4     
SUBP1  STZ     BUF2     
       STZ     BUF2+1   
       LAC     JFLG,4   
       TXL     SUB4,4,0 
S3     TXL     S4,4,0   
       CAL     BUF5-2,4 
       ANA     =O77777  
       LAS     BUF2+1   
       TRA     S6 
       TRA     S7 
       TXI     S3,4,2   
S6     SLW     BUF2+1   
       DLD     BUF5-2,4 
       ARS     18 
       STA     BUF2     
       SCA     BUF2+2,4 
       STQ     BUF2+3   
       TXI     S3,4,2   
S7     CAL     BUF5-2,4 
       ARS     18 
       LAS     BUF2     
       TXI     S3,4,2   
       TRA     TYPE9    
       CAL     BUF5-2,4 
       ANA     =O77777  
       TRA     S6 
S4     LAC     JFLG,4   
       TXI     *+1,4,2  
       SCA     JFLG,4   
       DLD     BUF5,4   
       LAC     BUF2+2,4 
       DST     BUF5-2,4 
       NZT     SFLG     
       TRA     S5 
       LAC     S5,4     
       CAL     00,4     
       PAC     ,4 
       SCA     S1,4     
       SCD     S1,4     
       TSX     SETBC,4  
S1     PZE     **,,**   
       STZ     BUF2+2   
       AXC     S9,4     
       SCA     S8+1,4   
       TSX     PRINT,4  
       PZE     S8 
       STL     RFLG     
       TSX     RDFLXA,4 
       PZE     BUF1,,1  
       STZ     RFLG     
       CAL     BUF1     
       LAS     =O6200550060   
       TRA     *+2
       TRA     S5 
       LAS     =O16200550060  
       TRA     SUBP1    
       TRA     S5 
       TRA     SUBP1    
S5     AXC     **,4     
       CAL     00,4     
       PAC     ,4 
       SCA     S13,4    
       SCD     S13,4    
       SCA     ADDR1,4  
       SCA     ADDR2,4  
       AXC     S18,4    
       SCA     S17+1,4  
       TSX     SETBC,4  
S13    PZE     **,,**   
       STZ     BUF2+2   
       STL     VFLG     
       TSX     INSERT,4 
       PZE     S17
       STZ     VFLG     
       TSX     DELETE,4 
       TSX     FREIT,4  
       LAC     JFLG,4   
S16    TXL     SUBP1,4,0
       CAL     BUF5-2,4 
       ANA     =O77777  
       LAS     BUF2     
       TRA     *+3
       TXI     S16,4,2  
       TXI     S16,4,2  
       CAL     BUF5-1,4 
       SLW     BUF2+3   
       SCA     SUBP2,4  
       TSX     FREIT,4  
       LAC     JFLG,4   
       TXI     *+1,4,2  
       SCA     JFLG,4   
       DLD     BUF5,4   
SUBP2  AXC     **,4     
       DST     BUF5-2,4 
       TXI     S16,4,2  
SUB4   AXC     **,4     
       TRA     1,4
S8     SCA     S12,4    
       TRA     ** 
S9     CLA     BUF2+2   
       ADD     =1 
       STO     BUF2+2   
       CAS     BUF2     
       TRA     TYPE9    
       TRA     *+2
       TRA     S11
       AXC     S10,4    
       SCA     S8+1,4   
       CAL     =O16     
       TRA     S12
S10    CLA     BUF2+2   
       CAS     BUF2+1   
       TRA     TYPE9    
       TRA     *+4
       ADD     =1 
       STO     BUF2+2   
       TRA     S11
       AXC     S11,4    
       SCA     S8+1,4   
       CAL     =O17     
       TRA     S12
S11    TSX     GETBC,4  
S12    AXC     **,4     
       TRA     1,4
S17    SCA     S21,4    
       TRA     ** 
S18    CLA     BUF2+2   
       ADD     =1 
       STO     BUF2+2   
       CAS     BUF2     
       TRA     TYPE9    
       TRA     *+2
       TRA     S20
       SXA     S19,0    
       AXC     S19,4    
       SCA     S17+1,4  
       SCA     S23,0    
S22    CAS     BUF2+1   
       TRA     TYPE9    
       TRA     S24
       ADD     =1 
       STO     BUF2+2   
       TSX     GETBC,4  
S23    AXC     **,4     
       SLW     BUF1,4   
       TXI     *+1,4,-1 
       SCA     S23,4    
       CLA     BUF2+2   
       TRA     S22
S24    LAC     S23,4    
       CAL     =3 
       SLW     BUF1,4   
       SCA     S23,0    
S28    NZT     EXFLG    
       TRA     S19
       LAC     S23,4    
       CAL     BUF1,4   
       LAS     =3 
       TRA     S19
       TRA     S20-2    
S19    AXT     **,4     
       CAL     BUF5+99,4
       TXI     *+1,4,1  
       SXA     S19,4    
       LAS     =O200    
       TRA     *+3
       TRA     S25
       TRA     S21
       LAS     =O201    
       TRA     *+2
       TRA     S27
       LAS     =O77777  
       TRA     S21
       TRA     *+2
       TRA     S32
       NZT     EXFLG    
       TRA     S20-2    
       LAC     S23,4    
       TXI     *+1,4,-1 
       SCA     S23,4    
       SCA     S19,0    
       TRA     S28
       AXC     S20,4    
       SCA     S17+1,4  
S20    TSX     GETBC,4  
S21    AXC     **,4     
       TRA     1,4
S25    SCA     S26,0    
       CAL     BUF2+1   
       SUB     BUF2     
S31    PAC     ,4 
       TXI     *+1,4,-1 
       SXD     S30,4    
       AXC     S26,4    
       SCA     S17+1,4  
S26    AXC     **,4     
       CAL     BUF1,4   
       TXI     *+1,4,-1 
       SCA     S26,4    
S30    TXH     S21,4,** 
       AXC     S19,4    
       SCA     S17+1,4  
       TRA     S19
S27    LAC     S23,4    
       CAL     BUF1,4   
       TRA     S21
S32    PAC     ,4 
       CAL     SCHARS-130,4   
       SLW     BUF2+4   
       LDC     BUF2+3,4 
S34    TXL     S19,4,0  
       CAL     1,4
       LAS     BUF2+4   
       TRA     *+2
       TRA     S33
       CAL     0,4
       PAC     ,4 
       TRA     S34
S33    CAL     2,4
       SUB     BUF2     
       STA     S26
       CAL     3,4
       SUB     BUF2     
       TRA     S31
FREIT  SCA     FRE1,4   
       LDC     BUF2+3,4 
       TXL     FRE1,4,0 
FRE0   CAL     0,4
       TZE     *+3
       PAC     ,4 
       TRA     FRE0     
       PCA     ,5 
       SLW     0,4
       LDC     BUF2+3,5 
FRE1   AXC     **,4     
       TRA     1,4
WRITE1 SCA     WRX,4    
       LAC     GOOD,4   
       SCD     *+5,4    
       TSX     WRWAIT,4 
       PAR     FILNAM   
       PAR     FILNAM+1 
       PAR     =0 
       PAR     BUF1,,** 
       PAR     =-0
       PAR     =-0
       LAC     WRX,4    
       TRA     1,4
WRITE  SCA     WRX,4    
       CAL     1,4
       PAC     ,4 
       SCD     *+5,4    
       TSX     WRWAIT,4 
       PAR     FILNAM   
       PAR     FILNAM+1 
       PAR     =0 
       PAR     BUF2,,** 
       PAR     =-0
       PAR     =-0
WRX    AXC     **,4     
       TRA     2,4
WRSXTW TXL     *+2,1,-4 
       TXL     WR12,1,-3
       TXL     *+2,1,-11
       TXL     WR7,1,-10
       IFF     XBUILD,0,1                                        DGPHACK
       CAL*    CNVTBL
       IFF     XBUILD,1,1                                        DGPHACK
       CAL*    VNVTBL                                            DGPHACK
WR13   ARS     ** 
       ANA     =O377    
       LAS     =O177    
       TRA     WR4-1    
       TRA     WR2
       TRA     WR4
       ERA     =O600    
WR4    TIX     WR6,3,1  
       TXI     *+1,2,-1 
       TXH     WR5,2,-100     
       PAC     ,1 
       TSX     WRITE,4  
       PZE     100
       PCA     ,1 
       AXC     00,2     
WR5    AXT     **,3     
WR6    LGR     ** 
       CAL     BUF2,2   
       LGL     ** 
       SLW     BUF2,2   
       TRA     WR2
WR7    LDQ     OCT3     
       CAL     BUF2,2   
       TRA     *+2
       XEC     WR6+2    
       TIX     *-1,3,1  
       SLW     BUF2,2   
       PCA     ,2 
       ORA     =O777777000000 
       SLW     BUF2     
       ANA     =O77777  
       ADD     =1 
       ACL     GOOD     
       LAS     =100     
       TRA     WR8
       TRA     *+1
WR11   SLW     GOOD     
       PAC     ,1 
       CAL     BUF2,2   
       SLW     BUF1-1,1 
       TXL     WR2,2,0  
       TXI     *+1,2,1  
       TXI     *-4,1,1  
WR8    TSX     WRITE1,4 
       PCA     ,2 
       ADD     =1 
       TRA     WR11     
WR12   TSX     WRITE1,4 
       TRA     WR10     
WRGE   TXL     WR2,1,-127     
WRASCI TXL     WR4,1,-4 
       TXH     WR4,1,-3 
       LDQ     OCT3     
       CAL     BUF2,2   
       TRA     *+2
       XEC     WR6+2    
       TIX     *-1,3,1  
       SLW     BUF2,2   
       TXI     *+1,2,-1 
       SCA     WR9,2    
       TSX     WRITE,4  
WR9    PZE     ** 
WR10   TSX     CLOS,4   
       TRA     ADDRES   
CNVBD  DIVIDE  10,97    
       DIVIDE  10,98    
       DIVIDE  10,99    
       STQ     BUF1+100 
       TRA     1,1
LENGTH SCA     LEN1,4   
       AXT     0,4
CEQ1   CLA     00,1     
       TMI     CEQ2     
       PDC     ,1 
       TXI     CEQ1,4,1 
CEQ2   PXA     ,4 
       XCA  
       TSX     CNVBD,1  
       CAL     =O160    
       SLW     BUF1+101       BLANK 
       CAL     =O132    
       SLW     BUF1+96        CR    
       CAL     =O123    
       SLW     BUF1+95        EOF   
       AXC     7,4
       SCA     CCL3,4   
       TSX     PRINT,4  
       PZE     CCL3     
LEN1   AXC     **,4     
       TRA     1,4
BORCR  SCA     BOR1,4   
       TSX     GETCHA,4 
       LAS     =O40     
       TRA     TYPE9    
       TRA     BOR1     
       LAS     =O12     
       TRA     TYPE9    
       TRA     BOR1     
       TRA     TYPE9    
BOR1   AXC     **,4     
       TRA     1,4
*      INSERT  2QED                                              DGPHACK
       IFF     XBUILD,0,1                                        DGPHACK
COMAND CAL*    CNVTBL
       IFF     XBUILD,1,1                                        DGPHACK
COMAND CAL*    VNVTBL                                            DGPHACK
       ARS     27 
       ANA     =O37     
       PAC     ,4 
       TRA     *+1,4    
       TRA     CUNKN    
       TRA     CAPEND   
       TRA     CBUFER   
       TRA     CCHANG   
       TRA     CDELET   
       TRA     CRNCOM   
       TRA     CUPDAT   
       TRA     CINSRT   
       TRA     CMOVE    
       TRA     CPRINT   
       TRA     CREAD    
       TRA     CSUBST   
       TRA     CVERIF   
       TRA     CWRITE   
       TRA     CNLCR    
       TRA     CBLANK   
       TRA     CQUIT    
       TRA     CEQALS   
       TRA     CDBUFR   
       TRA     COPTON   
       TRA     CLIST    
       TRA     CSORT    
       TRA     CSTRAP   
       TRA     CCOMNT   
       TRA     CCANON   
       TRA     CAUDIT   
       TRA     CCOLN    
       TRA     CGLOB    
CUNKN  TRA     TYPE2    
CAPEND NZT     AFLG     
       TSX     SETFL,4  
       TSX     BORCR,4  
       LAC     ADDR2,4  
       CAL     00,4     
       STA     ADDR2    
       TSX     INSERT,4 
       PZE     GETCHA   
       TRA     ADDRES   
CBUFER ZET     AFLG     
       TRA     TYPE2    
       TSX     GETBN,4  
       TSX     BOPEN,4  
       TRA     ADDRES   
CCHANG TSX     BORCR,4  
       TSX     DELETE,4 
       CAL     DOT
       SLW     ADDR2    
       TSX     INSERT,4 
       PZE     GETCHA   
       TRA     ADDRES   
CDELET TSX     DELETE,4 
       TRA     ADDRES   
CSTRAP ZET     AFLG     
       TRA     TYPE2    
       TSX     GETCHA,4 
       PAC     ,2 
       TSX     GETBN,4  
       TXL     *+2,1,-11
       TXL     TYPE9,1,-10    
       TXL     TYPE9,2,-58    
       TXH     TYPE9,2,-49    
       SLW     TRAP1-49,2     
       TRA     ADDRES   
CCANON TSX     GETCHA,4 
       TSX     CAN02,4  
       TRA     ADDRES   
CAN01  AXC     **,1     
CAN02  SCA     CCAN9,1  
       SCA     CCAN8,4  
       NZT     AFLG     
       TSX     SETDD,4  
       TSX     CKWRAP,4 
       LAC     ADDR1,4  
       SCA     CCAN1,4  
       LAC     ADDR2,4  
       SCD     CCAN1,4  
       TSX     SETBC,4  
CCAN1  PZE     **,,**   
       AXT     00,2     
       TSX     GETBC,4  
       LAS     =O3
       TRA     CCAN2    
       TRA     *+2
       TRA     CCAN2    
       LAC     DOT,4    
       CAL     00,4     
       PDC     ,4 
       SCA     DOT,4    
CCAN8  AXC     **,4     
       TRA     1,4
       TSX     GETBC,4  
CCAN2  SLW     BUF7+299,2     
       LAS     =O12     
       TXI     CCAN2-1,2,1    
       TXI     *+2,2,1  
       TXI     CCAN2-1,2,1    
       SXA     SIZ1,2   
CCAN9  AXC     **,1     
       PCA     ,1 
       TSX     ZCMD,4   
       PZE     BUF7+299 
       PZE     SIZ1     
       PZE     =300     
CCAN4  LXA     SIZ1,2   
       CAL     =O3
       SLW     BUF7+299,2     
       SCA     CCAN3,0  
       LAC     BC2,4    
       CAL     00,4     
       PDC     ,4 
       SCA     ADDR1,4  
       SCA     ADDR2,4  
       STL     VFLG     
       TSX     INSERT,4 
       PZE     CCAN3    
       STZ     VFLG     
       TSX     DELETE,4 
       TRA     CCAN1+1  
CCAN3  AXT     **,1     
       CAL     BUF7+299,1     
       TXI     *+1,1,1  
       SXA     CCAN3,1  
       TRA     1,4
CINSRT NZT     AFLG     
       TSX     SETDD,4  
       TSX     BORCR,4  
       TSX     INSERT,4 
       PZE     GETCHA   
       LAC     DOT,4    
       CAL     00,4     
       STA     DOT
       TRA     ADDRES   
CLIST  TSX     SETFC,4  
       TSX     PRINT,4  
       PZE     GETFC    
       TRA     ADDRES   
CUPDAT ZET     AFLG     
       TRA     TYPE2    
       TSX     WHOAMI,4 
       PZE     BUF2,,4  
       TSX     GETIME,4 
       SLW     BUF2+4   
       STQ     BUF2+5   
       LAC     BUFER,4  
       CAL     03,4     
       SLW     MO2
       TSX     GETBN,4  
       TSX     BOPEN,4  
       TSX     SETFL,4  
       LAC     ADDR1,4  
       CLA     00,4     
       TMI     *+4
       TSX     DELETE,4 
       LAC     ZER,4    
       SCA     ADDR2,4  
       AXC     00,2     
       CAL     BUF2+5   
       TSX     SPREAD,4 
       DLD     BUF1+4   
       DST     BUF1+6   
       DLD     BUF1+2   
       DST     BUF1+3   
       CAL     =O61     
       SLW     BUF1+2   
       SLW     BUF1+5   
       CAL     =O55     
       SLW     BUF1+8   
       SLW     BUF1+19  
       LDQ     BUF2+4   
       DIVIDE  6,18     
       DIVIDE  10,18    
       DIVIDE  10,16    
       DIVIDE  6,15     
       DIVIDE  10,13    
       DIVIDE  6,12     
       DIVIDE  10,10    
       STQ     BUF1+9   
       CAL     =O35     
       SLW     BUF1+11  
       SLW     BUF1+14  
       CAL     =O33     
       SLW     BUF1+17  
       AXC     20,2     
       CAL     BUF2     
       TSX     SPREAD,4 
       CAL     BUF2+1   
       TSX     SPREAD,4 
       CAL     BUF2+2   
       TSX     SPREAD,4 
       CAL     BUF2+3   
       TSX     SPREAD,4 
       SCA     UPD1,0   
       TSX     INSERT,4 
       PZE     UPD1-1   
       CAL     MO2
       TSX     BOPEN,4  
       TRA     ADDRES   
       SCA     UPD2,4   
UPD1   AXC     **,4     
       CAL     BUF1,4   
       TXI     *+1,4,-1 
       TXL     UPD2-1,4,-49   
       SCA     UPD1,4   
       PAC     ,1 
       IFF     XBUILD,0,1                                        DGPHACK
       CAL*    CNVTBL
       IFF     XBUILD,1,1                                        DGPHACK
       CAL*    VNVTBL                                            DGPHACK
       ARS     16 
       ANA     =O177    
       TRA     UPD2     
       CAL     =3 
UPD2   AXC     **,4     
       TRA     1,4
SPREAD SCA     UPD2,4   
       AXT     6,4
       XCL  
       ZAC  
       LGL     6  
       SLW     BUF1,2   
       TXI     *+1,2,-1 
       TIX     *-4,4,1  
       CAL     =O55     
       SLW     BUF1,2   
       TXI     UPD2,2,-1
CMOVE  NZT     AFLG     
       TSX     SETDD,4  
       TSX     CKWRAP,4 
       LAC     BUFER,4  
       CAL     03,4     
       SLW     MO2
       LAC     ADDR1,1  
       SCA     MO1,1    
       LAC     ADDR2,2  
       SCA     MO1+1,2  
       CAL     00,1     
       PDC     ,4 
       CAL     00,2     
       STA     00,4     
       STA     DOT
       PAC     ,4 
       CAL     00,1     
       STD     00,4     
       TSX     GETBN,4  
       TSX     BOPEN,4  
       TSX     SETFL,4  
       LAC     ADDR1,4  
       CLA     00,4     
       TMI     *+2
       TSX     DELETE,4 
MO1    AXC     **,1     
       AXC     **,2     
       LAC     ZER,4    
       CAL     00,4     
       STD     00,1     
       STA     00,2     
       PCA     ,1 
       STA     00,4     
       PCD     ,2 
       STD     00,4     
       CAL     MO2
       TSX     BOPEN,4  
       TRA     ADDRES   
MO2    PZE     ** 
COPTON ZET     AFLG     
       TRA     TYPE2    
CO1    TSX     COP1,2   
       ZET     AFLG     
       TRA     CO2
       PAC     ,1 
       IFF     XBUILD,0,1                                        DGPHACK
       CAL*    CNVTBL
       IFF     XBUILD,1,1                                        DGPHACK
       CAL*    VNVTBL                                            DGPHACK
       ARS     27 
       ANA     =O37     
       PAC     ,4 
       TRA*    COLST,4  
COLST  PZE     TYPE9,,TYPE9   
       PZE     CO15,,TYPE9    
       PZE     CO3,,TYPE9     
       PZE     CO4,,TYPE9     
       PZE     CO16,,DELETE   
       PZE     CO5,,TYPE9     
       PZE     CO6,,TYPE9     
       PZE     CO7,,TYPE9     
       PZE     CO17,,TYPE9    
       PZE     CO14,,PRINT0   
       PZE     CO8,,TYPE9     
       PZE     CO9,,TYPE9     
       PZE     CO18,,TYPE9    
       PZE     CO10,,TYPE9    
       PZE     TYPE9,,TYPE9   
       PZE     TYPE9,,TYPE9   
       PZE     CO19,,TYPE9    
       PZE     TYPE9,,EQALS0  
       PZE     TYPE9,,TYPE9   
       PZE     CO11,,TYPE9    
       PZE     TYPE9,,TYPE9   
       PZE     CO12,,TYPE9    
       PZE     CO13,,TYPE9    
       PZE     TYPE9,,TYPE9   
       PZE     TYPE9,,CAN01   
       PZE     TYPE9,,TYPE9   
       PZE     TYPE9,,COLN0   
       PZE     TYPE9,,TYPE9   
CO3    TSX     COP1,2   
       SLW     B  
       TRA     CO1
CO4    TSX     COP1,2   
       SLW     V  
       TRA     CO1
CO5    TSX     COP1,2   
       SLW     A  
       TRA     CO1
CO6    TSX     COP1,2   
       SLW     E  
       TRA     CO1
CO7    LDQ     ZET1     
       SLQ     IO1
       SLQ     IO2
       TRA     CO1
CO8    TSX     COP1,2   
       SLW     R  
       TRA     CO1
CO9    STL     CAPFLG   
       TRA     CO1
CO10   TSX     COP1,2   
       SLW     W  
       TRA     CO1
CO11   LDQ     NZT1     
       SLQ     IO1
       SLQ     IO2
       TRA     CO1
CO12   TSX     COP1,2   
       SLW     Q  
       TRA     CO1
CO13   STZ     CAPFLG   
       TRA     CO1
CO14   TSX     COP1,2   
       ORA     =O40     
       LAS     =O154    
       TRA     CO14.1   
       TRA     CO14.2   
       SUB     =O141    
       TNZ     TYPE9    
       STL     PAFLG    
       STZ     PLFLG    
       TRA     CO1
CO14.1 SUB     =O156    
       TNZ     TYPE9    
       STZ     PAFLG    
       STZ     PLFLG    
       TRA     CO1
CO14.2 STZ     PAFLG    
       STL     PLFLG    
       TRA     CO1
CO15   STZ     FBDFLG   
       TRA     CO1
CO16   STL     FBDFLG   
       TRA     CO1
CO17   STZ     FMODE    
       TSX     COP1,2   
       LAS     =O70     
       TRA     CO1+1    
       TRA     CO1+1    
       LAS     =O60     
       TRA     *+3
       TRA     *+2
       TRA     CO1+1    
       SUB     =O60     
       LGR     3  
       CAL     FMODE    
       LGL     3  
       ANA     =O177    
       SLW     FMODE    
       TRA     CO17+1   
CO18   STL     TRCFLG   
       TRA     CO1
CO19   STZ     TRCFLG   
       TRA     CO1
CO2    TSX     SETOPS,4 
       PZE     V  
       PZE     A  
       PZE     W  
       PZE     Q  
       TRA     ADDRES   
CPRINT TSX     PRINT0,4 
       TRA     ADDRES   
PRINT0 SCA     CPR10,4  
       NZT     AFLG     
       TSX     SETDD,4  
       LAC     ADDR1,4  
       SCA     CPR1,4   
       LAC     ADDR2,4  
       SCA     DOT,4    
       SCD     CPR1,4   
       STZ     JFLG     
       LDQ     CPR3     
       STQ     CPR2     
       LDQ     CPR6     
       STQ     CPR5     
       TSX     SETBC,4  
CPR1   PZE     **,,**   
       TSX     PRINT,4  
       PZE     CPR9     
CPR10  AXC     **,4     
       TRA     1,4
CPR9   ZET     PLFLG    
       TRA     CPR5     
       NZT     PAFLG    
       TRA     GETBC    
CPR2   TRA     ** 
CPR3   TRA     *+1
       ZET     EFLG     
       TRA     CPR4     
       LAC     BC2,1    
       ZET     03,1     
       TRA     CPR3.1   
       STZ     JFLG     
       CAL     =O52     
       TRA     CPR3.3   
CPR3.1 NZT     JFLG     
       TRA     CPR3.2   
       CAL     JFLG     
       SUB     03,1     
       TNZ     CPR3.4   
CPR3.2 CAL     03,1     
       ADD     =1 
       SLW     JFLG     
       CAL     =O40     
CPR3.3 LDQ     CPR4     
       STQ     CPR2     
       TRA     1,4
CPR3.4 LDQ     CPR3.5   
       STQ     CPR2     
       CAL     =O52     
       TRA     1,4
CPR3.5 TRA     *+1
       STZ     JFLG     
       CAL     =O12     
       TRA     CPR3.3   
CPR4   TRA     *+1
       SCA     CPR4.1,4 
       TSX     GETBC,4  
       LAS     =O12     
       TRA     CPR4.1   
       TRA     *+2
       TRA     CPR4.1   
       LDQ     CPR3     
       STQ     CPR2     
CPR4.1 AXC     **,4     
       TRA     1,4
CPR5   TRA     ** 
CPR6   TRA     *+1
       ZET     EFLG     
       TRA     CPR8     
       LAC     BC2,1    
       CLA     03,1     
       TZE     CPR7.2   
       TPL     *+2
       ZAC  
       XCL  
       TSX     CNVBD,1  
       LDQ     CPR7     
       STQ     CPR5     
       SCA     CPR7+1,0 
       CAL     =O47     
       TRA     1,4
CPR7   TRA     *+1
       AXC     **,1     
       CAL     BUF1+100,1     
       ACL     =O60     
       TXI     *+1,1,1  
       SCA     CPR7+1,1 
       TXL     CPR7.1,1,4     
CPR7.2 CAL     =O11     
       LDQ     CPR8     
       STQ     CPR5     
CPR7.1 TRA     1,4
CPR8   TRA     *+1
       SCA     CPR8.1,4 
       TSX     GETBC,4  
       LAS     =O12     
       TRA     CPR8.1   
       TRA     *+2
       TRA     CPR8.1   
       LDQ     CPR6     
       STQ     CPR5     
CPR8.1 AXC     **,4     
       TRA     1,4
CREAD  NZT     AFLG     
       TSX     SETFL,4  
       LAC     ADDR2,4  
       CAL     00,4     
       STA     ADDR2    
       TSX     SETFC,4  
CRD2   TSX     INSERT,4 
       PZE     GETFC    
       LAC     ZER,2    
       AXC     1,4
       CAL     00,2     
       PAC     ,2 
CRD1   PCA     ,4 
       SLW     03,2     
       CLA     00,2     
       TMI     ADDRES   
       PAC     ,2 
       TXI     CRD1,4,-1
CSUBST STZ     SFLG     
       NZT     AFLG     
       TSX     SETDD,4  
       TSX     CKWRAP,4 
       LAC     ADDR2,4  
       CAL     00,4     
       STA     SUB2     
       TSX     GETCHA,4 
       LAS     =O12     
       TRA     *+2
       TRA     TYPE9    
       SLW     SLCHR    
       STL     SUBFLG   
       TSX     COMPIL,4 
       TRA     SUBF     
       PZE     SUBC     
       STZ     EXFLG    
       AXC     00,2     
SUB3   TSX     GETCHA,4 
       ZET     VFLG     
       TRA     SUB31    
       LAC     NSCHA,4  
       TXL     SUB34,4,0
       LAS     SCHARS-1,4     
       TXI     *-2,4,1  
       TXI     *+2,4,-129     
       TXI     *-4,4,1  
       PCA     ,4 
SUB34  LAS     =O41     
       TRA     SUB32    
       TRA     *+2
       TRA     *+3
       CAL     =O201    
       STL     EXFLG    
       LAS     =O12     
       TRA     SUB33    
       TRA     TYPE9    
       TRA     SUB33    
SUB32  LAS     =O46     
       TRA     SUB33    
       CLA     =O200    
SUB33  LAS     SLCHR    
       TRA     *+2
       TRA     *+3
SUB31  SLW     BUF5+99,2
       TXI     SUB3,2,1 
       CAL     =O77777  
       SLW     BUF5+99,2
       CAL     ADDR2    
       ALS     18 
       ORA     ADDR1    
       SLW     *+2
       TSX     SETBC,4  
       PZE     **,,**   
       STL     IFLG     
       STZ     JFLG     
SUBC   STZ     FFLG     
       NZT     IFLG     
       TRA     SUB1     
       STZ     IFLG     
       STL     FFLG     
       STZ     BUF2+50  
       ZET     JFLG     
       TSX     SUB,4    
SUB1   TSX     GETBC,4  
       PAC     ,1 
       LAC     BUF2+50,4
       TXI     *+1,4,-1 
       SCA     BUF2+50,4
       SCD     BUF2+50,0
       TXL     *+2,1,-4 
       TXL     SUB2,1,-3
       AXC     BUF2+50-1,2    
       TXL     BUF3,1,-11     
       TXH     BUF3,1,-10     
       STL     IFLG     
       ZET     INTFLG   
       TRA     TYPE4    
       TRA     BUF3     
SUBF   LAC     JFLG,4   
       TXL     SUB5,4,0 
       CAL     BUF5-2,4 
       ANA     =O77777  
       SLW     COPY     
       CAL     BUF2+50  
       ANA     =O77777  
       LAS     COPY     
       TXI     SUBF+1,4,2     
       TRA     *+2
       TXI     SUBF+1,4,2     
       CAL     1,2
       ANA     =O77777  
       SLW     COPY     
       CAL     BUF5-2,4 
       ARS     18 
       LAS     COPY     
       TRA     SUB6     
       TRA     FAIL     
       TRA     FAIL     
SUB5   LAC     JFLG,4   
       TXI     *+1,4,-2 
       TXL     TYPE0,4,-100   
       SCA     JFLG,4   
SUB6   CAL     1,2
       SLW     BUF5-1,4 
       ALS     18 
       SLW     BUF5-2,4 
       CAL     BUF2+50  
       STA     BUF5-2,4 
       TRA     2,2
SUB2   AXC     **,4     
       CAL     00,4     
       PDC     ,4 
       SCA     DOT,4    
       ZET     SFLG     
       TSX     BLINE,4  
       TRA     ADDRES   
CVERIF STL     SFLG     
       TRA     CSUBST+1 
CWRITE NZT     AFLG     
       TSX     SETFL,4  
       TSX     CKWRAP,4 
       TSX     GETFN,4  
       CAL     SHFT,2   
       STA     WR5
       ARS     18 
       STA     WR6
       STA     WR6+2    
       CAL     RETRN+4,2
       STA     WR3
       ARS     18 
       STA     WR13     
       CAL     OCT3+4,2 
       SLW     OCT3     
       LAC     ADDR1,4  
       SCA     WR1,4    
       LAC     ADDR2,4  
       SCD     WR1,4    
       TSX     OPEN,4   
       PAR     =HW
       PAR     FILNAM   
       PAR     FILNAM+1 
       PAR     FMODE    
       PAR     =-0
       PAR     TYPE8    
       PAR     COPY     
       STL     FILFLG   
       STZ     GOOD     
       TSX     SETBC,4  
WR1    PZE     **,,**   
       TSX     BUFFER,4 
       PAR     FILNAM   
       PAR     FILNAM+1 
       PAR     BUF3,,432
       STZ     CPLFLG   
       TSX     TRFILE,4 
       PAR     FILNAM   
       PAR     FILNAM+1 
       PAR     =1 
       PAR     *+2
       PAR     COPY     
       TXH     *+3,2,-2 
       AXC     1,2
       TRA     *+2
       AXC     00,2     
       XEC     WR5
       TXI     *+1,3,1  
WR2    TSX     GETBC,4  
       PAC     ,1 
WR3    TRA     ** 
CAUDIT ZET     AFLG     
       TRA     TYPE2    
       TSX     GETBN,4  
       SLW     MO2
       LAC     ZER,4    
       CAL     00,4     
       PAC     ,4 
       XCL  
       CLA     00,4     
       TMI     TYPE5    
       STQ     *+2
       TSX     SETBC,4  
       PZE     **,,**   
       CAL     MO2
       TSX     BOPEN,4  
       LAC     ZER,4    
       CAL     00,4     
       PAC     ,4 
       CLA     00,4     
       TMI     *+3
       TSX     SETFL,4  
       TSX     DELETE,4 
       TSX     SETFL,4  
       STZ     JFLG     
       LDQ     CAU2     
       STQ     CAU1     
       STL     VFLG     
       TSX     INSERT,4 
       PZE     CAU1     
       STZ     VFLG     
       TRA     ADDRES   
CAU1   TRA     ** 
CAU2   TRA     *+1
       LAC     BC2,1    
       ZET     03,1     
       TRA     CAU2.1   
       LDQ     CAU3     
       STQ     CAU1     
       LDQ     JFLG     
       TSX     CNVBD,1  
       CAL     =O61     
       SLW     BUF1+96  
       SCA     CAU3+1,0 
       CAL     =O47     
       TRA     1,4
CAU2.1 CAL     JFLG     
       ADD     =1 
       LAS     03,1     
       TRA     TYPE5    
       TRA     CAU2.2   
       LDQ     CAU4     
       STQ     CAU1     
       CAL     JFLG     
       ADD     =1 
       XCA  
       TSX     CNVBD,1  
       CAL     =O174    
       SLW     BUF1+96  
       LAC     BC2,1    
       CAL     03,1     
       SUB     =1 
       SLW     JFLG     
       XCA  
       DIVIDE  10,91    
       DIVIDE  10,92    
       DIVIDE  10,93    
       STQ     BUF1+94  
       CAL     =O167    
       SLW     BUF1+95  
       CAL     =O64     
       SLW     BUF1+90  
       CAL     =O47     
       SCA     CAU4+1,0 
       TRA     1,4
CAU2.2 ZET     EFLG     
       TRA     GETBC    
       CAL     03,1     
       SLW     JFLG     
       SCA     *+6,4    
       TSX     GETBC,4  
       LAS     =O12     
       TRA     *-2
       TRA     *+2
       TRA     *-4
       AXC     **,4     
       TRA     CAU2     
CAU3   TRA     *+1
       AXC     **,1     
       CAL     BUF1+100,1     
       ACL     =O60     
       TXI     *+1,1,1  
       SCA     CAU3+1,1 
       TXL     CAU3.1,1,5     
       LDQ     CAU5     
       STQ     CAU1     
       STZ     IFLG     
       CAL     =O12     
CAU3.1 TRA     1,4
CAU4   TRA     *+1
       AXC     **,1     
       CAL     BUF1+100,1     
       ACL     =O60     
       ANA     =O177    
       TXI     *+1,1,1  
       SCA     CAU4+1,1 
       TXL     CAU4.1,1,11    
       LDQ     CAU2     
       STQ     CAU1     
       CAL     =O12     
CAU4.1 TRA     1,4
CAU5   TRA     *+1
       SCA     CAU5.3,4 
       ZET     IFLG     
       TRA     CAU5.1   
       TSX     GETBC,4  
       LAS     =O30     
       TRA     CAU5.2   
       TRA     CAU5.2   
       LAS     =O12     
       TRA     CAU5.3   
       TRA     *+2
       TRA     CAU5.3   
       LAC     BC2,1    
       NZT     03,1     
       TRA     CAU5.3   
       CAL     =O12     
       SLW     IFLG     
       TRA     CAU5.3   
CAU5.1 CAL     IFLG     
       STZ     IFLG     
       LAS     =O12     
       TRA     CAU5.3   
       TRA     *+2
       TRA     CAU5.3   
       LDQ     CAU2     
       STQ     CAU1     
       CAL     =O34     
       TRA     CAU5.3   
CAU5.2 LAS     =O40     
       TRA     CAU5.3   
       TRA     CAU5.3   
       SLW     IFLG     
       CAL     =O31     
CAU5.3 AXC     **,4     
       TRA     1,4
CNLCR  ZET     CFLG     
       TRA     CNC1     
       LAC     DOT,4    
       CAL     00,4     
       STA     DOT
       TRA     CPRINT   
CNC1   NZT     AFLG     
       TRA     ADDRES   
       CAL     ADDR2    
       SLW     ADDR1    
       TRA     CPRINT   
CBLANK TSX     GETCHA,4 
       TRA     COMAND   
CQUIT  ZET     AFLG     
       TRA     TYPE2    
       TSX     GETCHA,4 
       LAS     =O12     
       TRA     TYPE9    
       TRA     *+2
       TRA     TYPE2    
       TSX     BLINE,4  
       ZET     PMTFLG   
       TSX     FORBID,4 
       TSX     SETBCD,4 
       TSX     CHNCOM,4 
       PZE     1  
       TRA     TYPE9    
CGLOB  NZT     AFLG     
       TSX     SETFL,4  
       TSX     GETCHA,4 
       TXL     *+2,1,-11
       TXL     TYPE9,1,-10    
       IFF     XBUILD,0,1                                        DGPHACK
       CAL*    CNVTBL
       IFF     XBUILD,1,1                                        DGPHACK
       CAL*    VNVTBL                                            DGPHACK
       ARS     27 
       ANA     =O37     
       PAC     ,1 
       SUB     =24
       TNZ     CG6
       SCA     *+3,1    
       TSX     GETCHA,4 
       SCA     CAN01,1  
       AXC     **,1     
       SUB     =O12     
       TZE     TYPE9    
CG6    CAL     COLST,1  
       PDC     ,4 
       SCA     CG3,4    
       TSX     GETCHA,4 
       TXL     *+2,1,-11
       TXL     TYPE9,1,-10    
       SLW     SLCHR    
       STZ     SUBFLG   
       TSX     COMPIL,4 
       TRA     CGF
       PZE     CGC
       LAC     ADDR1,4  
       SCA     CG1,4    
       LAC     ADDR2,4  
     U SCD     CG1,4    
       CAL     00,4     
       STA     CG4
       TSX     SETBC,4  
CG1    PZE     **,,**   
       STL     IFLG     
       STZ     JFLG     
CGC    STZ     FFLG     
       NZT     IFLG     
       TRA     CG5
       STZ     IFLG     
       STL     FFLG     
       ZET     JFLG     
       TRA     CG2
CG5    TSX     GETBC,4  
       PAC     ,1 
       TXL     *+2,1,-4 
       TXL     CG4,1,-3 
       AXC     BUF2+50-1,2    
       TXL     BUF3,1,-11     
       TXH     BUF3,1,-10     
       STL     IFLG     
       TRA     BUF3     
CGF    STL     JFLG     
       TRA     2,2
CG2    LAC     BC2,2    
       CAL     00,2     
       PDC     ,2 
       SCA     ADDR1,2  
       SCA     ADDR2,2  
       STL     AFLG     
CG3    TSX     **,4     
       STL     IFLG     
       STZ     JFLG     
       TRA     CGC
CG4    AXC     **,2     
       CAL     00,2     
       PDC     ,2 
       SCA     DOT,2    
       TRA     ADDRES   
CCOLN  TSX     COLN0,4  
       TRA     ADDRES   
COLN0  SCA     CCL4,4   
       NZT     AFLG     
       TSX     SETFL,4  
       CAL     =O167    
       SLW     BUF1+101 
       LAC     ADDR2,4  
       CLA     00,4     
       TPL     *+3
       ZAC  
       TRA     CCL1     
       CLA     03,4     
       TNZ     CCL1     
       CAL     =O132    
       SLW     BUF1+100 
       CAL     =O123    
       SLW     BUF1+99  
       TRA     CCL2     
CCL1   XCA  
       TSX     CNVBD,1  
       CAL     =O132    
       SLW     BUF1+96  
       CAL     =O123    
       SLW     BUF1+95  
CCL2   SCA     CCL3,0   
       TSX     PRINT,4  
       PZE     CCL3     
       CAL     ADDR2    
       SLW     DOT
CCL4   AXC     **,4     
       TRA     1,4
CCL3   AXC     **,1     
       CLA     BUF1+101,1     
       TXI     *+1,1,1  
       SCA     CCL3,1   
       ACL     =O60     
       ANA     =O177    
       TZE     CCL3     
       TRA     1,4
CEQALS TSX     EQALS0,4 
       TRA     ADDRES   
EQALS0 SCA     EQALS1,4 
       NZT     AFLG     
       TSX     SETFL,4  
       LAC     ADDR2,1  
       SCA     DOT,1    
       CAL     =O120    
       SLW     BUF1+108 
       SLW     BUF1+107 
       SLW     BUF1+106 
       SLW     BUF1+105 
       SLW     BUF1+104 
       SLW     BUF1+103 
       SLW     BUF1+102 
       TSX     LENGTH,4 
EQALS1 AXC     **,4     
       TRA     1,4
CDBUFR ZET     AFLG     
       TRA     TYPE2    
       LAC     BBASE,4  
       SCA     DBU1,4   
       LAC     BUFER,4  
       CAL     ZER
       SLW     01,4     
       CAL     =O162    
       SLW     BUF1+108 
       SLW     BUF1+102 
       CAL     03,4     
       SLW     BUF2+3   
       TRA     DBU2     
DBU1   AXC     **,4     
       TXL     ADDRES,4,0     
       CAL     00,4     
       STA     DBU1     
       CAL     03,4     
       LAS     BUF2+3   
       TRA     DBU2     
       TRA     DBU1     
DBU2   XCL  
       ZAC  
       LGL     8  
       ACL     =O120    
       SLW     BUF1+107 
       ZAC  
       LGL     7  
       ACL     =O120    
       SLW     BUF1+106 
       ZAC  
       LGL     7  
       ACL     =O120    
       SLW     BUF1+105 
       ZAC  
       LGL     7  
       ACL     =O120    
       SLW     BUF1+104 
       ZAC  
       LGL     7  
       ACL     =O120    
       SLW     BUF1+103 
       CAL*    01,4     
       PDC     ,1 
       TSX     LENGTH,4 
       TRA     DBU1     
ICOM   CAL     =O60     
       LGR     6  
       STQ     BUF1+102 
       CAL     BUF1+1   
       SLW     BUF1+103 
       CAL     =HFROM   
       SLW     BUF1+101 
       CAL     =O555560606060 
       SLW     BUF1+104 
       TSX     SETBCD,4 
       TSX     WRFLX,4  
       PZE     BUF1+101,,4    
       STL     ICFLG    
       NZT     FBDFLG   
       TRA     CRNCOM-1 
       TSX     WRFLX,4  
       PZE     MES5,,3  
       TSX     WRMESS,4 
       PZE     BUF1+102 
       PZE     BUF1+103 
       PZE     MES6,,7  
       STZ     ICFLG    
       TSX     SETFUL,4 
       TRA     GC0
MES5   BCI     3,WITH APOLOGIES.    
MES6   OCT     106162157155,040121105104,040167151164 
       OCT     150040141160,157154157147,151145163056,003040040040
       TRA     *+3
CRNCOM ZET     AFLG     
       TRA     TYPE2    
       TSX     GETCLS,4 
       PZE     BUF1,,1  
       TSX     GETCLS,4 
       PZE     BUF1+20,,2     
       TSX     GETCLS,4 
       PZE     BUF1+40,,3     
       TSX     GETCLS,4 
       PZE     BUF1+60,,4     
       TSX     GETCLS,4 
       PZE     BUF1+80,,5     
       TSX     GETCLC,4 
       SLW     BUF1+100 
       ZET     PMTFLG   
       TSX     FORBID,4 
       ZET     ICFLG    
       TRA     CRCIC    
       LAC     BUFER,4  
       CAL     03,4     
       SLW     CRC1     
       TSX     GETBN,4  
       TSX     BOPEN,4  
       LAC     ZER,4    
       CAL     00,4     
       STO     *+2
       TSX     SETBC,4  
       PZE     **,,**   
       AXC     1,4
       SCD     CRC8,4   
CRC2   TSX     GETBC,4  
       LAS     =3 
       TRA     *+2
       TRA     CRC1+1   
       LAS     =O12     
       TRA     *+2
       TRA     CRC2     
       LAS     =O40     
       TRA     *+2
       TRA     CRC2     
       AXC     00,2     
       LDQ     =H 
       STQ     BUF1+101,2     
CRC4   PAC     ,1 
       IFF     XBUILD,0,1                                        DGPHACK
       CAL*    CNVTBL
       IFF     XBUILD,1,1                                        DGPHACK
       CAL*    VNVTBL                                            DGPHACK
       ARS     8  
       LDQ     BUF1+101,2     
       RQL     6  
       LGR     6  
       RQL     6  
       STQ     BUF1+101,2     
       TSX     GETBC,4  
       LAS     =O12     
       TRA     *+2
       TXI     CRC3,2,-1
       LAS     =O40     
       TRA     CRC4     
       TXI     CRC3,2,-1
       TRA     CRC4     
CRC3   LAS     =O12     
       TRA     *+2
       TRA     CRC5     
       LAS     =O40     
       TRA     CRC4-2   
       TRA     *+2
       TRA     CRC4-2   
       TSX     GETBC,4  
       TRA     CRC3     
CRCIC  AXT     1,4
       SXD     CRC8,4   
       CAL     =H WRITE 
       SLW     BUF1+101 
       AXC     3,2
CRC5   LDQ     =O777777777777 
       STQ     BUF1+101,2     
       TSX     SETCLS,4 
CRC8   PZE     BUF1+101,,**   
       LDC     CRC8,4   
       TXI     *+1,4,-1 
       SCD     CRC8,4   
       ZET     ICFLG    
       TRA     CRC1.1   
       TXH     CRC2,4,-5
       TSX     SETCLS,4 
       PZE     CRC6,,0  
       TSX     SETCLS,4 
       PZE     CRC7,,5  
       CAL     =O5000001
       TSX     SETCLC,4 
       CAL     CRC6     
       LDQ     CRC6+1   
       TSX     NEXCOM,4 
       TRA     CRC2-2   
CRC6   BCI     3,  SAVEQEDXXX     T 
       OCT     777777777777   
CRC7   BCI     2,RESUMEQEDXXX 
       OCT     777777777777   
CRC1   PZE  
       CAL     CRC1     
       TSX     BOPEN,4  
CRC1.1 LDC     CRC8,1   
       TXH     CRC9,1,-2
       TSX     SETCLS,4 
       PZE     CRC6,,0  
       SCD     *+2,1    
       TSX     SETCLS,4 
       PZE     CRC7,,** 
       PCD     ,1 
       ORA     =1 
       TSX     SETCLC,4 
       CAL     CRC6     
       LDQ     CRC6+1   
       TSX     NEXCOM,4 
CRC9   TSX     SETFUL,4 
       TSX     BLINE,4  
       TSX     SETCLS,4 
       PZE     BUF1,,1  
       TSX     SETCLS,4 
       PZE     BUF1+20,,2     
       TSX     SETCLS,4 
       PZE     BUF1+40,,3     
       TSX     SETCLS,4 
       PZE     BUF1+60,,4     
       TSX     SETCLS,4 
       PZE     BUF1+80,,5     
       CAL     BUF1+100 
       TSX     SETCLC,4 
       NZT     PMTFLG   
       TRA     CRC9.1   
       TSX     PERMIT,4 
       PAR     PPPMT    
       PAR     PPPMT+1  
CRC9.1 NZT     ICFLG    
       TRA     ADDRES   
       STZ     ICFLG    
       TRA     GC0
CCOMNT TSX     GETCHA,4 
       LAS     =O12     
       TRA     CCOMNT   
       TRA     ADDRES   
       TRA     CCOMNT   
CSORT  NZT     AFLG     
       TSX     SETFL,4  
       TSX     CKWRAP,4 
       SCA     CSRT3,3  
       LAC     ADDR1,1  
NEWI   CAL     1,1
       PAC     ,2 
       ARS     18 
       SLW     ENDI     
       PCA     ,1 
       PAC     ,3 
INCJ   PCA     ,3 
       SUB     ADDR2    
       TZE     INCI     
       CAL     0,3
       PAC     ,3 
       CAL     1,3
       PAC     ,4 
       CAL     1,4
       LAS     1,2
       TRA     INCJ     
       TRA     CSRT2    
CSRT1  CAL     1,3
       LDQ     1,1
       STQ     1,3
       SLW     1,1
       STZ     3,1
       STZ     3,3
       PAC     ,2 
       ARS     18 
       SLW     ENDI     
       TRA     INCJ     
CSRT2  SCA     CSRT4,2  
       CAL     2,4
       LAS     2,2
       TRA     CSRT4    
       TRA     *+2
       TRA     CSRT1    
       CAL     3,4
       LAS     3,2
       TRA     CSRT4    
       TRA     *+2
       TRA     CSRT1    
       CAL     0,2
       ARS     15 
       SLW     CSRTT    
       CAL     0,4
       PAC     ,4 
       ARS     15 
       LAS     CSRTT    
       TRA     CSRT4    
       TRA     *+2
       TRA     CSRT1    
       PCA     ,2 
       LAS     ENDI     
       TRA     *+2
       TRA     CSRT4    
       CAL     0,2
       PAC     ,2 
       CAL     1,4
       LAS     1,2
       TRA     CSRT4    
       TRA     CSRT2+1  
       TRA     CSRT1    
CSRT4  AXC     **,2     
       TRA     INCJ     
CSRTT 
ENDI  
INCI   PCA     ,1 
       SUB     ADDR2    
       TZE     CSRT3    
       CAL     0,1
       PAC     ,1 
       TRA     NEWI     
CSRT3  AXC     **,3     
       TRA     ADDRES   
DOT   
CAPFLG
PPPMT  BSS     2  
TFLG   BSS     2  
SFLG  
VFLG  
EFLG  
TYCHA  OCT     101,1,141,1,107,2,147,2,123,3,163,3,124,4,164,4    
OCT3   OCT     00,003003003003,003003003003,777777000000,777777000000   
       OCT     003000000000,003000000000,575757575757,005700570057
SHFT   SYN     *-1
       PZE     4,,9     
       PZE     4,,9     
       PZE     6,,6     
       PZE     3,,12    
RETRN  SYN     *-1
       PZE     GFASCI   
       PZE     GFGE     
       PZE     GFSIX    
       PZE     GFTWLV   
       PZE     WRASCI   
       PZE     WRGE     
       PZE     WRSXTW,,8
       PZE     WRSXTW,,0
FNASUM SYN     *-1
       BCI     4, ASCII ERROR   FAP(MEMO) 
       DUP     1,5
       CAL     01,6     
       DUP     1,5
       CAL     02,6     
       DUP     1,5
       CAL     03,6     
       DUP     1,3
       CAL     00,6     
CAL    SYN     *  
       DUP     5,3
       ARS     28 
       ARS     21 
       ARS     14 
       ARS     7  
       AXT     00,0     
       ARS     29 
       ARS     22 
       ARS     15 
ARS    SYN     *  
       DUP     5,3
       ALS     28 
       ALS     21 
       ALS     14 
       ALS     7  
       AXT     00,0     
       ALS     29 
       ALS     22 
       ALS     15 
ALS    SYN     *  
       DUP     1,5
       ORS     01,2     
       DUP     1,5
       ORS     02,2     
       DUP     1,5
       ORS     03,2     
       DUP     1,3
       ORS     00,2     
ORS    SYN     *  
TRA1   TRA     BUF3     
TRA2   TRA     FAIL     
TRA3   TRA     XCHG     
TSX1   TSX     NODE,4   
TSX2   TSX     CNODE,4  
TSX3   TSX     1,2
TSX4   TSX     START,4  
TSX5   TSX     FINISH,4 
TXL1   TXL     FAIL,1,**
TXL2   TXL     BUF3+3,1,-11   
TXH1   TXH     FAIL,1,**
NZT1   NZT     FFLG     
ZET1   ZET     0  
FFLG  
IFLG  
INTFLG
RFLG  
JFLG  
CPLFLG
RECLEV
BUFER 
BBASE 
INPUT 
IBASE 
ADDR1 
ADDR2 
ADDR3 
ZER   
B      OCT     200
V      OCT     200
E      OCT     200
A      OCT     43 
W      OCT     200
Q      OCT     100
R      OCT     200
GOOD  
NSCHA 
SUBFLG
COPY  
AFLG  
BFLG  
TRCFLG
FMODE 
FBDFLG
EXFLG 
SLCHR 
CFLG  
DFLG  
PAFLG 
PLFLG 
FILFLG
PMTFLG
ICFLG 
WHOAMI TIA     =HWHOAMI 
GETIME TIA     =HGETIME 
PERMIT TIA     =HALLOW  
FORBID TIA     =HFORBID 
GETCLS TIA     =HGETCLS 
SETCLS TIA     =HSETCLS 
GETCLC TIA     =HGETCLC 
SETCLC TIA     =HSETCLC 
NEXCOM TIA     =HNEXCOM 
CHNCOM TIA     =HCHNCOM 
GETCOM TIA     =HGETCOM 
SETBRK TIA     =HSETBRK 
GETBRK TIA     =HGETBRK 
GETMEM TIA     =HGETMEM 
DORMNT TIA     =HDORMNT 
RDFLXA TIA     =HRDFLXA 
WRFLX  TIA     =HWRFLX  
CLOSE  TIA     =HCLOSE  
OPEN   TIA     =HOPEN   
BUFFER TIA     =HBUFFER 
RDWAIT TIA     =HRDWAIT 
WRWAIT TIA     =HWRWAIT 
TRFILE TIA     =HTRFILE 
WRMESS TIA     =HWRMESS 
WRFLXA TIA     =HWRFLXA 
SETBCD TIA     =HSETBCD 
SETFUL TIA     =HSETFUL 
SETMEM TIA     =HSETMEM 
FILNAM OCT     00,0     
       EVEN 
AC     BSS     2  
BUF1   BSS     128
BUF2   BSS     128
BUF3   SYN     QED
BUF5   BSS     128
BUF6   BSS     302
BUF7   BSS     302
SCHARS BSS     10 
SYLBUF OCT     00,0,0,0 
       END
