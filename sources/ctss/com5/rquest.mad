          R .. THE REQUEST COMMAND 'RQUEST'.
          R .. REWRITTEN BY ROGER ROACH 3/25/68 TO BELIEVE IN THE LISTF
          R .. STAR '*' CONVENTIONS.
          R .. LAST MODIFIED 04/16/68 BY D.A. ANDERSON
          R
           NORMAL MODE IS INTEGER
           BOOLEAN CARRYI, SCANI, ONCE, FIRST, CLOSEI,GOOD
           DIMENSION COMBUF(20),LINE(15),ENTRY(9),BUFF(431)
           EQUIVALENCE (COMBUF(20),FENCE),(ENTRY(9),N1),(ENTRY(8),N2)
           EQUIVALENCE (ENTRY(1),N3),(ENTRY,N4)
           VECTOR VALUES CARRYI = 0B
           VECTOR VALUES CLOSEI = 0B
           VECTOR VALUES ONCE = 1B
           VECTOR VALUES FIRST = 1B
           VECTOR VALUES GOOD = 0B
           VECTOR VALUES ANSWER = $YES$
           VECTOR VALUES D = 2
           VECTOR VALUES FENCE = 777777777777K
           VECTOR VALUES B = 605757575757K
           VECTOR VALUES CB = 736057575757K
           VECTOR VALUES OR = 465157575757K
           VECTOR VALUES OUT = $OUTPUT$
           VECTOR VALUES RQ = $RQUEST$
           V'S TABLE = 14,$ PRINT$,$PRNDEL$,$SSPRNT$,$SSPRDL$,$BPUNCH$,
          1 $BPUDEL$,$DPUNCH$,$DPUDEL$,$7PUNCH$,$7PUDEL$,$DELETE$,
          2 $  PLOT$,$PLODEL$,$ CARRY$
          R
          R INITIALIZE THE COMMAND BUFFERS AND TEST FOR NO ARGUMENTS.
          R
           GCLS.(COMBUF,0)
           RQ = COMBUF(0)
           WHENEVER COMBUF(1).E. FENCE
             PRMESS.(COMBUF,$ CMMD NAME1 NAME2 ...ETC$)
             PRMESS.($  WHERE CMMD MAY BY ANY OF THE FOLLOWING -$)
             THROUGH LOOP0, FOR I=1,1,I.G.TABLE-1
             PRMESA.(TABLE(I),CB)
LOOP0        WHENEVER I.E. I/9*9, PRMESS.
             PRMESS.(OR,TABLE(TABLE))
             CHNCOM.(0)
           END OF CONDITIONAL
          R
          R
LOOP1      THROUGH LOOP1, FOR I=1,1,COMBUF(1).E.TABLE(I).OR.I.G.TABLE
           WHENEVER I .G. TABLE
             PRMESS.(COMBUF(1),$ IS NOT A RQUEST$)
             CHNCOM.(0)
           END OF CONDITIONAL
          R
          R
           WHOAMI.(LINE(3)...4)
           FERRTN.(IOERR)
           LINE(15) = COMBUF(1)
           FSTATE.(OUT,RQ,ENTRY(7)...8,FERR,FCODE)
           WHENEVER ENTRY(5).NE.1
             PRMESS.($THE RQUEST FILE IS BUSY.  RETYPE RQUEST LATER.$)
             CHNCOM.(0)
           OR WHENEVER ENTRY(6) .A. 1 .NE. 0
             CHFILE.(OUT,RQ,1,-0,-0)
             DELFIL.(OUT,RQ)
           END OF CONDITIONAL
          R
          R
OK         WHENEVER COMBUF(1) .E. $ CARRY$
             CARRYI = 1B
             D = 6
           END OF CONDITIONAL
          R
          R
           THROUGH LOOP2,FOR I = 2,D,COMBUF(I).E.FENCE.OR.I.G.18
           WHENEVER COMBUF(I+1).E.FENCE
             PRMESS.(COMBUF(I),$ IS AN INCOMPLETE REQUEST. $)
             TRANSFER TO GOHOME
           END OF CONDITIONAL
          R
          R
           MASK1 = NAMASK.(COMBUF(I))
           MASK2 = NAMASK.(COMBUF(I+1))
           WHENEVER MASK1.A.MASK2 .E. FENCE
             SCANI = 0B
             N1 = COMBUF(I)
             N2 = COMBUF(I+1)
             FSTATE.(N1,N2,ENTRY(7)...8,IOERR2,ERCODE)
           OTHERWISE
             SCANI = 1B
             WHENEVER ONCE
               ONCE = 0B
               UPDATE.
               SCNFIL.($U.F.D.(FILE)$)
             END OF CONDITIONAL
             ISCAN.(COMBUF(I),COMBUF(I+1))
             SCAN.(ENTRY(9)...7,NOPE)
           END OF CONDITIONAL
          R
          R
           TRANSFER TO FTEST
          R
          R
LOOP2      CONTINUE
GOHOME     WRTBUF.
EGRETS     WHENEVER CLOSEI, CLOSE.($ALL$,-0)
           RESETF.
           CHNCOM.(0)
          R
          R
FERR       WHENEVER FCODE .E. 3 , TRANSFER TO OK
IOERR      PRNTER.(61K)
           TRANSFER TO EGRETS
          R
          R
IOERR2     WHENEVER ERCODE .E. 3
NOPE         PRMESS.(COMBUF(I),B,COMBUF(I+1),$ NOT FOUND,$)
             W'R CARRYI .AND. (COMBUF(I+2).E.FENCE.OR.COMBUF(I+3).E.
          1    FENCE.OR.COMBUF(I+4).E.FENCE.OR.COMBUF(I+5).E.FENCE),D=20
             WHENEVER .NOT.SCANI, TRANSFER TO ASK
             TRANSFER TO LOOP2
           OTHERWISE
             PRNTER.(61K)
             TRANSFER TO LOOP2
           END OF CONDITIONAL
          R
          R
FTEST      WHENEVER (.NOT.SCANI.AND.ENTRY(6).A.20K.NE.0).OR.
          1  (SCANI.AND.(ENTRY(5).RS.28).A.20K.NE.0)
               PRMESA.(N1,B,N2,$ IS IN 'PRIVATE' MODE.$)
ASK            PRMESA.($DO YOU STILL WANT REQUEST:$)
               WHENEVER LINE.NE.$(FIB)$, RDFLXC.(ANSWER,6)
               WHENEVER ANSWER.NE.$YES$, TRANSFER TO NEXT
           END OF CONDITIONAL
           WHENEVER FIRST
             FIRST = 0B
             OPEN.($W$,OUT,RQ)
             BUFFER.(OUT,RQ,BUFF(431)...432)
             SETBUF.(LINE(14)...11,GOOD,WRCARD.,PANIC)
           END OF CONDITIONAL
           WHENEVER CARRYI
             ATTNAM.(ENTRY(3)...2)
             ENTRY(1) = N1
             ENTRY = N2
             THROUGH LOOP3, FOR J=1,1,J.G.4.OR.COMBUF(I+1+J).E.FENCE
             WHENEVER COMBUF(I+J+1).E.$     *$, TRANSFER TO LOOP3
             ENTRY(4-J) = COMBUF(I+1+J)
LOOP3        CONTINUE
             WHENEVER COMBUF(I+J+1) .E. FENCE, D = 20
             MASK3 = NAMASK.(N3)
             MASK4 = NAMASK.(N4)
             MASK3P = COM.(MASK3)
             MASK4P = COM.(MASK4)
             N3 = (N1.A.MASK3P).V.(N3.A.MASK3)
             N4 = (N2.A.MASK4P).V.(N4.A.MASK4)
             FILBUF.(ENTRY(9)...2,ENTRY(3)...4)
             WRTBUF.
           OTHERWISE
             FILBUF.(ENTRY(9)...2)
           END OF CONDITIONAL
          R
NEXT       WHENEVER SCANI
             SCAN.(ENTRY(9)...7,LOOP2)
             TRANSFER TO FTEST
           OTHERWISE
             TRANSFER TO LOOP2
           END OF CONDITIONAL
          R
PANIC      PRMESS.($MACHINE OR SYSTEM ERROR. IGNORING$,N1,N2)
           TRANSFER TO NEXT
          R
           INTERNAL FUNCTION
           ENTRY TO WRCARD.
          R
           WRWAIT.(OUT,RQ,0,LINE(15)...14)
           GOOD = 0B
           CLOSEI = 1B
           FUNCTION RETURN
          R
           END OF FUNCTION
          R
           END OF PROGRAM
