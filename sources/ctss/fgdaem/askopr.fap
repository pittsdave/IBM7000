* SUBROUTINE TO TALK TO OPERATOR AND CONVERSE UNTIL WE GET AN ANSWER
*     
*     
       ENTRY   ASKOPR   
*     
*     
ASKOPR SXA     ASK1,1   
       SXA     ASK2,2   
       SXA     ASK3,3   
       SXA     ASK4,4   
       SXA     ASK7,7   
       TSX     $GETA,7  
       STZ     DEMKEY   
       PXA     ,4 
       PAX     ,2 
       AXT     0,1
MORMES CAL     1,2
       SLW     MESS+1   
       TSX     CKOP,4   
       TXH     SNOOZE   
       CAL     2,2
       PDX     ,3 
       SXA     MESS,3   
       TSX     $PRCODE,4
MESS   PTH     ** 
       PTH     **,,**   
       TXI     *+1,1,2  
       TXI     MORMES,2,-2    
SNOOZE TXL     ABORT,1,0
       CAL     =15
       TSX     $SLEEP,4 
       SXD     *+1,1    
       TXI     *+1,2,** 
       TSX     $GETLSG,4
       AXT     0,1
NOMTCH CAL     1,2
       TSX     CKOP,4   
       PTH     REPTMS   
       CAL     2,2
       STA     TRAFER   
       PDX     ,3 
       TSX     CKOP,4   
       TXH     REPTMS   
       ZAC  
       PXA     ,3 
       SUB     KEYVAL   
       TZE     FOUND    
       TXI     *+1,1,2  
       TXI     NOMTCH,2,-2    
REPTMS TXL     ABORT,1,0
       SXD     *+1,1    
       TXI     MORMES-1,2,**  
*     
FOUND  EQU     *  
ASK1   AXT     **,1     
ASK2   AXT     **,2     
ASK3   AXT     **,3     
ASK4   AXT     **,4     
ASK7   AXT     **,7     
TRAFER TRA     ** 
*     
*     
ABORT  LXA     ASK4,4   
       LXA     ASK3,3   
       LXA     ASK2,2   
       LXA     ASK1,1   
       LXA     ASK7,7   
       TRA     1,4
*     
CKOP   ANA     =O700000000000 
       LAS     =O300000000000 
       TRA*    1,4
       TRA     2,4
       TRA*    1,4
*     
       INSERT  BCMEQB   
       END  
