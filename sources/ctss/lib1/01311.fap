*      FAP
*.01311  EXPONENTIATION SUBROUTINE  FIXED TO FIXED    
*      AUGUST 22,1962   
       REM
       REM     AC=BASE  
       REM     MQ=POWER 
       REM
       LBL     1311
       ENTRY   .01311   
       REM
.01311 XCA  
       TNZ     *+3
       CLA     AZ1
       TRA     1,4
       XCA  
       TZE     1,4           RETURN 0 FOR 0 BASE
       STO     BASE     
       XCA  
       TNZ     *+3
       CLA     AZ1           RETURN 1 FOR 
       TRA     1,4           ZERO EXPONENT
       TPL     *+3
       PXD     0,0
       TRA     1,4           NEGATIVE EXPONENT  
       STO     EXP
       CLA     AZ1
       STO     PX 
EF1    CLA     EXP
       LBT  
       TXL     ZERO     
       LDQ     PX 
       MPY     BASE     
       STQ     PX 
       CLA     EXP
ZERO   ARS     1  
       STO     EXP
       TZE     EXIT     
       LDQ     BASE     
       MPY     BASE     
       STQ     BASE     
       TXL     EF1
EXIT   CLA     PX 
       TRA     1,4
AZ1    PZE     1  
EXP    BSS     1  
PX     BSS     1  
BASE   BSS     1  
END    SYN     *  
       END  
