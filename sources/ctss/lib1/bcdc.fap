*      FAP
*      BCDEC   BCD TO DECIMAL INTEGER     
*       CONVERTS A BCD DECIMAL NUMBER INTO RIGHT JUSTIFIED INTEGE 
*              ARG1= BCD NUMBER, RESULT IN SAC  
* CALLING SEQUENCE
* FAP  TSX     BCDEC,4  
*      PZE     BCD WORD LOCATION    
*      STO     BINARY INTEGER RIGHT JUSTIFIED   
* MAD  A=BCDEC.(BCD WORD)     
* FORTRAN, NO CALL
*      LEADING BLANKS ARE ALLOWED IN THE BCD WORD.    
* COMPUTATION CENTER        LOUIS POUZIN  
*   OCTOBER 63    
       REM
       LBL     BCDEC
       ENTRY   BCDEC    
       REM
X4     SYN     *-2
       REM
BCDEC  SXD     X4,4     
       LDQ*    1,4
       STZ     A  
       AXT     6,4
B      PXA  
       LGL     3             BITS 8, 16, 32 IN AC     
       ANA     =1            ERASE BLANKS IF ANY
       ADM     A             ADD PREVIOUS TERM  
       LGL     3             8 TIMES + BITS 4, 2, 1   
       ADM     A             +1 TIME
       ADM     A             +1 TIME = 10 TIMES PREVIOUS RESULT   
       SLW     A             INTERMEDIATE RESULT
       TIX     B,4,1    
       LXD     X4,4     
       CLA     A  
       TRA     2,4
A      PZE  
       END  
