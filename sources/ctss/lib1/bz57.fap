* BZ57 RECONSTRUCTED FROM THE BINARY BY ROGER ROACH 8-01-67
*     
* PROGRAM TO PLACE LEADING NULL CHARACTERS IN PLACE OF ZEROS OR   
* BLANKS IN A WORD.     
*     
* CALLING SEQUENCE -    
*     NEWWD = BZ57.(WORD)     
*     
       NOLNK
       LBL     BZ57
       ENTRY   BZ57     
BZ57   SXA     XR4,4    
       LDQ*    1,4
       AXT     0,4
       ZAC  
LOOP   LGL     6  
       LAS     =H00000       TEST FOR A BLANK   
       TNZ     NBLANK        NOT A BLANK  
       TRA     *+2           A BLANK, REPLACE BY A NULL     
       TNZ     NBLANK        NOT A BLANK NOR A ZERO   
       CAL     =O57          A BLANK OR ZERO GETS REPLACED BY A NULL    
       LGR     6  
       RQL     6  
       TXI     *+1,4,6  
       TXL     LOOP,4,24
       TRA     NBLANK+1      TESTED ALL OF THE WORD   
NBLANK LGR     6  
       RQL     36,4     
XR4    AXT     **,4     
       XCA  
       TRA     2,4
       END  
