       DETAIL
*      OCABC   CONVERTS ADDRESS FIELD INTO A BCD OCTAL WORD 
*      OCDBC   CONVERTS DECREMENT FIELD INTO A BCD OCTAL WORD     
*      OCLBC   CONVERTS LEFT HALF WORD INTO A BCD OCTAL WORD
*      OCRBC   CONVERTS RIGHT HALF WORD INTO A BCD OCTAL WORD     
       LBL     OCBC
       ENTRY   OCABC    
       ENTRY   OCDBC    
       ENTRY   OCRBC    
       ENTRY   OCLBC    
X4     SYN     *-2
OCDBC  CAL*    1,4
       ARS     18            SET AEG IN ADDRESS 
       TRA     OCABC+1  
OCABC  CAL*    1,4
       ANA     =O77777       KEEP ADDRESS 
       LDQ     =H 00000      SET LEFT BLANK     
       STQ     A  
       TRA     E  
OCLBC  CAL*    1,4
       ARS     18            SET IN ADDRESS     
       TRA     OCRBC+1  
OCRBC  CAL*    1,4
       ANA     =O777777      KEEP RIGHT HALF    
       STZ     A             ERASE LEFT BLANKS  
E      LGR     18 
       SXD     X4,4     
       AXT     6,4
B      ALS     3             INSERT ZERO AND 3 BITS   
       LGL     3             IN EVERY 6 BITS GROUP    
       TIX     B,4,1         6 TIMES
       ORS     A             INSERT LEFT BLANK IF ANY 
       CLA     A             LOAD SAC     
       LXD     X4,4     
       TRA     2,4
A      PZE  
       END  
