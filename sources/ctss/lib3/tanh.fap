       DETAIL
*TANH  709 FORTRAN LIBRARY / FLOATING POINT HYBERBOLIC TANGENT / 9TNH   
*      AUGUST 22,1962   
*  THIS ROUTINE HAS BEEN REVISED TO ACCOMODATE MAD / MAY 1960     
       REM
*  THE CALLING SEQUENCE IS    
       REM
*       CALL  TANH                TSX  $TANH,4  
*       N=ARG IN THE AC           TXH  N  
*    FOR FORTRAN               FOR MAD    
       REM
       LBL     TANH
       ENTRY   TANH     
       REM
TANH   STO     F             MODE TEST--MAD OR FORTRAN
       CLA     =O300000000000                                       JSMI
       LDQ     1,4            .     
       TLQ     *+3            .  FORTRAN  
       CLA*    1,4            .  GET MAD ARGUMENT     
       STO     F              .  .  
       CLA     F  
       SSP  
       SUB     =.00034                                              JSMI
       TPL T5     
       CLA F                 TEST IF ARGUMENT LESS THAN .00034    
       TRA 1,4               IF SO LET TANH X EQUAL X 
T5     SUB     =O017000000000                                       JSMI
       TMI     T6 
       CLA     F                                                    JSMI
       TMI     MINUS                                                JSMI
       CLA     =1.                                                  JSMI
       TRA     1,4           SET TANH (X) = 1   
MINUS  CLS     =1.                                                  JSMI
       TRA     1,4                                                  JSMI
 T6    CLA F
       LRS 27     
       SSM  
       ADD     =O242                                                JSMI
       STA SH1    
       MPY     =1.4426950409B1                                      JSMI
 SH1   LRS                   SEPARATE INTEGER AND FRACTION  
       ALS 27     
       STO M
       LLS 2
       TZE T2                IF ARGUMENT LESS THAN .25 TRANSFER   
       LRS 6                 IF GREATER COMPUTE E TO THE X  
       STQ F
       MPY F                 COMPUTE FRACTION SQUARED 
       STO SQ                COMPUTE CONTINUED FRACTION     
       ADD     =87.417497202B8                                      JSMI
       STO T                 8,27   
       CLA     =617.9722695B12                                      JSMI
       DVP T                 8,27 4,31    
       STQ T                 4,31   
       LDQ SQ                8,27   
       MPY     =.03465735903B0                                      JSMI
       LLS 4                 4,31   
       SUB T                 4,31   
       ADD     =9.9545957821B4                                      JSMI
       SUB F                 4,31   
       STO T                 4,31   
       CLA F                 5,31 NUMERATOR EQUALS 2F 
       LDQ     =1.                                                  JSMI
       VDP     T,0,27   
       XCA  
       ADD     =1.                                                  JSMI
 T1    ADD M
       STO M                 COMPUTE TANH X FROM E TO THE X 
       CLA     =1.                                                  JSMI
       FDP M
       STQ F
       CLA M
       FAD F
       STO T
       CLA M
       FSB F
 T10   FDP T
       XCA  
       TRA 1,4               RETURN 
 T2    LDQ F                 COMPUTE TANH 4LOGE IF X SMALL  
       FMP     =5.7707801636                                        JSMI
       STO F
       XCA  
       FMP F
       STO SQ     
       FAD     =349.6699888                                         JSMI
       STO T
       CLA     =14.1384114018                                       JSMI
       FDP T
       XCA  
       FAD     =.01732867951                                        JSMI
       XCA  
       FMP SQ     
       FAD     =5.7707801636                                        JSMI
   T12 STO T
       CLA F
       TRA T10    
       SPACE   4
*                            ERASABLES    
       REM
       COMMON  -206     
T      COMMON  1  
SQ     COMMON  1  
F      COMMON  1  
M      COMMON  1  
       END                                                          JSMI
